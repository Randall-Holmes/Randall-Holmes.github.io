
00  p   EB   prop

% (p:prop)
# declare a proposition variable p

p     q    EB  prop

% (p:prop,q:prop)
# declare a proposition variable q,
# and create a context (p,q)

q     and  PN  prop

%1. and(p:prop,q:prop) = PN : prop
# declare a primitive operation
# and which takes arguments (p,q)
# and returns a proposition.

q     pp    EB   p

% (p:prop,q:prop,pp:p)
# declare a variable pp representing
# a proof of p (p is a sort, with
# its inhabitants being its proofs.
# The context pp is (p,q,pp)

pp   qq    EB   q

% (p:prop,q:prop,pp:p,qq:q)
# declare a variable qq representing
# a proof of q.
# the context qq is (p,q,pp,qq)

qq   conj  PN  and(p,q)

%2. conj(p:prop,q:prop,pp:p,qq:q) = PN : and(p,q)
# declare a primitive operation
# conj which takes arguments
# (p,q,pp,qq) and returns
# a proof of and(p,q)
# [I could have just written
# and instead of and(p,q),
# but that would seem opaque]
# this is the rule of conjunction

q    rr    EB   and(p,q)

% (p:prop,q:prop,rr:and(p,q))
# in the context q=(p,q)
# declare a variable rr of type
# and(p,q) (a proof of p and q).
#the context rr = (p,q,rr)

rr   simp1  PN  p

%3. simp1(p:prop,q:prop,rr:and(p,q)) = PN : p

rr   simp2  PN  q

%4. simp2(p:prop,q:prop,rr:and(p,q)) = PN : q
# declare primitive notions simpl
# and simp2, both taking 
# argument lists (p,q,rr)
# and returning proofs of p
# and q respectively:
# the rules of simplification.

q    implies  PN prop

%5. implies(p:prop,q:prop) = PN : prop
# declare the primitive
# construction if(p,q).

pp   pthenq EB  implies(p,q)

% (p:prop,q:prop,pp:p,pthenq:implies(p,q))
# add a variable representing
# a proof of if(p,q)

pthenq  mp  PN  q

%6. mp(p:prop,q:prop,pp:p,pthenq:implies(p,q)) = PN : q
# This is the rule of modus
# ponens.  You should be able
# to provide your own analysis.

q    ded0   EB   [pp:p]q

% (p:prop,q:prop,ded0:[pp:p]q)
#  we define a variable ded0
# which is of type a function from
# proofs of p to proofs of q.
# The expression [pp:p]q
# represents this sort.

ded0  ded   PN  implies(p,q)

%7. ded(p:prop,q:prop,ded0:[pp:p]q) = PN : implies(p,q)
# this is the rule of deduction:
# from the context (p,q,ded0)
# where p and q are propositions
# and ded0 is a function from proofs
# of p to proofs of q, produce
# a proof of if(p,q)
# (again, the sort here could
# have been written just if,
# but that would be 
# mysterious).
# Now we prove 
# that and(p,q) implies
# and(q,p)

q   hyp1  EB  and(p,q)

% (p:prop,q:prop,hyp1:and(p,q))
# introduce a variable hyp1,
# a proof of and(p,q)
# [the context being
# (p,q, hyp1)]

hyp1  line1 simp1(hyp1)  p

%8. line1(p:prop,q:prop,hyp1:and(p,q)) = simp1(p,q,hyp1) : p
# present proof of p

hyp1  line2  simp2(hyp1)  q

%9. line2(p:prop,q:prop,hyp1:and(p,q)) = simp2(p,q,hyp1) : q
# present proof of q

hyp1  line3   conj(q,p,line2,line1)  and(q,p)

%10. line3(p:prop,q:prop,hyp1:and(p,q)) = conj(q,p,line2(p,q,hyp1),line1(p,q,hyp1)) : and(q,p)
# present proof of and(q,p)

q   thm  ded(and(p,q),and(q,p),[hyp1:and(p,q)]line3(hyp1))  implies(and(p,q),and(q,p))

%11. thm(p:prop,q:prop) = ded(and(p,q),and(q,p),[hyp1:and(p,q)]line3(p,q,hyp1)) : implies(and(p,q),and(q,p))
# Now present the function which actually
# witnesses this proof.  The environment is
# now just p and q.  the object thm
# is defined as the result of applying
# the primitive construction  ded
# to the function ([hyp1:and(p,q)]line3(hyp1)
# thus obtaining a proof of if(p,q)
# We are too used to our approach in Lestrade
# (or a more conservative older approach in Automath).
# The operation if does not have to be declared as a primitive.  The 
# following alternative declarations work (they depend
# on the subtyping).

q  If   [pp:p]q  [pp:p]prop

%12. If(p:prop,q:prop) = [pp:p]q : [pp:p]prop
# the computed type of [pp:p]q is actually
# [pp:p]prop, but this is regarded as
# a subtype of prop.

pp Pthenq  EB  If(p,q)

% (p:prop,q:prop,pp:p,Pthenq:If(p,q))
# introduce a proof of If(p,q)

Pthenq Mp <pp>Pthenq  q

%13. Mp(p:prop,q:prop,pp:p,Pthenq:If(p,q)) = <pp>Pthenq : q
# the environment is
# (p,q,pp,Pthenq):
# apply Pthenq to pp
# and a proof of q results.
# No declaration of ded is needed
# at all:  a function such as ded0 above
# just *is* a proof of If(p,q).

q Ded0 EB [pp:p]q

% (p:prop,q:prop,Ded0:[pp:p]q)

Ded0 Ded Ded0 If(p,q)

%14. Ded(p:prop,q:prop,Ded0:[pp:p]q) = Ded0 : If(p,q)
# the Automath lines above express this thought.
# Notice that the type checker needs to be able
# to use definitional expansion (and the bit
# of subtyping that we have) to check that
# types are actually the same.  Equivalence
# of function or function type terms
# [x:s]T will also need to take into account
# equivalence up to renaming of bound
# variables.
# test of effect of extra declarations.

00 Nat PN type

%15. Nat = PN : type

00 n EB Nat

% (n:Nat)

n Succ PN Nat

%16. Succ(n:Nat) = PN : Nat
# These declarations demonstrate the automatic
# additional declarations of function constants 
# if the experimental feature is turned on.
# functions representing the constructor itself
# and its type are declared if legal:  this supports
# a style without user entered bound variables.

n Addtwo Succ(Succ(n)) Nat

%17. Addtwo(n:Nat) = Succ(Succ(n)) : Nat

%18. Addtwo@ = [n:Nat]Nat : [n:Nat]type

%19. Addtwo! = [n:Nat]Succ(Succ(n)) : Addtwo@

n Nattonat Nat type

%20. Nattonat(n:Nat) = Nat : type

%21. Nattonat@ = [n:Nat]Nat : [n:Nat]type
# I demonstrate declaration of an operation on functions which doesn't involve
# any use of bound variables.

n f EB Addtwo@

% (n:Nat,f:Addtwo@)

f Funsucc Succ(<n>f) Nat

%22. Funsucc(n:Nat,f:Addtwo@) = Succ(<n>f) : Nat

%23. Funsucc@ = [n:Nat][f:Addtwo@]Nat : [n:Nat][f:Addtwo@]type

%24. Funsucc! = [n:Nat][f:Addtwo@]Succ(<n>f) : Funsucc@
quit
