@[p:cut][q:cut][r:cut]
3pl13:=tr3is(cut,pl(p,pl(q,r)),pl(pl(q,r),p),pl(pl(r,q),p),pl(r,pl(q,p)),compl(p,pl(q,r)),ispl1(pl(q,r),pl(r,q),p,compl(q,r)),asspl1(r,q,p)):is(pl(p,pl(q,r)),pl(r,pl(q,p)))
[s:cut]
4pl24:=tr3is(cut,pl(pl(p,q),pl(r,s)),pl(p,pl(q,pl(r,s))),pl(p,pl(s,pl(r,q))),pl(pl(p,s),pl(r,q)),asspl1(p,q,pl(r,s)),ispl2(pl(q,pl(r,s)),pl(s,pl(r,q)),p,3pl13(q,r,s)),asspl2(p,s,pl(r,q))):is(pl(pl(p,q),pl(r,s)),pl(pl(p,s),pl(r,q)))
r@3pl12:=tr3is(cut,pl(p,pl(q,r)),pl(pl(p,q),r),pl(pl(q,p),r),pl(q,pl(p,r)),asspl2(p,q,r),ispl1(pl(p,q),pl(q,p),r,compl(p,q)),asspl1(q,p,r)):is(pl(p,pl(q,r)),pl(q,pl(p,r)))
s@4pl23:=tr3is(cut,pl(pl(p,q),pl(r,s)),pl(p,pl(q,pl(r,s))),pl(p,pl(r,pl(q,s))),pl(pl(p,r),pl(q,s)),asspl1(p,q,pl(r,s)),ispl2(pl(q,pl(r,s)),pl(r,pl(q,s)),p,3pl12(q,r,s)),asspl2(p,r,pl(q,s))):is(pl(pl(p,q),pl(r,s)),pl(pl(p,r),pl(q,s)))
r@3pl23:=tr3is(cut,pl(pl(p,q),r),pl(p,pl(q,r)),pl(p,pl(r,q)),pl(pl(p,r),q),asspl1(p,q,r),ispl2(pl(q,r),pl(r,q),p,compl(q,r)),asspl2(p,r,q)):is(pl(pl(p,q),r),pl(pl(p,r),q))
p@a2isapa:=tris(cut,ts(p,pl(1rp,1rp)),pl(ts(p,1rp),ts(p,1rp)),pl(p,p),disttp2(p,1rp,1rp),ispl12(ts(p,1rp),p,ts(p,1rp),p,satz151(p),satz151(p))):is(ts(p,pl(1rp,1rp)),pl(p,p))
@dif:=pair1type(cut):'type'
[a1:cut][a2:cut]
df:=pair1(cut,a1,a2):dif
@[a:dif]
stm:=first1(cut,a):cut
std:=second1(cut,a):cut
a2@stmis:=first1is1(cut,a1,a2):is(stm(df(a1,a2)),a1)
isstm:=first1is2(cut,a1,a2):is(a1,stm(df(a1,a2)))
stdis:=second1is1(cut,a1,a2):is(std(df(a1,a2)),a2)
isstd:=second1is2(cut,a1,a2):is(a2,std(df(a1,a2)))
a@1a:=stm(a):cut
2a:=std(a):cut
dfis:=pair1is1(cut,a):is"e"(dif,df(1a,2a),a)
isdf:=pair1is2(cut,a):is"e"(dif,a,df(1a,2a))
a2@[b1:cut][b2:cut]
12issmsd:=ispl12(a1,stm(df(a1,a2)),b2,std(df(b1,b2)),isstm(a1,a2),isstd(b1,b2)):is(pl(a1,b2),pl(stm(df(a1,a2)),std(df(b1,b2))))
smsdis12:=symis(cut,pl(a1,b2),pl(stm(df(a1,a2)),std(df(b1,b2))),12issmsd):is(pl(stm(df(a1,a2)),std(df(b1,b2))),pl(a1,b2))
a@[r1:cut][r2:cut]
1sdissmsd:=ispl1(r1,stm(df(r1,r2)),2a,isstm(r1,r2)):is(pl(r1,2a),pl(stm(df(r1,r2)),2a))
smsdis1sd:=symis(cut,pl(r1,2a),pl(stm(df(r1,r2)),2a),1sdissmsd):is(pl(stm(df(r1,r2)),2a),pl(r1,2a))
sm2issmsd:=ispl2(r2,std(df(r1,r2)),1a,isstd(r1,r2)):is(pl(1a,r2),pl(1a,std(df(r1,r2))))
smsdissm2:=symis(cut,pl(1a,r2),pl(1a,std(df(r1,r2))),sm2issmsd):is(pl(1a,std(df(r1,r2))),pl(1a,r2))
a2@[r:cut][i:is(a1,r)]
issm:=isf(cut,dif,[t:cut]df(t,a2),a1,r,i):is"e"(dif,df(a1,a2),df(r,a2))
r@[i:is(a2,r)]
issd:=isf(cut,dif,[t:cut]df(a1,t),a2,r,i):is"e"(dif,df(a1,a2),df(a1,r))
b2@[i:is(a1,b1)][j:is(a2,b2)]
issmsd:=tris(dif,df(a1,a2),df(b1,a2),df(b1,b2),issm(a1,a2,b1,i),issd(b1,a2,b2,j)):is"e"(dif,df(a1,a2),df(b1,b2))
a@[b:dif]
1b:=stm(b):cut
2b:=std(b):cut
eq:=is(pl(1a,2b),pl(1b,2a)):'prop'
b2@[i:is(pl(a1,b2),pl(b1,a2))]
eqi12:=tr3is(cut,pl(stm(df(a1,a2)),std(df(b1,b2))),pl(a1,b2),pl(b1,a2),pl(stm(df(b1,b2)),std(df(a1,a2))),smsdis12(a1,a2,b1,b2),i,12issmsd(b1,b2,a1,a2)):eq(df(a1,a2),df(b1,b2))
r2@[i:is(pl(1a,r2),pl(r1,2a))]
eqi1:=isp(dif,[x:dif]eq(x,df(r1,r2)),df(1a,2a),a,eqi12(1a,2a,r1,r2,i),dfis):eq(a,df(r1,r2))
r2@[i:is(pl(r1,2a),pl(1a,r2))]
eqi2:=isp(dif,[x:dif]eq(df(r1,r2),x),df(1a,2a),a,eqi12(r1,r2,1a,2a,i),dfis):eq(df(r1,r2),a)
b2@[e:eq(df(a1,a2),df(b1,b2))]
eqe12:=tr3is(cut,pl(a1,b2),pl(stm(df(a1,a2)),std(df(b1,b2))),pl(stm(df(b1,b2)),std(df(a1,a2))),pl(b1,a2),12issmsd(a1,a2,b1,b2),e,smsdis12(b1,b2,a1,a2)):is(pl(a1,b2),pl(b1,a2))
a@satzd163:=refis(cut,pl(1a,2a)):eq(a,a)
refeq:=satzd163:eq(a,a)
b@[i:is"e"(dif,a,b)]
refeq1:=isp(dif,[x:dif]eq(a,x),a,b,refeq,i):eq(a,b)
refeq2:=isp(dif,[x:dif]eq(x,a),a,b,refeq,i):eq(b,a)
b2@[i:is(a1,b1)][j:is(a2,b2)]
eqsmsd:=refeq1(df(a1,a2),df(b1,b2),issmsd(i,j)):eq(df(a1,a2),df(b1,b2))
r@[i:is(a1,r)]
eqsm:=refeq1(df(a1,a2),df(r,a2),issm(i)):eq(df(a1,a2),df(r,a2))
r@[i:is(a2,r)]
eqsd:=refeq1(df(a1,a2),df(a1,r),issd(i)):eq(df(a1,a2),df(a1,r))
b@[e:eq(a,b)]
satzd164:=symis(cut,pl(1a,2b),pl(1b,2a),e):eq(b,a)
symeq:=satzd164:eq(b,a)
b@[c:dif]
1c:=stm(c):cut
2c:=std(c):cut
[e:eq(a,b)][f:eq(b,c)]
+1d165
t1:=ispl12(pl(1a,2b),pl(1b,2a),pl(1b,2c),pl(1c,2b),e,f):is(pl(pl(1a,2b),pl(1b,2c)),pl(pl(1b,2a),pl(1c,2b)))
t2:=tr4is(cut,pl(pl(1a,2c),pl(1b,2b)),pl(pl(1a,2b),pl(1b,2c)),pl(pl(1b,2a),pl(1c,2b)),pl(pl(1c,2b),pl(1b,2a)),pl(pl(1c,2a),pl(1b,2b)),4pl24(1a,2c,1b,2b),t1,compl(pl(1b,2a),pl(1c,2b)),4pl24(1c,2b,1b,2a)):is(pl(pl(1a,2c),pl(1b,2b)),pl(pl(1c,2a),pl(1b,2b)))
-1d165
satzd165:=satz136b(pl(1a,2c),pl(1c,2a),pl(1b,2b),t2".1d165"):eq(a,c)
treq:=satzd165:eq(a,c)
c@[e:eq(c,a)][f:eq(c,b)]
treq1:=treq(a,c,b,symeq(c,a,e),f):eq(a,b)
c@[e:eq(a,c)][f:eq(b,c)]
treq2:=treq(a,c,b,e,symeq(b,c,f)):eq(a,b)
c@[d:dif][e1:eq(a,b)][e2:eq(b,c)][e3:eq(c,d)]
tr3eq:=treq(a,b,d,e1,treq(b,c,d,e2,e3)):eq(a,d)
d@[e:dif][e1:eq(a,b)][e2:eq(b,c)][e3:eq(c,d)][e4:eq(d,e)]
tr4eq:=tr3eq(a,b,c,e,e1,e2,treq(c,d,e,e3,e4)):eq(a,e)
a@posd:=more(1a,2a):'prop'
zero:=is(1a,2a):'prop'
negd:=less(1a,2a):'prop'
a2@[m:more(a1,a2)]
posdi:=ismore12(a1,stm(df(a1,a2)),a2,std(df(a1,a2)),isstm(a1,a2),isstd(a1,a2),m):posd(df(a1,a2))
a2@[i:is(a1,a2)]
zeroi:=tr3is(cut,stm(df(a1,a2)),a1,a2,std(df(a1,a2)),stmis(a1,a2),i,isstd(a1,a2)):zero(df(a1,a2))
a2@[l:less(a1,a2)]
negdi:=isless12(a1,stm(df(a1,a2)),a2,std(df(a1,a2)),isstm(a1,a2),isstd(a1,a2),l):negd(df(a1,a2))
a@axrde:=satz123b(1a,2a):ec3(zero(a),posd(a),negd(a))
axrdo:=satz123a(1a,2a):or3(zero(a),posd(a),negd(a))
axrd:=orec3i(zero(a),posd(a),negd(a),axrdo,axrde):orec3(zero(a),posd(a),negd(a))
[p:'prop'][p1:[t:posd(a)]p][p2:[t:zero(a)]p][p3:[t:negd(a)]p]
rappd:=or3app(zero(a),posd(a),negd(a),p,axrdo,p2,p1,p3):p
a@[p:posd(a)]
pnot0d:=ec3e21(zero(a),posd(a),negd(a),axrde,p):not(zero(a))
pnotnd:=ec3e23(zero(a),posd(a),negd(a),axrde,p):not(negd(a))
a@[z:zero(a)]
0notpd:=ec3e12(zero(a),posd(a),negd(a),axrde,z):not(posd(a))
0notnd:=ec3e13(zero(a),posd(a),negd(a),axrde,z):not(negd(a))
a@[n:negd(a)]
nnotpd:=ec3e32(zero(a),posd(a),negd(a),axrde,n):not(posd(a))
nnot0d:=ec3e31(zero(a),posd(a),negd(a),axrde,n):not(zero(a))
b@[e:eq(a,b)][p:posd(a)]
+iv1d
t1:=ismore12(pl(1a,2b),pl(1b,2a),pl(2a,2b),pl(2b,2a),e,compl(2a,2b),satz135a(1a,2a,2b,p)):more(pl(1b,2a),pl(2b,2a))
-iv1d
eqposd:=satz136a(1b,2b,2a,t1".iv1d"):posd(b)
e@[z:zero(a)]
+*iv1d
z@t2:=tr3is(cut,pl(1b,2a),pl(1a,2b),pl(2a,2b),pl(2b,2a),symeq(a,b,e),ispl1(1a,2a,2b,z),compl(2a,2b)):is(pl(1b,2a),pl(2b,2a))
-iv1d
z@eqzero:=satz136b(1b,2b,2a,t2".iv1d"):zero(b)
e@[n:negd(a)]
+*iv1d
n@t3:=isless12(pl(1a,2b),pl(1b,2a),pl(2a,2b),pl(2b,2a),e,compl(2a,2b),satz135c(1a,2a,2b,n)):less(pl(1b,2a),pl(2b,2a))
-iv1d
n@eqnegd:=satz136c(1b,2b,2a,t3".iv1d"):negd(b)
b@[z:zero(a)][y:zero(b)]
zeroeq:=tris(cut,pl(1a,2b),pl(2a,1b),pl(1b,2a),ispl12(1a,2a,2b,1b,z,symis(cut,1b,2b,y)),compl(2a,1b)):eq(a,b)
@[r:cut]
pdofrp:=df(pl(r,1rp),1rp):dif
ndofrp:=df(1rp,pl(r,1rp)):dif
[s:cut][i:is(r,s)]
isrpepd:=refeq1(pdofrp(r),pdofrp(s),isf(cut,dif,[x:cut]pdofrp(x),r,s,i)):eq(pdofrp(r),pdofrp(s))
isrpend:=refeq1(ndofrp(r),ndofrp(s),isf(cut,dif,[x:cut]ndofrp(x),r,s,i)):eq(ndofrp(r),ndofrp(s))
s@[e:eq(pdofrp(r),pdofrp(s))]
+*iv1d
e@t4:=satz136b(pl(r,1rp),pl(s,1rp),1rp,eqe12(pl(r,1rp),1rp,pl(s,1rp),1rp,e)):is(pl(r,1rp),pl(s,1rp))
-iv1d
e@isrpipd:=satz136b(r,s,1rp,t4".iv1d"):is(r,s)
s@[e:eq(ndofrp(r),ndofrp(s))]
+*iv1d
e@t5:=satz136e(pl(s,1rp),pl(r,1rp),1rp,eqe12(1rp,pl(r,1rp),1rp,pl(s,1rp),e)):is(pl(s,1rp),pl(r,1rp))
-iv1d
e@isrpind:=symis(cut,s,r,satz136b(s,r,1rp,t5".iv1d")):is(r,s)
r@posdirp:=posdi(pl(r,1rp),1rp,ismore1(pl(1rp,r),pl(r,1rp),1rp,compl(1rp,r),satz133(1rp,r))):posd(pdofrp(r))
negdirp:=negdi(1rp,pl(r,1rp),isless2(pl(1rp,r),pl(r,1rp),1rp,compl(1rp,r),satz133a(1rp,r))):negd(ndofrp(r))
a@[p:posd(a)]
rpofpd:=mn(1a,2a,p):cut
+*iv1d
p@t6:=tr4is(cut,pl(1a,1rp),pl(pl(rpofpd,2a),1rp),pl(rpofpd,pl(2a,1rp)),pl(rpofpd,pl(1rp,2a)),pl(pl(rpofpd,1rp),2a),ispl1(1a,pl(rpofpd,2a),1rp,satz140f(1a,2a,p)),asspl1(rpofpd,2a,1rp),ispl2(pl(2a,1rp),pl(1rp,2a),rpofpd,compl(2a,1rp)),asspl2(rpofpd,1rp,2a)):is(pl(1a,1rp),pl(pl(rpofpd,1rp),2a))
-iv1d
p@eqpdrp1:=eqi1(a,pl(rpofpd,1rp),1rp,t6".iv1d"):eq(a,pdofrp(rpofpd(a,p)))
eqpdrp2:=symeq(a,pdofrp(rpofpd(a,p)),eqpdrp1):eq(pdofrp(rpofpd(a,p)),a)
a@[n:negd(a)]
rpofnd:=mn(2a,1a,satz122(1a,2a,n)):cut
+*iv1d
n@t7:=tr3is(cut,pl(1a,pl(rpofnd,1rp)),pl(pl(1a,rpofnd),1rp),pl(2a,1rp),pl(1rp,2a),asspl2(1a,rpofnd,1rp),ispl1(pl(1a,rpofnd),2a,1rp,satz140c(2a,1a,satz122(1a,2a,n))),compl(2a,1rp)):is(pl(1a,pl(rpofnd,1rp)),pl(1rp,2a))
-iv1d
n@eqndrp1:=eqi1(a,1rp,pl(rpofnd,1rp),t7".iv1d"):eq(a,ndofrp(rpofnd(a,n)))
eqndrp2:=symeq(a,ndofrp(rpofnd(a,n)),eqndrp1):eq(ndofrp(rpofnd(a,n)),a)
@[h:dif][p:posd(h)][k:dif][q:posd(k)][e:eq(h,k)]
+*iv1d
e@t8:=tr3eq(pdofrp(rpofpd(h,p)),h,k,pdofrp(rpofpd(k,q)),eqpdrp2(h,p),e,eqpdrp1(k,q)):eq(pdofrp(rpofpd(h,p)),pdofrp(rpofpd(k,q)))
-iv1d
e@eqpderp:=isrpipd(rpofpd(h,p),rpofpd(k,q),t8".iv1d"):is(rpofpd(h,p),rpofpd(k,q))
q@[i:is(rpofpd(h,p),rpofpd(k,q))]
+*iv1d
i@t9:=isrpepd(rpofpd(h,p),rpofpd(k,q),i):eq(pdofrp(rpofpd(h,p)),pdofrp(rpofpd(k,q)))
-iv1d
i@eqpdirp:=tr3eq(h,pdofrp(rpofpd(h,p)),pdofrp(rpofpd(k,q)),k,eqpdrp1(h,p),t9".iv1d",eqpdrp2(k,q)):eq(h,k)
h@[n:negd(h)][k:dif][o:negd(k)][e:eq(h,k)]
+*iv1d
e@t10:=tr3eq(ndofrp(rpofnd(h,n)),h,k,ndofrp(rpofnd(k,o)),eqndrp2(h,n),e,eqndrp1(k,o)):eq(ndofrp(rpofnd(h,n)),ndofrp(rpofnd(k,o)))
-iv1d
e@eqnderp:=isrpind(rpofnd(h,n),rpofnd(k,o),t10".iv1d"):is(rpofnd(h,n),rpofnd(k,o))
o@[i:is(rpofnd(h,n),rpofnd(k,o))]
+*iv1d
i@t11:=isrpend(rpofnd(h,n),rpofnd(k,o),i):eq(ndofrp(rpofnd(h,n)),ndofrp(rpofnd(k,o)))
-iv1d
i@eqndirp:=tr3eq(h,ndofrp(rpofnd(h,n)),ndofrp(rpofnd(k,o)),k,eqndrp1(h,n),t11".iv1d",eqndrp2(k,o)):eq(h,k)
@[r:cut]
+*iv1d
r@t12:=eqpdrp1(pdofrp(r),posdirp(r)):eq(pdofrp(r),pdofrp(rpofpd(pdofrp(r),posdirp(r))))
-iv1d
r@isrppd1:=isrpipd(r,rpofpd(pdofrp(r),posdirp(r)),t12".iv1d"):is(r,rpofpd(pdofrp(r),posdirp(r)))
isrppd2:=symis(cut,r,rpofpd(pdofrp(r),posdirp(r)),isrppd1):is(rpofpd(pdofrp(r),posdirp(r)),r)
+*iv1d
r@t13:=eqndrp1(ndofrp(r),negdirp(r)):eq(ndofrp(r),ndofrp(rpofnd(ndofrp(r),negdirp(r))))
-iv1d
r@isrpnd1:=isrpind(r,rpofnd(ndofrp(r),negdirp(r)),t13".iv1d"):is(r,rpofnd(ndofrp(r),negdirp(r)))
isrpnd2:=symis(cut,r,rpofnd(ndofrp(r),negdirp(r)),isrpnd1):is(rpofnd(ndofrp(r),negdirp(r)),r)
a2@[r:cut]
lemmad1:=eqi12(a1,a2,pl(a1,r),pl(a2,r),tris(cut,pl(a1,pl(a2,r)),pl(a1,pl(r,a2)),pl(pl(a1,r),a2),ispl2(pl(a2,r),pl(r,a2),a1,compl(a2,r)),asspl2(a1,r,a2))):eq(df(a1,a2),df(pl(a1,r),pl(a2,r)))
lemmad2:=symeq(df(a1,a2),df(pl(a1,r),pl(a2,r)),lemmad1):eq(df(pl(a1,r),pl(a2,r)),df(a1,a2))
a@[r:cut]
lemmad3:=treq(a,df(1a,2a),df(pl(1a,r),pl(2a,r)),refeq1(a,df(1a,2a),isdf),lemmad1(1a,2a,r)):eq(a,df(pl(1a,r),pl(2a,r)))
lemmad4:=symeq(a,df(pl(1a,r),pl(2a,r)),lemmad3):eq(df(pl(1a,r),pl(2a,r)),a)
a@absd:=ite(negd(a),dif,df(2a,1a),a):dif
[n:negd(a)]
absnd:=refeq1(absd(a),df(2a,1a),itet(negd(a),dif,df(2a,1a),a,n)):eq(absd(a),df(2a,1a))
a@[n:not(negd(a))]
absnnd:=refeq1(absd(a),a,itef(negd(a),dif,df(2a,1a),a,n)):eq(absd(a),a)
a2@[l:less(a1,a2)]
absdeql:=treq(absd(df(a1,a2)),df(std(df(a1,a2)),stm(df(a1,a2))),df(a2,a1),absnd(df(a1,a2),negdi(a1,a2,l)),eqsmsd(std(df(a1,a2)),stm(df(a1,a2)),a2,a1,stdis(a1,a2),stmis(a1,a2))):eq(absd(df(a1,a2)),df(a2,a1))
a2@[m:moreis(a1,a2)]
absdeqm:=absnnd(df(a1,a2),th3"l.imp"(negd(df(a1,a2)),less(a1,a2),satz123c(a1,a2,m),[t:negd(df(a1,a2))]isless12(stm(df(a1,a2)),a1,std(df(a1,a2)),a2,stmis(a1,a2),stdis(a1,a2),t))):eq(absd(df(a1,a2)),df(a1,a2))
b@[e:eq(a,b)]
+iv2d
[n:negd(a)]
t1:=tr3is(cut,pl(2a,1b),pl(1b,2a),pl(1a,2b),pl(2b,1a),compl(2a,1b),symeq(a,b,e),compl(1a,2b)):is(pl(2a,1b),pl(2b,1a))
t2:=tr3eq(absd(a),df(2a,1a),df(2b,1b),absd(b),absnd(a,n),eqi12(2a,1a,2b,1b,t1),symeq(absd(b),df(2b,1b),absnd(b,eqnegd(a,b,e,n)))):eq(absd(a),absd(b))
e@[n:not(negd(a))]
t3:=tr3eq(absd(a),a,b,absd(b),absnnd(a,n),e,symeq(absd(b),b,absnnd(b,th3"l.imp"(negd(b),negd(a),n,[t:negd(b)]eqnegd(b,a,symeq(a,b,e),t))))):eq(absd(a),absd(b))
-iv2d
eqabsd:=th1"l.imp"(negd(a),eq(absd(a),absd(b)),[t:negd(a)]t2".iv2d"(t),[t:not(negd(a))]t3".iv2d"(t)):eq(absd(a),absd(b))
a@[p:posd(a)]
satzd166a:=eqposd(a,absd(a),symeq(absd(a),a,absnnd(a,pnotnd(a,p))),p):posd(absd(a))
a@[n:negd(a)]
+2d166
t1:=posdi(2a,1a,satz122(1a,2a,n)):posd(df(2a,1a))
-2d166
satzd166b:=eqposd(df(2a,1a),absd(a),symeq(absd(a),df(2a,1a),absnd(a,n)),t1".2d166"):posd(absd(a))
b@[p:posd(a)][q:posd(b)][e:eq(absd(a),absd(b))]
satzd166c:=tr3eq(a,absd(a),absd(b),b,symeq(absd(a),a,absnnd(a,pnotnd(a,p))),e,absnnd(b,pnotnd(b,q))):eq(a,b)
b@[n:negd(a)][o:negd(b)][e:eq(absd(a),absd(b))]
+*2d166
e@t2:=tr3eq(df(2a,1a),absd(a),absd(b),df(2b,1b),symeq(absd(a),df(2a,1a),absnd(a,n)),e,absnd(b,o)):eq(df(2a,1a),df(2b,1b))
-2d166
e@satzd166d:=tr3is(cut,pl(1a,2b),pl(2b,1a),pl(2a,1b),pl(1b,2a),compl(1a,2b),symis(cut,pl(2a,1b),pl(2b,1a),eqe12(2a,1a,2b,1b,t2".2d166")),compl(2a,1b)):eq(a,b)
a@[n:not(zero(a))]
satzd166e:=rappd(a,posd(absd(a)),[t:posd(a)]satzd166a(a,t),th2"l.imp"(zero(a),posd(absd(a)),n),[t:negd(a)]satzd166b(a,t)):posd(absd(a))
a@[z:zero(a)]
satzd166f:=eqzero(a,absd(a),symeq(absd(a),a,absnnd(a,0notnd(a,z))),z):zero(absd(a))
b@mored:=more(pl(1a,2b),pl(1b,2a)):'prop'
b2@[m:more(pl(a1,b2),pl(b1,a2))]
moredi12:=ismore12(pl(a1,b2),pl(stm(df(a1,a2)),std(df(b1,b2))),pl(b1,a2),pl(stm(df(b1,b2)),std(df(a1,a2))),12issmsd(a1,a2,b1,b2),12issmsd(b1,b2,a1,a2),m):mored(df(a1,a2),df(b1,b2))
r2@[m:more(pl(1a,r2),pl(r1,2a))]
moredi1:=ismore12(pl(1a,r2),pl(1a,std(df(r1,r2))),pl(r1,2a),pl(stm(df(r1,r2)),2a),sm2issmsd(a,r1,r2),1sdissmsd(a,r1,r2),m):mored(a,df(r1,r2))
r2@[m:more(pl(r1,2a),pl(1a,r2))]
moredi2:=ismore12(pl(r1,2a),pl(stm(df(r1,r2)),2a),pl(1a,r2),pl(1a,std(df(r1,r2))),1sdissmsd(a,r1,r2),sm2issmsd(a,r1,r2),m):mored(df(r1,r2),a)
b2@[m:mored(df(a1,a2),df(b1,b2))]
morede12:=ismore12(pl(stm(df(a1,a2)),std(df(b1,b2))),pl(a1,b2),pl(stm(df(b1,b2)),std(df(a1,a2))),pl(b1,a2),smsdis12(a1,a2,b1,b2),smsdis12(b1,b2,a1,a2),m):more(pl(a1,b2),pl(b1,a2))
b@lessd:=less(pl(1a,2b),pl(1b,2a)):'prop'
[m:mored(a,b)]
lemmad5:=satz121(pl(1a,2b),pl(1b,2a),m):lessd(b,a)
b@[l:lessd(a,b)]
lemmad6:=satz122(pl(1a,2b),pl(1b,2a),l):mored(b,a)
b2@[l:less(pl(a1,b2),pl(b1,a2))]
lessdi12:=lemmad5(df(b1,b2),df(a1,a2),moredi12(b1,b2,a1,a2,satz122(pl(a1,b2),pl(b1,a2),l))):lessd(df(a1,a2),df(b1,b2))
r2@[l:less(pl(1a,r2),pl(r1,2a))]
lessdi1:=lemmad5(df(r1,r2),a,moredi2(a,r1,r2,satz122(pl(1a,r2),pl(r1,2a),l))):lessd(a,df(r1,r2))
r2@[l:less(pl(r1,2a),pl(1a,r2))]
lessdi2:=lemmad5(a,df(r1,r2),moredi1(a,r1,r2,satz122(pl(r1,2a),pl(1a,r2),l))):lessd(df(r1,r2),a)
b2@[l:lessd(df(a1,a2),df(b1,b2))]
lessde12:=satz121(pl(b1,a2),pl(a1,b2),morede12(b1,b2,a1,a2,lemmad6(df(a1,a2),df(b1,b2),l))):less(pl(a1,b2),pl(b1,a2))
b@satzd167:=satz123(pl(1a,2b),pl(1b,2a)):orec3(eq(a,b),mored(a,b),lessd(a,b))
satzd167a:=satz123a(pl(1a,2b),pl(1b,2a)):or3(eq(a,b),mored(a,b),lessd(a,b))
satzd167b:=satz123b(pl(1a,2b),pl(1b,2a)):ec3(eq(a,b),mored(a,b),lessd(a,b))
d@1d:=stm(d):cut
2d:=std(d):cut
[e:eq(a,b)][f:eq(c,d)][m:mored(a,c)]
+*iv2d
m@t4:=tr4is(cut,pl(pl(1b,2d),pl(1c,2a)),pl(pl(1b,2a),pl(1c,2d)),pl(pl(1a,2b),pl(1d,2c)),pl(pl(1a,2c),pl(1d,2b)),pl(pl(1d,2b),pl(1a,2c)),4pl24(1b,2d,1c,2a),ispl12(pl(1b,2a),pl(1a,2b),pl(1c,2d),pl(1d,2c),symeq(a,b,e),f),4pl24(1a,2b,1d,2c),compl(pl(1a,2c),pl(1d,2b))):is(pl(pl(1b,2d),pl(1c,2a)),pl(pl(1d,2b),pl(1a,2c)))
t5:=ismore2(pl(pl(1b,2d),pl(1c,2a)),pl(pl(1d,2b),pl(1a,2c)),pl(pl(1b,2d),pl(1a,2c)),t4,satz135d(pl(1a,2c),pl(1c,2a),pl(1b,2d),m)):more(pl(pl(1b,2d),pl(1a,2c)),pl(pl(1d,2b),pl(1a,2c)))
-iv2d
m@eqmored12:=satz136a(pl(1b,2d),pl(1d,2b),pl(1a,2c),t5".iv2d"):mored(b,d)
f@[l:lessd(a,c)]
eqlessd12:=lemmad5(d,b,eqmored12(c,d,a,b,f,e,lemmad6(a,c,l))):lessd(b,d)
c@[e:eq(a,b)][m:mored(a,c)]
eqmored1:=eqmored12(a,b,c,c,e,refeq(c),m):mored(b,c)
e@[m:mored(c,a)]
eqmored2:=eqmored12(c,c,a,b,refeq(c),e,m):mored(c,b)
e@[l:lessd(a,c)]
eqlessd1:=eqlessd12(a,b,c,c,e,refeq(c),l):lessd(b,c)
e@[l:lessd(c,a)]
eqlessd2:=eqlessd12(c,c,a,b,refeq(c),e,l):lessd(c,b)
b@moreq:=or(mored(a,b),eq(a,b)):'prop'
lesseq:=or(lessd(a,b),eq(a,b)):'prop'
[m:moreq(a,b)]
satzd168a:=th9"l.or"(mored(a,b),eq(a,b),lessd(b,a),eq(b,a),m,[t:mored(a,b)]lemmad5(a,b,t),[t:eq(a,b)]symeq(a,b,t)):lesseq(b,a)
b@[l:lesseq(a,b)]
satzd168b:=th9"l.or"(lessd(a,b),eq(a,b),mored(b,a),eq(b,a),l,[t:lessd(a,b)]lemmad6(a,b,t),[t:eq(a,b)]symeq(a,b,t)):moreq(b,a)
c@[e:eq(a,b)][m:moreq(a,c)]
eqmoreq1:=th9"l.or"(mored(a,c),eq(a,c),mored(b,c),eq(b,c),m,[t:mored(a,c)]eqmored1(a,b,c,e,t),[t:eq(a,c)]treq1(b,c,a,e,t)):moreq(b,c)
e@[m:moreq(c,a)]
eqmoreq2:=th9"l.or"(mored(c,a),eq(c,a),mored(c,b),eq(c,b),m,[t:mored(c,a)]eqmored2(a,b,c,e,t),[t:eq(c,a)]treq(c,a,b,t,e)):moreq(c,b)
e@[l:lesseq(a,c)]
eqlesseq1:=satzd168a(c,b,eqmoreq2(a,b,c,e,satzd168b(a,c,l))):lesseq(b,c)
e@[l:lesseq(c,a)]
eqlesseq2:=satzd168a(b,c,eqmoreq1(a,b,c,e,satzd168b(c,a,l))):lesseq(c,b)
d@[e:eq(a,b)][f:eq(c,d)][m:moreq(a,c)]
eqmoreq12:=eqmoreq1(a,b,d,e,eqmoreq2(c,d,a,f,m)):moreq(b,d)
f@[l:lesseq(a,c)]
eqlesseq12:=eqlesseq1(a,b,d,e,eqlesseq2(c,d,a,f,l)):lesseq(b,d)
b@[m:mored(a,b)]
moreqi1:=ori1(mored(a,b),eq(a,b),m):moreq(a,b)
b@[l:lessd(a,b)]
lesseqi1:=ori1(lessd(a,b),eq(a,b),l):lesseq(a,b)
b@[e:eq(a,b)]
moreqi2:=ori2(mored(a,b),eq(a,b),e):moreq(a,b)
lesseqi2:=ori2(lessd(a,b),eq(a,b),e):lesseq(a,b)
b@[m:moreq(a,b)]
satzd167c:=th7"l.ec3"(eq(a,b),mored(a,b),lessd(a,b),satzd167b,comor(mored(a,b),eq(a,b),m)):not(lessd(a,b))
b@[l:lesseq(a,b)]
satzd167d:=th9"l.ec3"(eq(a,b),mored(a,b),lessd(a,b),satzd167b,l):not(mored(a,b))
b@[n:not(mored(a,b))]
satzd167e:=th2"l.or3"(eq(a,b),mored(a,b),lessd(a,b),satzd167a,n):lesseq(a,b)
b@[n:not(lessd(a,b))]
satzd167f:=comor(eq(a,b),mored(a,b),th3"l.or3"(eq(a,b),mored(a,b),lessd(a,b),satzd167a,n)):moreq(a,b)
b@[m:mored(a,b)]
satzd167g:=th3"l.imp"(lesseq(a,b),not(mored(a,b)),weli(mored(a,b),m),[t:lesseq(a,b)]satzd167d(t)):not(lesseq(a,b))
b@[l:lessd(a,b)]
satzd167h:=th3"l.imp"(moreq(a,b),not(lessd(a,b)),weli(lessd(a,b),l),[t:moreq(a,b)]satzd167c(t)):not(moreq(a,b))
b@[n:not(moreq(a,b))]
satzd167j:=or3e3(eq(a,b),mored(a,b),lessd(a,b),satzd167a,th5"l.or"(mored(a,b),eq(a,b),n),th4"l.or"(mored(a,b),eq(a,b),n)):lessd(a,b)
b@[n:not(lesseq(a,b))]
satzd167k:=or3e2(eq(a,b),mored(a,b),lessd(a,b),satzd167a,th4"l.or"(lessd(a,b),eq(a,b),n),th5"l.or"(lessd(a,b),eq(a,b),n)):mored(a,b)
b@[z:zero(b)][p:posd(a)]
satzd169a:=ismore12(pl(1a,1b),pl(1a,2b),pl(2a,1b),pl(1b,2a),ispl2(1b,2b,1a,z),compl(2a,1b),satz135a(1a,2a,1b,p)):mored(a,b)
z@[m:mored(a,b)]
satzd169b:=satz136d(1a,2a,2b,ismore12(pl(1a,2b),pl(2b,1a),pl(1b,2a),pl(2b,2a),compl(1a,2b),ispl1(1b,2b,2a,z),m)):posd(a)
z@[n:negd(a)]
satzd169c:=isless12(pl(1a,1b),pl(1a,2b),pl(2a,1b),pl(1b,2a),ispl2(1b,2b,1a,z),compl(2a,1b),satz135c(1a,2a,1b,n)):lessd(a,b)
z@[l:lessd(a,b)]
satzd169d:=satz136f(1a,2a,2b,isless12(pl(1a,2b),pl(2b,1a),pl(1b,2a),pl(2b,2a),compl(1a,2b),ispl1(1b,2b,2a,z),l)):negd(a)
+2d170
z@[p:posd(a)]
t1:=moreqi1(absd(a),b,satzd169a(absd(a),b,z,satzd166a(a,p))):moreq(absd(a),b)
z@[y:zero(a)]
t2:=moreqi2(absd(a),b,treq(absd(a),a,b,absnnd(a,0notnd(a,y)),zeroeq(a,b,y,z))):moreq(absd(a),b)
z@[n:negd(a)]
t3:=moreqi1(absd(a),b,satzd169a(absd(a),b,z,satzd166b(a,n))):moreq(absd(a),b)
-2d170
z@satzd170:=rappd(a,moreq(absd(a),b),[t:posd(a)]t1".2d170"(t),[t:zero(a)]t2".2d170"(t),[t:negd(a)]t3".2d170"(t)):moreq(absd(a),b)
c@[l:lessd(a,b)][k:lessd(b,c)]
+2d171
t1:=satz137a(pl(1a,2b),pl(1b,2a),pl(1b,2c),pl(1c,2b),l,k):less(pl(pl(1a,2b),pl(1b,2c)),pl(pl(1b,2a),pl(1c,2b)))
t2:=isless12(pl(pl(1a,2b),pl(1b,2c)),pl(pl(1a,2c),pl(1b,2b)),pl(pl(1b,2a),pl(1c,2b)),pl(pl(1c,2a),pl(1b,2b)),4pl24(1a,2b,1b,2c),tris(cut,pl(pl(1b,2a),pl(1c,2b)),pl(pl(1b,2b),pl(1c,2a)),pl(pl(1c,2a),pl(1b,2b)),4pl24(1b,2a,1c,2b),compl(pl(1b,2b),pl(1c,2a))),t1):less(pl(pl(1a,2c),pl(1b,2b)),pl(pl(1c,2a),pl(1b,2b)))
-2d171
satzd171:=satz136c(pl(1a,2c),pl(1c,2a),pl(1b,2b),t2".2d171"):lessd(a,c)
trlessd:=satzd171:lessd(a,c)
c@[m:mored(a,b)][n:mored(b,c)]
trmored:=lemmad6(c,a,trlessd(c,b,a,lemmad5(b,c,n),lemmad5(a,b,m))):mored(a,c)
c@[l:lesseq(a,b)][k:lessd(b,c)]
satzd172a:=orapp(lessd(a,b),eq(a,b),lessd(a,c),l,[t:lessd(a,b)]trlessd(t,k),[t:eq(a,b)]eqlessd1(b,a,c,symeq(a,b,t),k)):lessd(a,c)
c@[l:lessd(a,b)][k:lesseq(b,c)]
satzd172b:=orapp(lessd(b,c),eq(b,c),lessd(a,c),k,[t:lessd(b,c)]trlessd(l,t),[t:eq(b,c)]eqlessd2(b,c,a,t,l)):lessd(a,c)
c@[m:moreq(a,b)][n:mored(b,c)]
satzd172c:=lemmad6(c,a,satzd172b(c,b,a,lemmad5(b,c,n),satzd168a(a,b,m))):mored(a,c)
c@[m:mored(a,b)][n:moreq(b,c)]
satzd172d:=lemmad6(c,a,satzd172a(c,b,a,satzd168a(b,c,n),lemmad5(a,b,m))):mored(a,c)
c@[l:lesseq(a,b)][k:lesseq(b,c)]
+2d173
[j:lessd(a,b)]
t1:=lesseqi1(a,c,satzd172b(j,k)):lesseq(a,c)
k@[e:eq(a,b)]
t2:=eqlesseq1(b,a,c,symeq(a,b,e),k):lesseq(a,c)
-2d173
satzd173:=orapp(lessd(a,b),eq(a,b),lesseq(a,c),l,[t:lessd(a,b)]t1".2d173"(t),[t:eq(a,b)]t2".2d173"(t)):lesseq(a,c)
trlesseq:=satzd173:lesseq(a,c)
c@[m:moreq(a,b)][n:moreq(b,c)]
trmoreq:=satzd168b(c,a,trlesseq(c,b,a,satzd168a(b,c,n),satzd168a(a,b,m))):moreq(a,c)
a@ratd:=[t:not(zero(a))]ratrp(rpofpd(absd(a),satzd166e(a,t))):'prop'
irratd:=not(ratd(a)):'prop'
b@[e:eq(a,b)][r:ratd(a)]
+*iv2d
r@[n:not(zero(b))]
t6:=th3"l.imp"(zero(a),zero(b),n,[t:zero(a)]eqzero(a,b,e,t)):not(zero(a))
t7:=eqpderp(absd(a),satzd166e(a,t6),absd(b),satzd166e(b,n),eqabsd(a,b,e)):is(rpofpd(absd(a),satzd166e(a,t6)),rpofpd(absd(b),satzd166e(b,n)))
t8:=isp(cut,[t:cut]ratrp(t),rpofpd(absd(a),satzd166e(a,t6)),rpofpd(absd(b),satzd166e(b,n)),<t6>r,t7):ratrp(rpofpd(absd(b),satzd166e(b,n)))
-iv2d
r@eqratd:=[t:not(zero(b))]t8".iv2d"(t):ratd(b)
e@[i:irratd(a)]
eqirratd:=th3"l.imp"(ratd(b),ratd(a),i,[t:ratd(b)]eqratd(b,a,symeq(a,b,e),t)):irratd(b)
a@[z:zero(a)]
ratdi0:=th2"l.r.imp"(not(zero(a)),[t:not(zero(a))]ratrp(rpofpd(absd(a),satzd166e(a,t))),weli(zero(a),z)):ratd(a)
@[r:cut][i:irratrp(r)][x0:rat]
+*iv2d
x0@[s:ratrp(pl(r,rpofrt(x0)))][y0:rat][j:is(pl(r,rpofrt(x0)),rpofrt(y0))]
t9:=tris(cut,pl(rpofrt(x0),r),pl(r,rpofrt(x0)),rpofrt(y0),compl(rpofrt(x0),r),j):is(pl(rpofrt(x0),r),rpofrt(y0))
t10:=ismore1(pl(rpofrt(x0),r),rpofrt(y0),rpofrt(x0),t9,satz133(rpofrt(x0),r)):more(rpofrt(y0),rpofrt(x0))
t11:=satz154d(y0,x0,t10):more"rt"(y0,x0)
t12:=satz155b(y0,x0,t11):is(rpofrt(mn"rt"(y0,x0,t11)),mn(rpofrt(y0),rpofrt(x0),satz154a(y0,x0,t11)))
t13:=satz140g(rpofrt(y0),rpofrt(x0),r,satz154a(y0,x0,t11),t9):is(r,mn(rpofrt(y0),rpofrt(x0),satz154a(y0,x0,t11)))
t14:=tris2(cut,r,rpofrt(mn"rt"(y0,x0,t11)),mn(rpofrt(y0),rpofrt(x0),satz154a(y0,x0,t11)),t13,t12):is(r,rpofrt(mn"rt"(y0,x0,t11)))
t15:=somei(rat,[x:rat]is(r,rpofrt(x)),mn"rt"(y0,x0,t11),t14):ratrp(r)
s@t16:=someapp(rat,[x:rat]is(pl(r,rpofrt(x0)),rpofrt(x)),s,con,[x:rat][t:is(pl(r,rpofrt(x0)),rpofrt(x))]<t15(x,t)>i):con
-iv2d
x0@remark1:=[t:ratrp(pl(r,rpofrt(x0)))]t16".iv2d"(t):irratrp(pl(r,rpofrt(x0)))
+*iv2d
r@rp:=pdofrp(r):dif
rn:=ndofrp(r):dif
t17:=posdirp(r):posd(rp)
t18:=pnot0d(rp,t17):not(zero(rp))
t19:=nnot0d(rn,negdirp(r)):not(zero(rn))
[n:not(zero(rp))]
t20:=tris2(cut,r,rpofpd(absd(rp),satzd166e(rp,n)),rpofpd(rp,t17),isrppd1(r),eqpderp(absd(rp),satzd166e(rp,n),rp,t17,absnnd(rp,pnotnd(rp,t17)))):is(r,rpofpd(absd(rp),satzd166e(rp,n)))
r@t21:=treq(absd(rn),df(std(rn),stm(rn)),rp,absnd(rn,negdirp(r)),eqsmsd(std(rn),stm(rn),pl(r,1rp),1rp,stdis(1rp,pl(r,1rp)),stmis(1rp,pl(r,1rp)))):eq(absd(rn),rp)
[n:not(zero(rn))]
t22:=tris2(cut,r,rpofpd(absd(rn),satzd166e(rn,n)),rpofpd(rp,t17),isrppd1(r),eqpderp(absd(rn),satzd166e(rn,n),rp,t17,t21)):is(r,rpofpd(absd(rn),satzd166e(rn,n)))
r@[s:cut][i:is(r,s)][rr:ratrp(r)]
t23:=isp(cut,[x:cut]ratrp(x),r,s,rr,i):ratrp(s)
i@[rs:ratrp(s)]
t24:=isp1(cut,[x:cut]ratrp(x),s,r,rs,i):ratrp(r)
-iv2d
r@[rr:ratrp(r)]
remark2a:=[t:not(zero(pdofrp(r)))]t23".iv2d"(rpofpd(absd(pdofrp(r)),satzd166e(pdofrp(r),t)),t20".iv2d"(t),rr):ratd(pdofrp(r))
remark2b:=t17".iv2d":posd(pdofrp(r))
remark3a:=[t:not(zero(ndofrp(r)))]t23".iv2d"(rpofpd(absd(ndofrp(r)),satzd166e(ndofrp(r),t)),t22".iv2d"(t),rr):ratd(ndofrp(r))
remark3b:=negdirp(r):negd(ndofrp(r))
r@[i:irratrp(r)]
remark4a:=th3"l.imp"(ratd(pdofrp(r)),ratrp(r),i,[t:ratd(pdofrp(r))]t24".iv2d"(rpofpd(absd(pdofrp(r)),satzd166e(pdofrp(r),t18".iv2d")),t20".iv2d"(t18".iv2d"),<t18".iv2d">t)):irratd(pdofrp(r))
remark4b:=t17".iv2d":posd(pdofrp(r))
remark5a:=th3"l.imp"(ratd(ndofrp(r)),ratrp(r),i,[t:ratd(ndofrp(r))]t24".iv2d"(rpofpd(absd(ndofrp(r)),satzd166e(ndofrp(r),t19".iv2d")),t22".iv2d"(t19".iv2d"),<t19".iv2d">t)):irratd(ndofrp(r))
remark5b:=negdirp(r):negd(ndofrp(r))
a@natd:=and(posd(a),[t:posd(a)]natrp(rpofpd(a,t))):'prop'
[n:natd(a)]
natposd:=ande1(posd(a),[t:posd(a)]natrp(rpofpd(a,t)),n):posd(a)
natderp:=ande2"l.r"(posd(a),[t:posd(a)]natrp(rpofpd(a,t)),n):natrp(rpofpd(a,natposd(a,n)))
b@[e:eq(a,b)][n:natd(a)]
+*iv2d
n"rp"@t25:=eqposd(a,b,e,natposd(a,n)):posd(b)
[p:posd(b)]
t26:=eqpderp(a,natposd(a,n),b,p,e):is(rpofpd(a,natposd(a,n)),rpofpd(b,p))
t27:=isp(cut,[t:cut]natrp(t),rpofpd(a,natposd(a,n)),rpofpd(b,p),natderp(a,n),t26):natrp(rpofpd(b,p))
-iv2d
n@eqnatd:=andi(posd(b),[t:posd(b)]natrp(rpofpd(b,t)),t25".iv2d",[t:posd(b)]t27".iv2d"(t)):natd(b)
@[x:nat]
pdofnt:=pdofrp(rpofnt(x)):dif
+*iv2d
x@t28:=posdirp(rpofnt(x)):posd(pdofnt(x))
[p:posd(pdofnt(x))]
t29:=isrppd1(rpofnt(x)):is(rpofnt(x),rpofpd(pdofnt(x),t28))
t30:=eqpderp(pdofnt(x),t28,pdofnt(x),p,refeq(pdofnt(x))):is(rpofpd(pdofnt(x),t28),rpofpd(pdofnt(x),p))
t31:=tris(cut,rpofnt(x),rpofpd(pdofnt(x),t28),rpofpd(pdofnt(x),p),t29,t30):is(rpofnt(x),rpofpd(pdofnt(x),p))
t32:=isp(cut,[t:cut]natrp(t),rpofnt(x),rpofpd(pdofnt(x),p),natrpi(x),t31):natrp(rpofpd(pdofnt(x),p))
-iv2d
x@natdi:=andi(posd(pdofnt(x)),[t:posd(pdofnt(x))]natrp(rpofpd(pdofnt(x),t)),t28".iv2d",[t:posd(pdofnt(x))]t32".iv2d"(t)):natd(pdofnt(x))
a@intd:=or(zero(a),natd(absd(a))):'prop'
b@[e:eq(a,b)][i:intd(a)]
+*iv2d
i"rp"@[z:zero(a)]
t33:=eqzero(a,b,e,z):zero(b)
i"rp"@[n:natd(absd(a))]
t34:=eqnatd(absd(a),absd(b),eqabsd(a,b,e),n):natd(absd(b))
-iv2d
i@eqintd:=th9"l.or"(zero(a),natd(absd(a)),zero(b),natd(absd(b)),i,[t:zero(a)]t33".iv2d"(t),[t:natd(absd(a))]t34".iv2d"(t)):intd(b)
a@[n:natd(a)]
+*iv2d
n"rp"@t34a:=symeq(absd(a),a,absnnd(a,pnotnd(a,natposd(a,n)))):eq(a,absd(a))
t35:=eqnatd(a,absd(a),t34a,n):natd(absd(a))
-iv2d
n@natintd:=ori2(zero(a),natd(absd(a)),t35".iv2d"):intd(a)
a@[p:posd(a)][i:intd(a)]
+*iv2d
i"rp"@t36:=ore2(zero(a),natd(absd(a)),i,pnot0d(a,p)):natd(absd(a))
-iv2d
i@posintnatd:=eqnatd(absd(a),a,absnnd(a,pnotnd(a,p)),t36".iv2d"):natd(a)
a@[z:zero(a)]
intdi0:=ori1(zero(a),natd(absd(a)),z):intd(a)
r@[n:natrp(r)]
+*iv2d
n"rp"@t37:=posdirp(r):posd(pdofrp(r))
[p:posd(pdofrp(r))]
t38:=tris(cut,r,rpofpd(pdofrp(r),t37),rpofpd(pdofrp(r),p),isrppd1(r),eqpderp(pdofrp(r),t37,pdofrp(r),p,refeq(pdofrp(r)))):is(r,rpofpd(pdofrp(r),p))
t39:=isp(cut,[t:cut]natrp(t),r,rpofpd(pdofrp(r),p),n,t38):natrp(rpofpd(pdofrp(r),p))
-iv2d
n@remark6a:=andi(posd(pdofrp(r)),[t:posd(pdofrp(r))]natrp(rpofpd(pdofrp(r),t)),t37".iv2d",[t:posd(pdofrp(r))]t39".iv2d"(t)):natd(pdofrp(r))
remark6:=natintd(pdofrp(r),remark6a):intd(pdofrp(r))
+*iv2d
n"rp"@t40:=absdeql(1rp,pl(r,1rp),isless2(pl(1rp,r),pl(r,1rp),1rp,compl(1rp,r),satz133a(1rp,r))):eq(absd(ndofrp(r)),pdofrp(r))
t41:=eqnatd(pdofrp(r),absd(ndofrp(r)),symeq(absd(ndofrp(r)),pdofrp(r),t40),remark6a):natd(absd(ndofrp(r)))
-iv2d
n@remark7:=ori2(zero(ndofrp(r)),natd(absd(ndofrp(r))),t41".iv2d"):intd(ndofrp(r))
a@[i:intd(a)]
+2d174
[n:not(zero(a))]
t1:=ore2(zero(a),natd(absd(a)),i,n):natd(absd(a))
t2:=ande2(posd(absd(a)),[t:posd(absd(a))]natrp(rpofpd(absd(a),t)),t1):[t:posd(absd(a))]natrp(rpofpd(absd(a),t))
t3:=lemmaiii5(rpofpd(absd(a),satzd166e(a,n)),<satzd166e(a,n)>t2):ratrp(rpofpd(absd(a),satzd166e(a,n)))
-2d174
satzd174:=[t:not(zero(a))]t3".2d174"(t):ratd(a)
b@pd:=df(pl(1a,1b),pl(2a,2b)):dif
b2@pd12:=issmsd(pl(stm(df(a1,a2)),stm(df(b1,b2))),pl(std(df(a1,a2)),std(df(b1,b2))),pl(a1,b1),pl(a2,b2),ispl12(stm(df(a1,a2)),a1,stm(df(b1,b2)),b1,stmis(a1,a2),stmis(b1,b2)),ispl12(std(df(a1,a2)),a2,std(df(b1,b2)),b2,stdis(a1,a2),stdis(b1,b2))):is"e"(dif,pd(df(a1,a2),df(b1,b2)),df(pl(a1,b1),pl(a2,b2)))
r2@pd1:=issmsd(pl(1a,stm(df(r1,r2))),pl(2a,std(df(r1,r2))),pl(1a,r1),pl(2a,r2),ispl2(stm(df(r1,r2)),r1,1a,stmis(r1,r2)),ispl2(std(df(r1,r2)),r2,2a,stdis(r1,r2))):is"e"(dif,pd(a,df(r1,r2)),df(pl(1a,r1),pl(2a,r2)))
pd2:=issmsd(pl(stm(df(r1,r2)),1a),pl(std(df(r1,r2)),2a),pl(r1,1a),pl(r2,2a),ispl1(stm(df(r1,r2)),r1,1a,stmis(r1,r2)),ispl1(std(df(r1,r2)),r2,2a,stdis(r1,r2))):is"e"(dif,pd(df(r1,r2),a),df(pl(r1,1a),pl(r2,2a)))
b2@pdeq12a:=refeq1(pd(df(a1,a2),df(b1,b2)),df(pl(a1,b1),pl(a2,b2)),pd12):eq(pd(df(a1,a2),df(b1,b2)),df(pl(a1,b1),pl(a2,b2)))
pdeq12b:=refeq2(pd(df(a1,a2),df(b1,b2)),df(pl(a1,b1),pl(a2,b2)),pd12):eq(df(pl(a1,b1),pl(a2,b2)),pd(df(a1,a2),df(b1,b2)))
r2@pdeq1a:=refeq1(pd(a,df(r1,r2)),df(pl(1a,r1),pl(2a,r2)),pd1):eq(pd(a,df(r1,r2)),df(pl(1a,r1),pl(2a,r2)))
pdeq1b:=refeq2(pd(a,df(r1,r2)),df(pl(1a,r1),pl(2a,r2)),pd1):eq(df(pl(1a,r1),pl(2a,r2)),pd(a,df(r1,r2)))
pdeq2a:=refeq1(pd(df(r1,r2),a),df(pl(r1,1a),pl(r2,2a)),pd2):eq(pd(df(r1,r2),a),df(pl(r1,1a),pl(r2,2a)))
pdeq2b:=refeq2(pd(df(r1,r2),a),df(pl(r1,1a),pl(r2,2a)),pd2):eq(df(pl(r1,1a),pl(r2,2a)),pd(df(r1,r2),a))
b@satzd175:=eqsmsd(pl(1a,1b),pl(2a,2b),pl(1b,1a),pl(2b,2a),compl(1a,1b),compl(2a,2b)):eq(pd(a,b),pd(b,a))
compd:=satzd175:eq(pd(a,b),pd(b,a))
c@[e:eq(a,b)]
+iv3d
t1:=tr3is(cut,pl(pl(1a,1c),pl(2b,2c)),pl(pl(1a,2b),pl(1c,2c)),pl(pl(1b,2a),pl(1c,2c)),pl(pl(1b,1c),pl(2a,2c)),4pl23(1a,1c,2b,2c),ispl1(pl(1a,2b),pl(1b,2a),pl(1c,2c),e),4pl23(1b,2a,1c,2c)):is(pl(pl(1a,1c),pl(2b,2c)),pl(pl(1b,1c),pl(2a,2c)))
-iv3d
eqpd1:=eqi12(pl(1a,1c),pl(2a,2c),pl(1b,1c),pl(2b,2c),t1".iv3d"):eq(pd(a,c),pd(b,c))
eqpd2:=tr3eq(pd(c,a),pd(a,c),pd(b,c),pd(c,b),compd(c,a),eqpd1,compd(b,c)):eq(pd(c,a),pd(c,b))
d@[e:eq(a,b)][f:eq(c,d)]
eqpd12:=treq(pd(a,c),pd(b,c),pd(b,d),eqpd1(a,b,c,e),eqpd2(c,d,b,f)):eq(pd(a,c),pd(b,d))
b@[z:zero(a)]
+*iv3d
z@t2:=tr4is(cut,pl(pl(1a,1b),2b),pl(1a,pl(1b,2b)),pl(2a,pl(2b,1b)),pl(pl(2a,2b),1b),pl(1b,pl(2a,2b)),asspl1(1a,1b,2b),ispl12(1a,2a,pl(1b,2b),pl(2b,1b),z,compl(1b,2b)),asspl2(2a,2b,1b),compl(pl(2a,2b),1b)):is(pl(pl(1a,1b),2b),pl(1b,pl(2a,2b)))
-iv3d
z@pd01:=eqi2(b,pl(1a,1b),pl(2a,2b),t2".iv3d"):eq(pd(a,b),b)
b@[z:zero(b)]
pd02:=treq(pd(a,b),pd(b,a),a,compd(a,b),pd01(b,a,z)):eq(pd(a,b),a)
b@[p:posd(a)][q:posd(b)]
ppd:=posdi(pl(1a,1b),pl(2a,2b),satz137(1a,2a,1b,2b,p,q)):posd(pd(a,b))
b@[n:negd(a)][o:negd(b)]
npd:=negdi(pl(1a,1b),pl(2a,2b),satz137a(1a,2a,1b,2b,n,o)):negd(pd(a,b))
a@m0d:=df(2a,1a):dif
a2@m0deqa:=eqsmsd(std(df(a1,a2)),stm(df(a1,a2)),a2,a1,stdis(a1,a2),stmis(a1,a2)):eq(m0d(df(a1,a2)),df(a2,a1))
m0deqb:=symeq(m0d(df(a1,a2)),df(a2,a1),m0deqa):eq(df(a2,a1),m0d(df(a1,a2)))
b@[e:eq(a,b)]
+*iv3d
e@t3:=tr3is(cut,pl(2a,1b),pl(1b,2a),pl(1a,2b),pl(2b,1a),compl(2a,1b),symeq(a,b,e),compl(1a,2b)):is(pl(2a,1b),pl(2b,1a))
-iv3d
e@eqm0d:=eqi12(2a,1a,2b,1b,t3".iv3d"):eq(m0d(a),m0d(b))
a@[p:posd(a)]
satzd176a:=negdi(2a,1a,satz121(1a,2a,p)):negd(m0d(a))
a@[z:zero(a)]
satzd176b:=zeroi(2a,1a,symis(cut,1a,2a,z)):zero(m0d(a))
a@[n:negd(a)]
satzd176c:=posdi(2a,1a,satz122(1a,2a,n)):posd(m0d(a))
a@[n:negd(m0d(a))]
satzd176d:=satz122(2a,1a,isless12(stm(m0d(a)),2a,std(m0d(a)),1a,stmis(2a,1a),stdis(2a,1a),n)):posd(a)
a@[z:zero(m0d(a))]
satzd176e:=symis(cut,2a,1a,tr3is(cut,2a,stm(df(2a,1a)),std(df(2a,1a)),1a,isstm(2a,1a),z,stdis(2a,1a))):zero(a)
a@[p:posd(m0d(a))]
satzd176f:=satz121(2a,1a,ismore12(stm(m0d(a)),2a,std(m0d(a)),1a,stmis(2a,1a),stdis(2a,1a),p)):negd(a)
a@[z:zero(a)]
m0d0:=zeroeq(m0d(a),a,satzd176b(a,z),z):eq(m0d(a),a)
+3d177
a@t1:=tris(dif,m0d(m0d(a)),df(1a,2a),a,issmsd(std(m0d(a)),stm(m0d(a)),1a,2a,stdis(2a,1a),stmis(2a,1a)),dfis(a)):is"e"(dif,m0d(m0d(a)),a)
-3d177
a@satzd177:=refeq1(m0d(m0d(a)),a,t1".3d177"):eq(m0d(m0d(a)),a)
satzd177a:=symeq(m0d(m0d(a)),a,satzd177):eq(a,m0d(m0d(a)))
b@[e:eq(a,m0d(b))]
satzd177b:=treq(m0d(a),m0d(m0d(b)),b,eqm0d(a,m0d(b),e),satzd177(b)):eq(m0d(a),b)
satzd177c:=symeq(m0d(a),b,satzd177b):eq(b,m0d(a))
b@[e:eq(m0d(a),b)]
satzd177d:=satzd177c(b,a,symeq(m0d(a),b,e)):eq(a,m0d(b))
satzd177e:=symeq(a,m0d(b),satzd177d):eq(m0d(b),a)
+3d178
a@[p:posd(a)]
t1:=tr3eq(absd(m0d(a)),m0d(m0d(a)),a,absd(a),absnd(m0d(a),satzd176a(a,p)),satzd177(a),symeq(absd(a),a,absnnd(a,pnotnd(a,p)))):eq(absd(m0d(a)),absd(a))
a@[z:zero(a)]
t2:=tr3eq(absd(m0d(a)),m0d(a),a,absd(a),absnnd(m0d(a),0notnd(m0d(a),satzd176b(a,z))),m0d0(a,z),symeq(absd(a),a,absnnd(a,0notnd(a,z)))):eq(absd(m0d(a)),absd(a))
a@[n:negd(a)]
t3:=treq(absd(m0d(a)),m0d(a),absd(a),absnnd(m0d(a),pnotnd(m0d(a),satzd176c(a,n))),symeq(absd(a),m0d(a),absnd(a,n))):eq(absd(m0d(a)),absd(a))
-3d178
a@satzd178:=rappd(a,eq(absd(m0d(a)),absd(a)),[t:posd(a)]t1".3d178"(t),[t:zero(a)]t2".3d178"(t),[t:negd(a)]t3".3d178"(t)):eq(absd(m0d(a)),absd(a))
satzd178a:=symeq(absd(m0d(a)),absd(a),satzd178):eq(absd(a),absd(m0d(a)))
+3d179
t1:=pdeq1b(a,2a,1a):eq(df(pl(1a,2a),pl(2a,1a)),pd(a,m0d(a)))
t2:=zeroi(pl(1a,2a),pl(2a,1a),compl(1a,2a)):zero(df(pl(1a,2a),pl(2a,1a)))
-3d179
satzd179:=eqzero(df(pl(1a,2a),pl(2a,1a)),pd(a,m0d(a)),t1".3d179",t2".3d179"):zero(pd(a,m0d(a)))
satzd179a:=eqzero(pd(a,m0d(a)),pd(m0d(a),a),compd(a,m0d(a)),satzd179):zero(pd(m0d(a),a))
b@satzd180:=treq(m0d(pd(a,b)),df(pl(2a,2b),pl(1a,1b)),pd(m0d(a),m0d(b)),m0deqa(pl(1a,1b),pl(2a,2b)),pdeq12b(2a,1a,2b,1b)):eq(m0d(pd(a,b)),pd(m0d(a),m0d(b)))
satzd180a:=symeq(m0d(pd(a,b)),pd(m0d(a),m0d(b)),satzd180):eq(pd(m0d(a),m0d(b)),m0d(pd(a,b)))
md:=pd(a,m0d(b)):dif
b2@mdeq12a:=treq(md(df(a1,a2),df(b1,b2)),pd(df(a1,a2),df(b2,b1)),df(pl(a1,b2),pl(a2,b1)),eqpd2(m0d(df(b1,b2)),df(b2,b1),df(a1,a2),m0deqa(b1,b2)),pdeq12a(a1,a2,b2,b1)):eq(md(df(a1,a2),df(b1,b2)),df(pl(a1,b2),pl(a2,b1)))
mdeq12b:=symeq(md(df(a1,a2),df(b1,b2)),df(pl(a1,b2),pl(a2,b1)),mdeq12a):eq(df(pl(a1,b2),pl(a2,b1)),md(df(a1,a2),df(b1,b2)))
r2@mdeq1a:=treq(md(a,df(r1,r2)),pd(a,df(r2,r1)),df(pl(1a,r2),pl(2a,r1)),eqpd2(m0d(df(r1,r2)),df(r2,r1),a,m0deqa(r1,r2)),pdeq1a(a,r2,r1)):eq(md(a,df(r1,r2)),df(pl(1a,r2),pl(2a,r1)))
mdeq1b:=symeq(md(a,df(r1,r2)),df(pl(1a,r2),pl(2a,r1)),mdeq1a):eq(df(pl(1a,r2),pl(2a,r1)),md(a,df(r1,r2)))
mdeq2a:=pdeq12a(r1,r2,2a,1a):eq(md(df(r1,r2),a),df(pl(r1,2a),pl(r2,1a)))
mdeq2b:=pdeq12b(r1,r2,2a,1a):eq(df(pl(r1,2a),pl(r2,1a)),md(df(r1,r2),a))
c@[e:eq(a,b)]
eqmd1:=eqpd1(a,b,m0d(c),e):eq(md(a,c),md(b,c))
eqmd2:=eqpd2(m0d(a),m0d(b),c,eqm0d(a,b,e)):eq(md(c,a),md(c,b))
d@[e:eq(a,b)][f:eq(c,d)]
eqmd12:=treq(md(a,c),md(b,c),md(b,d),eqmd1(a,b,c,e),eqmd2(c,d,b,f)):eq(md(a,c),md(b,d))
b@satzd181:=tr3eq(m0d(md(a,b)),pd(m0d(a),m0d(m0d(b))),pd(m0d(a),b),md(b,a),satzd180(a,m0d(b)),eqpd2(m0d(m0d(b)),b,m0d(a),satzd177(b)),compd(m0d(a),b)):eq(m0d(md(a,b)),md(b,a))
satzd181a:=symeq(m0d(md(b,a)),md(a,b),satzd181(b,a)):eq(md(a,b),m0d(md(b,a)))
+3d182
t1:=treq(md(a,b),df(pl(1a,2b),pl(2a,1b)),df(pl(1a,2b),pl(1b,2a)),pdeq1a(a,2b,1b),eqsd(pl(1a,2b),pl(2a,1b),pl(1b,2a),compl(2a,1b))):eq(md(a,b),df(pl(1a,2b),pl(1b,2a)))
t2:=symeq(md(a,b),df(pl(1a,2b),pl(1b,2a)),t1):eq(df(pl(1a,2b),pl(1b,2a)),md(a,b))
t3:=stmis(pl(1a,2b),pl(1b,2a)):is(stm(df(pl(1a,2b),pl(1b,2a))),pl(1a,2b))
t4:=stdis(pl(1a,2b),pl(1b,2a)):is(std(df(pl(1a,2b),pl(1b,2a))),pl(1b,2a))
-3d182
[p:posd(md(a,b))]
+*3d182
p@t5:=eqposd(md(a,b),df(pl(1a,2b),pl(1b,2a)),t1,p):posd(df(pl(1a,2b),pl(1b,2a)))
-3d182
p@satzd182a:=ismore12(stm(df(pl(1a,2b),pl(1b,2a))),pl(1a,2b),std(df(pl(1a,2b),pl(1b,2a))),pl(1b,2a),t3".3d182",t4".3d182",t5".3d182"):mored(a,b)
b@[z:zero(md(a,b))]
+*3d182
z@t6:=eqzero(md(a,b),df(pl(1a,2b),pl(1b,2a)),t1,z):zero(df(pl(1a,2b),pl(1b,2a)))
-3d182
z@satzd182b:=tr3is(cut,pl(1a,2b),stm(df(pl(1a,2b),pl(1b,2a))),std(df(pl(1a,2b),pl(1b,2a))),pl(1b,2a),isstm(pl(1a,2b),pl(1b,2a)),t6".3d182",t4".3d182"):eq(a,b)
b@[n:negd(md(a,b))]
+*3d182
n@t7:=eqnegd(md(a,b),df(pl(1a,2b),pl(1b,2a)),t1,n):negd(df(pl(1a,2b),pl(1b,2a)))
-3d182
n@satzd182c:=isless12(stm(df(pl(1a,2b),pl(1b,2a))),pl(1a,2b),std(df(pl(1a,2b),pl(1b,2a))),pl(1b,2a),t3".3d182",t4".3d182",t7".3d182"):lessd(a,b)
b@[m:mored(a,b)]
+*3d182
m@t8:=posdi(pl(1a,2b),pl(1b,2a),m):posd(df(pl(1a,2b),pl(1b,2a)))
-3d182
m@satzd182d:=eqposd(df(pl(1a,2b),pl(1b,2a)),md(a,b),t2".3d182",t8".3d182"):posd(md(a,b))
b@[e:eq(a,b)]
+*3d182
e@t9:=zeroi(pl(1a,2b),pl(1b,2a),e):zero(df(pl(1a,2b),pl(1b,2a)))
-3d182
e@satzd182e:=eqzero(df(pl(1a,2b),pl(1b,2a)),md(a,b),t2".3d182",t9".3d182"):zero(md(a,b))
b@[l:lessd(a,b)]
+*3d182
l@t10:=negdi(pl(1a,2b),pl(1b,2a),l):negd(df(pl(1a,2b),pl(1b,2a)))
-3d182
l@satzd182f:=eqnegd(df(pl(1a,2b),pl(1b,2a)),md(a,b),t2".3d182",t10".3d182"):negd(md(a,b))
+3d183
b@t1:=tris(cut,pl(1a,2b),pl(2b,1a),pl(stm(m0d(b)),std(m0d(a))),compl(1a,2b),12issmsd(2b,1b,2a,1a)):is(pl(1a,2b),pl(stm(m0d(b)),std(m0d(a))))
t2:=t1(b,a):is(pl(1b,2a),pl(stm(m0d(a)),std(m0d(b))))
-3d183
b@[m:mored(a,b)]
satzd183a:=isless12(pl(1b,2a),pl(stm(m0d(a)),std(m0d(b))),pl(1a,2b),pl(stm(m0d(b)),std(m0d(a))),t2".3d183",t1".3d183",lemmad5(a,b,m)):lessd(m0d(a),m0d(b))
b@[e:eq(a,b)]
staz183b:=eqm0d(a,b,e):eq(m0d(a),m0d(b))
b@[l:lessd(a,b)]
satzd183c:=ismore12(pl(1b,2a),pl(stm(m0d(a)),std(m0d(b))),pl(1a,2b),pl(stm(m0d(b)),std(m0d(a))),t2".3d183",t1".3d183",lemmad6(a,b,l)):mored(m0d(a),m0d(b))
b@[l:lessd(m0d(a),m0d(b))]
satzd183d:=eqmored12(m0d(m0d(a)),a,m0d(m0d(b)),b,satzd177(a),satzd177(b),satzd183c(m0d(a),m0d(b),l)):mored(a,b)
b@[e:eq(m0d(a),m0d(b))]
satzd183e:=tr3eq(a,m0d(m0d(a)),m0d(m0d(b)),b,satzd177a(a),eqm0d(m0d(a),m0d(b),e),satzd177(b)):eq(a,b)
b@[m:mored(m0d(a),m0d(b))]
satzd183f:=eqlessd12(m0d(m0d(a)),a,m0d(m0d(b)),b,satzd177(a),satzd177(b),satzd183a(m0d(a),m0d(b),m)):lessd(a,b)
+3d184
a@t1:=tr3eq(a,df(pl(1a,pl(1rp,1rp)),pl(2a,pl(1rp,1rp))),df(pl(pl(1a,1rp),1rp),pl(1rp,pl(2a,1rp))),md(pdofrp(1a),pdofrp(2a)),lemmad3(a,pl(1rp,1rp)),eqsmsd(pl(1a,pl(1rp,1rp)),pl(2a,pl(1rp,1rp)),pl(pl(1a,1rp),1rp),pl(1rp,pl(2a,1rp)),asspl2(1a,1rp,1rp),3pl12(2a,1rp,1rp)),mdeq12b(pl(1a,1rp),1rp,pl(2a,1rp),1rp)):eq(a,md(pdofrp(1a),pdofrp(2a)))
t2:=and3i(posd(pdofrp(1a)),posd(pdofrp(2a)),eq(a,md(pdofrp(1a),pdofrp(2a))),posdirp(1a),posdirp(2a),t1):and3(posd(pdofrp(1a)),posd(pdofrp(2a)),eq(a,md(pdofrp(1a),pdofrp(2a))))
t3:=somei(dif,[x:dif]and3(posd(pdofrp(1a)),posd(x),eq(a,md(pdofrp(1a),x))),pdofrp(2a),t2):some"l"(dif,[x:dif]and3(posd(pdofrp(1a)),posd(x),eq(a,md(pdofrp(1a),x))))
-3d184
a@satzd184:=somei(dif,[x:dif]some"l"(dif,[y:dif]and3(posd(x),posd(y),eq(a,md(x,y)))),pdofrp(1a),t3".3d184"):some"l"(dif,[x:dif]some"l"(dif,[y:dif]and3(posd(x),posd(y),eq(a,md(x,y)))))
c@asspd1:=tr3eq(pd(pd(a,b),c),df(pl(pl(1a,1b),1c),pl(pl(2a,2b),2c)),df(pl(1a,pl(1b,1c)),pl(2a,pl(2b,2c))),pd(a,pd(b,c)),pdeq2a(c,pl(1a,1b),pl(2a,2b)),eqsmsd(pl(pl(1a,1b),1c),pl(pl(2a,2b),2c),pl(1a,pl(1b,1c)),pl(2a,pl(2b,2c)),asspl1(1a,1b,1c),asspl1(2a,2b,2c)),pdeq1b(a,pl(1b,1c),pl(2b,2c))):eq(pd(pd(a,b),c),pd(a,pd(b,c)))
asspd2:=symeq(pd(pd(a,b),c),pd(a,pd(b,c)),asspd1):eq(pd(a,pd(b,c)),pd(pd(a,b),c))
3pd23:=tr3eq(pd(pd(a,b),c),pd(a,pd(b,c)),pd(a,pd(c,b)),pd(pd(a,c),b),asspd1(a,b,c),eqpd2(pd(b,c),pd(c,b),a,compd(b,c)),asspd2(a,c,b)):eq(pd(pd(a,b),c),pd(pd(a,c),b))
d@4pd23:=tr3eq(pd(pd(a,b),pd(c,d)),pd(pd(pd(a,b),c),d),pd(pd(pd(a,c),b),d),pd(pd(a,c),pd(b,d)),asspd2(pd(a,b),c,d),eqpd1(pd(pd(a,b),c),pd(pd(a,c),b),d,3pd23),asspd1(pd(a,c),b,d)):eq(pd(pd(a,b),pd(c,d)),pd(pd(a,c),pd(b,d)))
b@pdmd:=treq(pd(md(a,b),b),pd(a,pd(m0d(b),b)),a,asspd1(a,m0d(b),b),pd02(a,pd(m0d(b),b),satzd179a(b))):eq(pd(md(a,b),b),a)
mdpd:=treq(md(pd(a,b),b),pd(a,pd(b,m0d(b))),a,asspd1(a,b,m0d(b)),pd02(a,pd(b,m0d(b)),satzd179(b))):eq(md(pd(a,b),b),a)
d@satzd185:=treq(pd(md(a,b),md(c,d)),pd(pd(a,c),pd(m0d(b),m0d(d))),md(pd(a,c),pd(b,d)),4pd23(a,m0d(b),c,m0d(d)),eqpd2(pd(m0d(b),m0d(d)),m0d(pd(b,d)),pd(a,c),satzd180a(b,d))):eq(pd(md(a,b),md(c,d)),md(pd(a,c),pd(b,d)))
c@satzd186:=asspd1:eq(pd(pd(a,b),c),pd(a,pd(b,c)))
b@satzd187a:=treq(pd(b,md(a,b)),pd(md(a,b),b),a,compd(b,md(a,b)),pdmd):eq(pd(b,md(a,b)),a)
[x:dif][e:eq(pd(b,x),a)]
satzd187c:=treq(md(a,b),md(pd(x,b),b),x,eqmd1(a,pd(x,b),b,treq1(a,pd(x,b),pd(b,x),e,compd(b,x))),mdpd(x,b)):eq(md(a,b),x)
satzd187d:=symeq(md(a,b),x,satzd187c):eq(x,md(a,b))
x@[e:eq(pd(x,b),a)]
satzd187e:=satzd187c(treq(pd(b,x),pd(x,b),a,compd(b,x),e)):eq(md(a,b),x)
satzd187f:=symeq(md(a,b),x,satzd187e):eq(x,md(a,b))
+3d188
c@t1:=tr3eq(md(pd(a,c),pd(b,c)),pd(pd(a,c),pd(m0d(b),m0d(c))),pd(md(a,b),md(c,c)),md(a,b),eqpd2(m0d(pd(b,c)),pd(m0d(b),m0d(c)),pd(a,c),satzd180(b,c)),4pd23(a,c,m0d(b),m0d(c)),pd02(md(a,b),md(c,c),satzd179(c))):eq(md(pd(a,c),pd(b,c)),md(a,b))
t2:=symeq(md(pd(a,c),pd(b,c)),md(a,b),t1):eq(md(a,b),md(pd(a,c),pd(b,c)))
-3d188
c@[m:mored(pd(a,c),pd(b,c))]
+*3d188
m@t3:=eqposd(md(pd(a,c),pd(b,c)),md(a,b),t1,satzd182d(pd(a,c),pd(b,c),m)):posd(md(a,b))
-3d188
m@satzd188a:=satzd182a(a,b,t3".3d188"):mored(a,b)
c@[e:eq(pd(a,c),pd(b,c))]
+*3d188
e@t4:=eqzero(md(pd(a,c),pd(b,c)),md(a,b),t1,satzd182e(pd(a,c),pd(b,c),e)):zero(md(a,b))
-3d188
e@satzd188b:=satzd182b(a,b,t4".3d188"):eq(a,b)
c@[l:lessd(pd(a,c),pd(b,c))]
+*3d188
l@t5:=eqnegd(md(pd(a,c),pd(b,c)),md(a,b),t1,satzd182f(pd(a,c),pd(b,c),l)):negd(md(a,b))
-3d188
l@satzd188c:=satzd182c(a,b,t5".3d188"):lessd(a,b)
c@[m:mored(a,b)]
+*3d188
m@t6:=eqposd(md(a,b),md(pd(a,c),pd(b,c)),t2,satzd182d(a,b,m)):posd(md(pd(a,c),pd(b,c)))
-3d188
m@satzd188d:=satzd182a(pd(a,c),pd(b,c),t6".3d188"):mored(pd(a,c),pd(b,c))
c@[e:eq(a,b)]
satzd188e:=eqpd1(a,b,c,e):eq(pd(a,c),pd(b,c))
c@[l:lessd(a,b)]
+*3d188
l@t7:=eqnegd(md(a,b),md(pd(a,c),pd(b,c)),t2,satzd182f(a,b,l)):negd(md(pd(a,c),pd(b,c)))
-3d188
l@satzd188f:=satzd182c(pd(a,c),pd(b,c),t7".3d188"):lessd(pd(a,c),pd(b,c))
c@[m:mored(pd(c,a),pd(c,b))]
satzd188g:=satzd188a(eqmored12(pd(c,a),pd(a,c),pd(c,b),pd(b,c),compd(c,a),compd(c,b),m)):mored(a,b)
c@[e:eq(pd(c,a),pd(c,b))]
satzd188h:=satzd188b(tr3eq(pd(a,c),pd(c,a),pd(c,b),pd(b,c),compd(a,c),e,compd(c,b))):eq(a,b)
[l:lessd(pd(c,a),pd(c,b))]
satzd188j:=satzd188c(eqlessd12(pd(c,a),pd(a,c),pd(c,b),pd(b,c),compd(c,a),compd(c,b),l)):lessd(a,b)
c@[m:mored(a,b)]
satzd188k:=eqmored12(pd(a,c),pd(c,a),pd(b,c),pd(c,b),compd(a,c),compd(b,c),satzd188d(m)):mored(pd(c,a),pd(c,b))
c@[e:eq(a,b)]
satzd188l:=eqpd2(a,b,c,e):eq(pd(c,a),pd(c,b))
c@[l:lessd(a,b)]
satzd188m:=eqlessd12(pd(a,c),pd(c,a),pd(b,c),pd(c,b),compd(a,c),compd(b,c),satzd188f(l)):lessd(pd(c,a),pd(c,b))
d@[e:eq(a,b)][m:mored(c,d)]
satzd188n:=eqmored2(pd(a,d),pd(b,d),pd(a,c),eqpd1(a,b,d,e),satzd188k(c,d,a,m)):mored(pd(a,c),pd(b,d))
satzd188o:=eqmored12(pd(a,c),pd(c,a),pd(b,d),pd(d,b),compd(a,c),compd(b,d),satzd188n):mored(pd(c,a),pd(d,b))
e@[l:lessd(c,d)]
satzd188p:=eqlessd2(pd(a,d),pd(b,d),pd(a,c),eqpd1(a,b,d,e),satzd188m(c,d,a,l)):lessd(pd(a,c),pd(b,d))
satzd188q:=eqlessd12(pd(a,c),pd(c,a),pd(b,d),pd(d,b),compd(a,c),compd(b,d),satzd188p):lessd(pd(c,a),pd(d,b))
d@[m:mored(a,b)][n:mored(c,d)]
satzd189:=trmored(pd(a,c),pd(b,c),pd(b,d),satzd188d(a,b,c,m),satzd188k(c,d,b,n)):mored(pd(a,c),pd(b,d))
d@[l:lessd(a,b)][k:lessd(c,d)]
satzd189a:=lemmad5(pd(b,d),pd(a,c),satzd189(b,a,d,c,lemmad6(a,b,l),lemmad6(c,d,k))):lessd(pd(a,c),pd(b,d))
d@[m:moreq(a,b)][n:mored(c,d)]
satzd190a:=orapp(mored(a,b),eq(a,b),mored(pd(a,c),pd(b,d)),m,[t:mored(a,b)]satzd189(t,n),[t:eq(a,b)]satzd188n(t,n)):mored(pd(a,c),pd(b,d))
d@[m:mored(a,b)][n:moreq(c,d)]
satzd190b:=eqmored12(pd(c,a),pd(a,c),pd(d,b),pd(b,d),compd(c,a),compd(d,b),satzd190a(c,d,a,b,n,m)):mored(pd(a,c),pd(b,d))
d@[l:lesseq(a,b)][k:lessd(c,d)]
satzd190c:=lemmad5(pd(b,d),pd(a,c),satzd190a(b,a,d,c,satzd168b(a,b,l),lemmad6(c,d,k))):lessd(pd(a,c),pd(b,d))
d@[l:lessd(a,b)][k:lesseq(c,d)]
satzd190d:=lemmad5(pd(b,d),pd(a,c),satzd190b(b,a,d,c,lemmad6(a,b,l),satzd168b(c,d,k))):lessd(pd(a,c),pd(b,d))
d@[m:moreq(a,b)][n:moreq(c,d)]
+3d191
[e:eq(a,b)][f:eq(c,d)]
t1:=moreqi2(pd(a,c),pd(b,d),eqpd12(a,b,c,d,e,f)):moreq(pd(a,c),pd(b,d))
e@[o:mored(c,d)]
t2:=moreqi1(pd(a,c),pd(b,d),satzd190a(m,o)):moreq(pd(a,c),pd(b,d))
e@t3:=orapp(mored(c,d),eq(c,d),moreq(pd(a,c),pd(b,d)),n,[t:mored(c,d)]t2(t),[t:eq(c,d)]t1(t)):moreq(pd(a,c),pd(b,d))
n@[o:mored(a,b)]
t4:=moreqi1(pd(a,c),pd(b,d),satzd190b(o,n)):moreq(pd(a,c),pd(b,d))
-3d191
satzd191:=orapp(mored(a,b),eq(a,b),moreq(pd(a,c),pd(b,d)),m,[t:mored(a,b)]t4".3d191"(t),[t:eq(a,b)]t3".3d191"(t)):moreq(pd(a,c),pd(b,d))
d@[l:lesseq(a,b)][k:lesseq(c,d)]
satzd191a:=satzd168a(pd(b,d),pd(a,c),satzd191(b,a,d,c,satzd168b(a,b,l),satzd168b(c,d,k))):lesseq(pd(a,c),pd(b,d))
b@td:=df(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b))):dif
+iv4d
a2@[r:cut]
t1:=ists1(stm(df(a1,a2)),a1,r,stmis(a1,a2)):is(ts(stm(df(a1,a2)),r),ts(a1,r))
t2:=ists2(stm(df(a1,a2)),a1,r,stmis(a1,a2)):is(ts(r,stm(df(a1,a2))),ts(r,a1))
t3:=ists1(std(df(a1,a2)),a2,r,stdis(a1,a2)):is(ts(std(df(a1,a2)),r),ts(a2,r))
t4:=ists2(std(df(a1,a2)),a2,r,stdis(a1,a2)):is(ts(r,std(df(a1,a2))),ts(r,a2))
[s:cut]
t5:=ispl12(ts(stm(df(a1,a2)),r),ts(a1,r),ts(std(df(a1,a2)),s),ts(a2,s),t1(r),t3(s)):is(pl(ts(stm(df(a1,a2)),r),ts(std(df(a1,a2)),s)),pl(ts(a1,r),ts(a2,s)))
t6:=ispl12(ts(r,stm(df(a1,a2))),ts(r,a1),ts(s,std(df(a1,a2))),ts(s,a2),t2(r),t4(s)):is(pl(ts(r,stm(df(a1,a2))),ts(s,std(df(a1,a2)))),pl(ts(r,a1),ts(s,a2)))
t7:=ispl12(ts(std(df(a1,a2)),r),ts(a2,r),ts(stm(df(a1,a2)),s),ts(a1,s),t3(r),t1(s)):is(pl(ts(std(df(a1,a2)),r),ts(stm(df(a1,a2)),s)),pl(ts(a2,r),ts(a1,s)))
t8:=ispl12(ts(r,std(df(a1,a2))),ts(r,a2),ts(s,stm(df(a1,a2))),ts(s,a1),t4(r),t2(s)):is(pl(ts(r,std(df(a1,a2))),ts(s,stm(df(a1,a2)))),pl(ts(r,a2),ts(s,a1)))
b2@t9:=tris(cut,pl(ts(stm(df(a1,a2)),stm(df(b1,b2))),ts(std(df(a1,a2)),std(df(b1,b2)))),pl(ts(a1,stm(df(b1,b2))),ts(a2,std(df(b1,b2)))),pl(ts(a1,b1),ts(a2,b2)),t5(a1,a2,stm(df(b1,b2)),std(df(b1,b2))),t6(b1,b2,a1,a2)):is(pl(ts(stm(df(a1,a2)),stm(df(b1,b2))),ts(std(df(a1,a2)),std(df(b1,b2)))),pl(ts(a1,b1),ts(a2,b2)))
t10:=tris(cut,pl(ts(stm(df(a1,a2)),std(df(b1,b2))),ts(std(df(a1,a2)),stm(df(b1,b2)))),pl(ts(a1,std(df(b1,b2))),ts(a2,stm(df(b1,b2)))),pl(ts(a1,b2),ts(a2,b1)),t5(a1,a2,std(df(b1,b2)),stm(df(b1,b2))),t8(b1,b2,a1,a2)):is(pl(ts(stm(df(a1,a2)),std(df(b1,b2))),ts(std(df(a1,a2)),stm(df(b1,b2)))),pl(ts(a1,b2),ts(a2,b1)))
-iv4d
b2@td12:=issmsd(pl(ts(stm(df(a1,a2)),stm(df(b1,b2))),ts(std(df(a1,a2)),std(df(b1,b2)))),pl(ts(stm(df(a1,a2)),std(df(b1,b2))),ts(std(df(a1,a2)),stm(df(b1,b2)))),pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1)),t9".iv4d",t10".iv4d"):is"e"(dif,td(df(a1,a2),df(b1,b2)),df(pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1))))
r2@td1:=issmsd(pl(ts(1a,stm(df(r1,r2))),ts(2a,std(df(r1,r2)))),pl(ts(1a,std(df(r1,r2))),ts(2a,stm(df(r1,r2)))),pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1)),t6".iv4d"(r1,r2,1a,2a),t8".iv4d"(r1,r2,1a,2a)):is"e"(dif,td(a,df(r1,r2)),df(pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1))))
td2:=issmsd(pl(ts(stm(df(r1,r2)),1a),ts(std(df(r1,r2)),2a)),pl(ts(stm(df(r1,r2)),2a),ts(std(df(r1,r2)),1a)),pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a)),t5".iv4d"(r1,r2,1a,2a),t5".iv4d"(r1,r2,2a,1a)):is"e"(dif,td(df(r1,r2),a),df(pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a))))
b2@tdeq12a:=refeq1(td(df(a1,a2),df(b1,b2)),df(pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1))),td12):eq(td(df(a1,a2),df(b1,b2)),df(pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1))))
tdeq12b:=refeq2(td(df(a1,a2),df(b1,b2)),df(pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1))),td12):eq(df(pl(ts(a1,b1),ts(a2,b2)),pl(ts(a1,b2),ts(a2,b1))),td(df(a1,a2),df(b1,b2)))
r2@tdeq1a:=refeq1(td(a,df(r1,r2)),df(pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1))),td1):eq(td(a,df(r1,r2)),df(pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1))))
tdeq1b:=refeq2(td(a,df(r1,r2)),df(pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1))),td1):eq(df(pl(ts(1a,r1),ts(2a,r2)),pl(ts(1a,r2),ts(2a,r1))),td(a,df(r1,r2)))
tdeq2a:=refeq1(td(df(r1,r2),a),df(pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a))),td2):eq(td(df(r1,r2),a),df(pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a))))
tdeq2b:=refeq2(td(df(r1,r2),a),df(pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a))),td2):eq(df(pl(ts(r1,1a),ts(r2,2a)),pl(ts(r1,2a),ts(r2,1a))),td(df(r1,r2),a))
+4d194
b@t1:=ispl12(ts(1a,1b),ts(1b,1a),ts(2a,2b),ts(2b,2a),comts(1a,1b),comts(2a,2b)):is(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1b,1a),ts(2b,2a)))
t2:=tris(cut,pl(ts(1a,2b),ts(2a,1b)),pl(ts(2a,1b),ts(1a,2b)),pl(ts(1b,2a),ts(2b,1a)),compl(ts(1a,2b),ts(2a,1b)),ispl12(ts(2a,1b),ts(1b,2a),ts(1a,2b),ts(2b,1a),comts(2a,1b),comts(1a,2b))):is(pl(ts(1a,2b),ts(2a,1b)),pl(ts(1b,2a),ts(2b,1a)))
-4d194
b@satzd194:=eqsmsd(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)),pl(ts(1b,1a),ts(2b,2a)),pl(ts(1b,2a),ts(2b,1a)),t1".4d194",t2".4d194"):eq(td(a,b),td(b,a))
comtd:=satzd194:eq(td(a,b),td(b,a))
c@[e:eq(a,b)]
+*iv4d
e@[r:cut]
t11:=tr3is(cut,pl(ts(1a,r),ts(2b,r)),ts(pl(1a,2b),r),ts(pl(1b,2a),r),pl(ts(1b,r),ts(2a,r)),distpt1(1a,2b,r),ists1(pl(1a,2b),pl(1b,2a),r,e),disttp1(1b,2a,r)):is(pl(ts(1a,r),ts(2b,r)),pl(ts(1b,r),ts(2a,r)))
e@t12:=tr3is(cut,pl(pl(ts(1a,1c),ts(2a,2c)),pl(ts(1b,2c),ts(2b,1c))),pl(pl(ts(1a,1c),ts(2b,1c)),pl(ts(1b,2c),ts(2a,2c))),pl(pl(ts(1b,1c),ts(2a,1c)),pl(ts(1a,2c),ts(2b,2c))),pl(pl(ts(1b,1c),ts(2b,2c)),pl(ts(1a,2c),ts(2a,1c))),4pl24(ts(1a,1c),ts(2a,2c),ts(1b,2c),ts(2b,1c)),ispl12(pl(ts(1a,1c),ts(2b,1c)),pl(ts(1b,1c),ts(2a,1c)),pl(ts(1b,2c),ts(2a,2c)),pl(ts(1a,2c),ts(2b,2c)),t11(1c),t11(b,a,c,symeq(a,b,e),2c)),4pl24(ts(1b,1c),ts(2a,1c),ts(1a,2c),ts(2b,2c))):is(pl(pl(ts(1a,1c),ts(2a,2c)),pl(ts(1b,2c),ts(2b,1c))),pl(pl(ts(1b,1c),ts(2b,2c)),pl(ts(1a,2c),ts(2a,1c))))
-iv4d
e@eqtd1:=eqi12(pl(ts(1a,1c),ts(2a,2c)),pl(ts(1a,2c),ts(2a,1c)),pl(ts(1b,1c),ts(2b,2c)),pl(ts(1b,2c),ts(2b,1c)),t12".iv4d"):eq(td(a,c),td(b,c))
eqtd2:=tr3eq(td(c,a),td(a,c),td(b,c),td(c,b),comtd(c,a),eqtd1,comtd(b,c)):eq(td(c,a),td(c,b))
d@[e:eq(a,b)][f:eq(c,d)]
eqtd12:=treq(td(a,c),td(b,c),td(b,d),eqtd1(a,b,c,e),eqtd2(c,d,b,f)):eq(td(a,c),td(b,d))
b@[z:zero(a)]
+4d192
t1:=tris(cut,pl(ts(1a,1b),ts(2a,2b)),pl(ts(2a,1b),ts(1a,2b)),pl(ts(1a,2b),ts(2a,1b)),ispl12(ts(1a,1b),ts(2a,1b),ts(2a,2b),ts(1a,2b),ists1(1a,2a,1b,z),ists1(2a,1a,2b,symis(cut,1a,2a,z))),compl(ts(2a,1b),ts(1a,2b))):is(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)))
-4d192
satzd192a:=zeroi(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)),t1".4d192"):zero(td(a,b))
b@[z:zero(b)]
satzd192b:=eqzero(td(b,a),td(a,b),comtd(b,a),satzd192a(b,a,z)):zero(td(a,b))
b@[z:zero(a)]
td01:=satzd192a(z):zero(td(a,b))
b@[z:zero(b)]
td02:=satzd192b(z):zero(td(a,b))
b@satzd197a:=tr3eq(td(m0d(a),b),df(pl(ts(2a,1b),ts(1a,2b)),pl(ts(2a,2b),ts(1a,1b))),df(pl(ts(1a,2b),ts(2a,1b)),pl(ts(1a,1b),ts(2a,2b))),m0d(td(a,b)),tdeq2a(b,2a,1a),eqsmsd(pl(ts(2a,1b),ts(1a,2b)),pl(ts(2a,2b),ts(1a,1b)),pl(ts(1a,2b),ts(2a,1b)),pl(ts(1a,1b),ts(2a,2b)),compl(ts(2a,1b),ts(1a,2b)),compl(ts(2a,2b),ts(1a,1b))),m0deqb(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)))):eq(td(m0d(a),b),m0d(td(a,b)))
satzd197b:=tr3eq(td(a,m0d(b)),td(m0d(b),a),m0d(td(b,a)),m0d(td(a,b)),comtd(a,m0d(b)),satzd197a(b,a),eqm0d(td(b,a),td(a,b),comtd(b,a))):eq(td(a,m0d(b)),m0d(td(a,b)))
satzd197c:=treq2(td(m0d(a),b),td(a,m0d(b)),m0d(td(a,b)),satzd197a,satzd197b):eq(td(m0d(a),b),td(a,m0d(b)))
satzd197d:=symeq(td(m0d(a),b),td(a,m0d(b)),satzd197c):eq(td(a,m0d(b)),td(m0d(a),b))
satzd197e:=symeq(td(m0d(a),b),m0d(td(a,b)),satzd197a):eq(m0d(td(a,b)),td(m0d(a),b))
satzd197f:=symeq(td(a,m0d(b)),m0d(td(a,b)),satzd197b):eq(m0d(td(a,b)),td(a,m0d(b)))
satzd198:=treq(td(m0d(a),m0d(b)),td(a,m0d(m0d(b))),td(a,b),satzd197c(a,m0d(b)),eqtd2(m0d(m0d(b)),b,a,satzd177(b))):eq(td(m0d(a),m0d(b)),td(a,b))
satzd198a:=symeq(td(m0d(a),m0d(b)),td(a,b),satzd198):eq(td(a,b),td(m0d(a),m0d(b)))
[p:posd(a)][q:posd(b)]
+*iv4d
q@[r:cut]
t13:=tris(cut,pl(ts(r,mn(1b,2b,q)),ts(r,2b)),ts(r,pl(mn(1b,2b,q),2b)),ts(r,1b),distpt2(r,mn(1b,2b,q),2b),ists2(pl(mn(1b,2b,q),2b),1b,r,satz140e(1b,2b,q))):is(pl(ts(r,mn(1b,2b,q)),ts(r,2b)),ts(r,1b))
[s:cut]
t14:=tris(cut,pl(ts(r,mn(1b,2b,q)),pl(ts(r,2b),s)),pl(pl(ts(r,mn(1b,2b,q)),ts(r,2b)),s),pl(ts(r,1b),s),asspl2(ts(r,mn(1b,2b,q)),ts(r,2b),s),ispl1(pl(ts(r,mn(1b,2b,q)),ts(r,2b)),ts(r,1b),s,t13)):is(pl(ts(r,mn(1b,2b,q)),pl(ts(r,2b),s)),pl(ts(r,1b),s))
t15:=tris(cut,pl(ts(r,mn(1b,2b,q)),pl(ts(r,2b),s)),pl(ts(r,1b),s),pl(s,ts(r,1b)),t14,compl(ts(r,1b),s)):is(pl(ts(r,mn(1b,2b,q)),pl(ts(r,2b),s)),pl(s,ts(r,1b)))
q@t16:=satz135h(pl(ts(1a,2b),ts(2a,2b)),pl(ts(2a,2b),ts(1a,2b)),ts(1a,mn(1b,2b,q)),ts(2a,mn(1b,2b,q)),compl(ts(1a,2b),ts(2a,2b)),satz145a(1a,2a,mn(1b,2b,q),p)):more(pl(ts(1a,mn(1b,2b,q)),pl(ts(1a,2b),ts(2a,2b))),pl(ts(2a,mn(1b,2b,q)),pl(ts(2a,2b),ts(1a,2b))))
t17:=ismore12(pl(ts(1a,mn(1b,2b,q)),pl(ts(1a,2b),ts(2a,2b))),pl(ts(1a,1b),ts(2a,2b)),pl(ts(2a,mn(1b,2b,q)),pl(ts(2a,2b),ts(1a,2b))),pl(ts(1a,2b),ts(2a,1b)),t14(1a,ts(2a,2b)),t15(2a,ts(1a,2b)),t16):more(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)))
-iv4d
q@ptdpp:=posdi(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)),t17".iv4d"):posd(td(a,b))
b@[p:posd(a)][n:negd(b)]
+*iv4d
n@t18:=eqposd(td(a,m0d(b)),m0d(td(a,b)),satzd197b(a,b),ptdpp(a,m0d(b),p,satzd176c(b,n))):posd(m0d(td(a,b)))
-iv4d
n@ntdpn:=satzd176f(td(a,b),t18".iv4d"):negd(td(a,b))
b@[n:negd(a)][p:posd(b)]
ntdnp:=eqnegd(td(b,a),td(a,b),comtd(b,a),ntdpn(b,a,p,n)):negd(td(a,b))
b@[n:negd(a)][o:negd(b)]
ptdnn:=eqposd(td(m0d(a),m0d(b)),td(a,b),satzd198(a,b),ptdpp(m0d(a),m0d(b),satzd176c(a,n),satzd176c(b,o))):posd(td(a,b))
b@[n:not(zero(a))][o:not(zero(b))]
+*4d192
o@[p:posd(a)][q:posd(b)]
t2:=pnot0d(td(a,b),ptdpp(a,b,p,q)):not(zero(td(a,b)))
p@[m:negd(b)]
t3:=nnot0d(td(a,b),ntdpn(a,b,p,m)):not(zero(td(a,b)))
p@t4:=rappd(b,not(zero(td(a,b))),[t:posd(b)]t2(t),th2"l.imp"(zero(b),not(zero(td(a,b))),o),[t:negd(b)]t3(t)):not(zero(td(a,b)))
o@[m:negd(a)][p:posd(b)]
t5:=nnot0d(td(a,b),ntdnp(a,b,m,p)):not(zero(td(a,b)))
m@[l:negd(b)]
t6:=pnot0d(td(a,b),ptdnn(a,b,m,l)):not(zero(td(a,b)))
m@t7:=rappd(b,not(zero(td(a,b))),[t:posd(b)]t5(t),th2"l.imp"(zero(b),not(zero(td(a,b))),o),[t:negd(b)]t6(t)):not(zero(td(a,b)))
-4d192
o@satzd192d:=rappd(a,not(zero(td(a,b))),[t:posd(a)]t4".4d192"(t),th2"l.imp"(zero(a),not(zero(td(a,b))),n),[t:negd(a)]t7".4d192"(t)):not(zero(td(a,b)))
b@[z:zero(td(a,b))]
+*4d192
z@[n:not(zero(a))]
t8:=et(zero(b),th3"l.imp"(not(zero(b)),not(zero(td(a,b))),weli(zero(td(a,b)),z),[t:not(zero(b))]satzd192d(n,t))):zero(b)
-4d192
z@satzd192c:=[t:not(zero(a))]t8".4d192"(t):or(zero(a),zero(b))
+4d193
b@[p:posd(a)][q:posd(b)]
t1:=treq2(absd(td(a,b)),td(absd(a),absd(b)),td(a,b),absnnd(td(a,b),pnotnd(td(a,b),ptdpp(a,b,p,q))),eqtd12(absd(a),a,absd(b),b,absnnd(a,pnotnd(a,p)),absnnd(b,pnotnd(b,q)))):eq(absd(td(a,b)),td(absd(a),absd(b)))
p@[n:negd(b)]
t2:=treq(absd(td(a,b)),m0d(td(a,b)),td(a,m0d(b)),absnd(td(a,b),ntdpn(a,b,p,n)),satzd197f(a,b)):eq(absd(td(a,b)),td(a,m0d(b)))
t3:=treq2(absd(td(a,b)),td(absd(a),absd(b)),td(a,m0d(b)),t2,eqtd12(absd(a),a,absd(b),m0d(b),absnnd(a,pnotnd(a,p)),absnd(b,n))):eq(absd(td(a,b)),td(absd(a),absd(b)))
b@[z:zero(a)]
t4:=zeroeq(absd(td(a,b)),td(absd(a),absd(b)),satzd166f(td(a,b),td01(a,b,z)),td01(absd(a),absd(b),satzd166f(a,z))):eq(absd(td(a,b)),td(absd(a),absd(b)))
b@[z:zero(b)]
t5:=zeroeq(absd(td(a,b)),td(absd(a),absd(b)),satzd166f(td(a,b),td02(a,b,z)),td02(absd(a),absd(b),satzd166f(b,z))):eq(absd(td(a,b)),td(absd(a),absd(b)))
b@[n:negd(a)][p:posd(b)]
t6:=tr3eq(absd(td(a,b)),absd(td(b,a)),td(absd(b),absd(a)),td(absd(a),absd(b)),eqabsd(td(a,b),td(b,a),comtd(a,b)),t3(b,a,p,n),comtd(absd(b),absd(a))):eq(absd(td(a,b)),td(absd(a),absd(b)))
n@[o:negd(b)]
t7:=treq(td(absd(a),absd(b)),td(m0d(a),m0d(b)),td(a,b),eqtd12(absd(a),m0d(a),absd(b),m0d(b),absnd(a,n),absnd(b,o)),satzd198(a,b)):eq(td(absd(a),absd(b)),td(a,b))
t8:=treq2(absd(td(a,b)),td(absd(a),absd(b)),td(a,b),absnnd(td(a,b),pnotnd(td(a,b),ptdnn(a,b,n,o))),t7):eq(absd(td(a,b)),td(absd(a),absd(b)))
b@[p:posd(a)]
t9:=rappd(b,eq(absd(td(a,b)),td(absd(a),absd(b))),[t:posd(b)]t1(p,t),[t:zero(b)]t5(t),[t:negd(b)]t3(p,t)):eq(absd(td(a,b)),td(absd(a),absd(b)))
b@[n:negd(a)]
t10:=rappd(b,eq(absd(td(a,b)),td(absd(a),absd(b))),[t:posd(b)]t6(n,t),[t:zero(b)]t5(t),[t:negd(b)]t8(n,t)):eq(absd(td(a,b)),td(absd(a),absd(b)))
-4d193
b@satzd193:=rappd(a,eq(absd(td(a,b)),td(absd(a),absd(b))),[t:posd(a)]t9".4d193"(t),[t:zero(a)]t4".4d193"(t),[t:negd(a)]t10".4d193"(t)):eq(absd(td(a,b)),td(absd(a),absd(b)))
satzd103a:=symeq(absd(td(a,b)),td(absd(a),absd(b)),satzd193):eq(td(absd(a),absd(b)),absd(td(a,b)))
@1df:=pdofrp(1rp):dif
+4d195
a@t1:=tris(cut,pl(pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),2a),pl(ts(1a,pl(1rp,1rp)),pl(ts(2a,1rp),2a)),pl(pl(1a,1a),pl(2a,2a)),asspl1(ts(1a,pl(1rp,1rp)),ts(2a,1rp),2a),ispl12(ts(1a,pl(1rp,1rp)),pl(1a,1a),pl(ts(2a,1rp),2a),pl(2a,2a),a2isapa(1a),ispl1(ts(2a,1rp),2a,2a,satz151(2a)))):is(pl(pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),2a),pl(pl(1a,1a),pl(2a,2a)))
t2:=tris(cut,pl(1a,pl(ts(1a,1rp),ts(2a,pl(1rp,1rp)))),pl(pl(1a,ts(1a,1rp)),ts(2a,pl(1rp,1rp))),pl(pl(1a,1a),pl(2a,2a)),asspl2(1a,ts(1a,1rp),ts(2a,pl(1rp,1rp))),ispl12(pl(1a,ts(1a,1rp)),pl(1a,1a),ts(2a,pl(1rp,1rp)),pl(2a,2a),ispl2(ts(1a,1rp),1a,1a,satz151(1a)),a2isapa(2a))):is(pl(1a,pl(ts(1a,1rp),ts(2a,pl(1rp,1rp)))),pl(pl(1a,1a),pl(2a,2a)))
t3:=tris2(cut,pl(pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),2a),pl(1a,pl(ts(1a,1rp),ts(2a,pl(1rp,1rp)))),pl(pl(1a,1a),pl(2a,2a)),t1,t2):is(pl(pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),2a),pl(1a,pl(ts(1a,1rp),ts(2a,pl(1rp,1rp)))))
-4d195
a@satzd195:=treq(td(a,1df),df(pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),pl(ts(1a,1rp),ts(2a,pl(1rp,1rp)))),a,tdeq1a(a,pl(1rp,1rp),1rp),eqi2(a,pl(ts(1a,pl(1rp,1rp)),ts(2a,1rp)),pl(ts(1a,1rp),ts(2a,pl(1rp,1rp))),t3".4d195")):eq(td(a,1df),a)
satzd195a:=symeq(td(a,1df),a,satzd195):eq(a,td(a,1df))
satzd195b:=treq(td(1df,a),td(a,1df),a,comtd(1df,a),satzd195):eq(td(1df,a),a)
satzd195c:=symeq(td(1df,a),a,satzd195b):eq(a,td(1df,a))
b@[p:posd(a)][q:posd(b)]
satzd196a:=symeq(td(absd(a),absd(b)),td(a,b),eqtd12(absd(a),a,absd(b),b,absnnd(a,pnotnd(a,p)),absnnd(b,pnotnd(b,q)))):eq(td(a,b),td(absd(a),absd(b)))
b@[n:negd(a)][o:negd(b)]
satzd196b:=treq2(td(a,b),td(absd(a),absd(b)),td(m0d(a),m0d(b)),satzd198a(a,b),eqtd12(absd(a),m0d(a),absd(b),m0d(b),absnd(a,n),absnd(b,o))):eq(td(a,b),td(absd(a),absd(b)))
b@[p:posd(a)][n:negd(b)]
satzd196c:=treq1(td(a,b),m0d(td(absd(a),absd(b))),td(absd(a),m0d(absd(b))),eqtd12(absd(a),a,m0d(absd(b)),b,absnnd(a,pnotnd(a,p)),satzd177b(absd(b),b,absnd(b,n))),satzd197b(absd(a),absd(b))):eq(td(a,b),m0d(td(absd(a),absd(b))))
b@[n:negd(a)][p:posd(b)]
satzd196d:=tr3eq(td(a,b),td(b,a),m0d(td(absd(b),absd(a))),m0d(td(absd(a),absd(b))),comtd(a,b),satzd196c(b,a,p,n),eqm0d(td(absd(b),absd(a)),td(absd(a),absd(b)),comtd(absd(b),absd(a)))):eq(td(a,b),m0d(td(absd(a),absd(b))))
+4d196
b@p1p2:=and(posd(a),posd(b)):'prop'
p1n2:=and(posd(a),negd(b)):'prop'
n1p2:=and(negd(a),posd(b)):'prop'
n1n2:=and(negd(a),negd(b)):'prop'
-4d196
b@[n:not(zero(a))][o:not(zero(b))][e:eq(td(a,b),td(absd(a),absd(b)))]
+*4d196
o@t1:=ptdpp(absd(a),absd(b),satzd166e(a,n),satzd166e(b,o)):posd(td(absd(a),absd(b)))
e@t2:=pnotnd(td(a,b),eqposd(td(absd(a),absd(b)),td(a,b),symeq(td(a,b),td(absd(a),absd(b)),e),t1)):not(negd(td(a,b)))
[p:posd(a)]
t3:=th3"l.imp"(negd(b),negd(td(a,b)),t2,[t:negd(b)]ntdpn(a,b,p,t)):not(negd(b))
t4:=or3e2(zero(b),posd(b),negd(b),axrdo(b),t3,o):posd(b)
t5:=andi(posd(a),posd(b),p,t4):p1p2
t6:=ori1(p1p2,n1n2,t5):or(p1p2,n1n2)
e@[m:negd(a)]
t7:=th3"l.imp"(posd(b),negd(td(a,b)),t2,[t:posd(b)]ntdnp(a,b,m,t)):not(posd(b))
t8:=or3e3(zero(b),posd(b),negd(b),axrdo(b),o,t7):negd(b)
t9:=andi(negd(a),negd(b),m,t8):n1n2
t10:=ori2(p1p2,n1n2,t9):or(p1p2,n1n2)
-4d196
e@satzd196e:=rappd(a,or(p1p2".4d196",n1n2".4d196"),[t:posd(a)]t6".4d196"(t),th2"l.imp"(zero(a),or(p1p2".4d196",n1n2".4d196"),n),[t:negd(a)]t10".4d196"(t)):or(and(posd(a),posd(b)),and(negd(a),negd(b)))
o@[e:eq(td(a,b),m0d(td(absd(a),absd(b))))]
+*4d196
o@t11:=satzd176a(td(absd(a),absd(b)),t1):negd(m0d(td(absd(a),absd(b))))
e@t12:=nnotpd(td(a,b),eqnegd(m0d(td(absd(a),absd(b))),td(a,b),symeq(td(a,b),m0d(td(absd(a),absd(b))),e),t11)):not(posd(td(a,b)))
[p:posd(a)]
t13:=th3"l.imp"(posd(b),posd(td(a,b)),t12,[t:posd(b)]ptdpp(a,b,p,t)):not(posd(b))
t14:=or3e3(zero(b),posd(b),negd(b),axrdo(b),o,t13):negd(b)
t15:=andi(posd(a),negd(b),p,t14):p1n2
t16:=ori1(p1n2,n1p2,t15):or(p1n2,n1p2)
e@[m:negd(a)]
t17:=th3"l.imp"(negd(b),posd(td(a,b)),t12,[t:negd(b)]ptdnn(a,b,m,t)):not(negd(b))
t18:=or3e2(zero(b),posd(b),negd(b),axrdo(b),t17,o):posd(b)
t19:=andi(negd(a),posd(b),m,t18):n1p2
t20:=ori2(p1n2,n1p2,t19):or(p1n2,n1p2)
-4d196
e@satzd196f:=rappd(a,or(p1n2".4d196",n1p2".4d196"),[t:posd(a)]t16".4d196"(t),th2"l.imp"(zero(a),or(p1n2".4d196",n1p2".4d196"),n),[t:negd(a)]t20".4d196"(t)):or(and(posd(a),negd(b)),and(negd(a),posd(b)))
+4d199
@[p:cut][q:cut][r:cut][s:cut][t:cut][u:cut]
t1:=tris(cut,ts(pl(ts(p,r),ts(q,s)),t),pl(ts(ts(p,r),t),ts(ts(q,s),t)),pl(ts(p,ts(r,t)),ts(q,ts(s,t))),disttp1(ts(p,r),ts(q,s),t),ispl12(ts(ts(p,r),t),ts(p,ts(r,t)),ts(ts(q,s),t),ts(q,ts(s,t)),assts1(p,r,t),assts1(q,s,t))):is(ts(pl(ts(p,r),ts(q,s)),t),pl(ts(p,ts(r,t)),ts(q,ts(s,t))))
t2:=tris(cut,pl(ts(q,ts(s,t)),ts(q,ts(r,u))),pl(ts(q,ts(r,u)),ts(q,ts(s,t))),ts(q,pl(ts(r,u),ts(s,t))),compl(ts(q,ts(s,t)),ts(q,ts(r,u))),distpt2(q,ts(r,u),ts(s,t))):is(pl(ts(q,ts(s,t)),ts(q,ts(r,u))),ts(q,pl(ts(r,u),ts(s,t))))
t3:=tr3is(cut,pl(ts(pl(ts(p,r),ts(q,s)),t),ts(pl(ts(p,s),ts(q,r)),u)),pl(pl(ts(p,ts(r,t)),ts(q,ts(s,t))),pl(ts(p,ts(s,u)),ts(q,ts(r,u)))),pl(pl(ts(p,ts(r,t)),ts(p,ts(s,u))),pl(ts(q,ts(s,t)),ts(q,ts(r,u)))),pl(ts(p,pl(ts(r,t),ts(s,u))),ts(q,pl(ts(r,u),ts(s,t)))),ispl12(ts(pl(ts(p,r),ts(q,s)),t),pl(ts(p,ts(r,t)),ts(q,ts(s,t))),ts(pl(ts(p,s),ts(q,r)),u),pl(ts(p,ts(s,u)),ts(q,ts(r,u))),t1,t1(p,q,s,r,u,t)),4pl23(ts(p,ts(r,t)),ts(q,ts(s,t)),ts(p,ts(s,u)),ts(q,ts(r,u))),ispl12(pl(ts(p,ts(r,t)),ts(p,ts(s,u))),ts(p,pl(ts(r,t),ts(s,u))),pl(ts(q,ts(s,t)),ts(q,ts(r,u))),ts(q,pl(ts(r,u),ts(s,t))),distpt2(p,ts(r,t),ts(s,u)),t2)):is(pl(ts(pl(ts(p,r),ts(q,s)),t),ts(pl(ts(p,s),ts(q,r)),u)),pl(ts(p,pl(ts(r,t),ts(s,u))),ts(q,pl(ts(r,u),ts(s,t)))))
-4d199
c@satzd199:=tr3eq(td(td(a,b),c),df(pl(ts(pl(ts(1a,1b),ts(2a,2b)),1c),ts(pl(ts(1a,2b),ts(2a,1b)),2c)),pl(ts(pl(ts(1a,1b),ts(2a,2b)),2c),ts(pl(ts(1a,2b),ts(2a,1b)),1c))),df(pl(ts(1a,pl(ts(1b,1c),ts(2b,2c))),ts(2a,pl(ts(1b,2c),ts(2b,1c)))),pl(ts(1a,pl(ts(1b,2c),ts(2b,1c))),ts(2a,pl(ts(1b,1c),ts(2b,2c))))),td(a,td(b,c)),tdeq2a(c,pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b))),eqsmsd(pl(ts(pl(ts(1a,1b),ts(2a,2b)),1c),ts(pl(ts(1a,2b),ts(2a,1b)),2c)),pl(ts(pl(ts(1a,1b),ts(2a,2b)),2c),ts(pl(ts(1a,2b),ts(2a,1b)),1c)),pl(ts(1a,pl(ts(1b,1c),ts(2b,2c))),ts(2a,pl(ts(1b,2c),ts(2b,1c)))),pl(ts(1a,pl(ts(1b,2c),ts(2b,1c))),ts(2a,pl(ts(1b,1c),ts(2b,2c)))),t3".4d199"(1a,2a,1b,2b,1c,2c),t3".4d199"(1a,2a,1b,2b,2c,1c)),tdeq1b(a,pl(ts(1b,1c),ts(2b,2c)),pl(ts(1b,2c),ts(2b,1c)))):eq(td(td(a,b),c),td(a,td(b,c)))
asstd1:=satzd199:eq(td(td(a,b),c),td(a,td(b,c)))
asstd2:=symeq(td(td(a,b),c),td(a,td(b,c)),satzd199):eq(td(a,td(b,c)),td(td(a,b),c))
+4d201
@[p:cut][q:cut][r:cut][s:cut][t:cut][u:cut]
t1:=tris(cut,pl(ts(p,pl(r,t)),ts(q,pl(s,u))),pl(pl(ts(p,r),ts(p,t)),pl(ts(q,s),ts(q,u))),pl(pl(ts(p,r),ts(q,s)),pl(ts(p,t),ts(q,u))),ispl12(ts(p,pl(r,t)),pl(ts(p,r),ts(p,t)),ts(q,pl(s,u)),pl(ts(q,s),ts(q,u)),disttp2(p,r,t),disttp2(q,s,u)),4pl23(ts(p,r),ts(p,t),ts(q,s),ts(q,u))):is(pl(ts(p,pl(r,t)),ts(q,pl(s,u))),pl(pl(ts(p,r),ts(q,s)),pl(ts(p,t),ts(q,u))))
-4d201
satzd201:=tr3eq(td(a,pd(b,c)),df(pl(ts(1a,pl(1b,1c)),ts(2a,pl(2b,2c))),pl(ts(1a,pl(2b,2c)),ts(2a,pl(1b,1c)))),df(pl(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,1c),ts(2a,2c))),pl(pl(ts(1a,2b),ts(2a,1b)),pl(ts(1a,2c),ts(2a,1c)))),pd(td(a,b),td(a,c)),tdeq1a(a,pl(1b,1c),pl(2b,2c)),eqsmsd(pl(ts(1a,pl(1b,1c)),ts(2a,pl(2b,2c))),pl(ts(1a,pl(2b,2c)),ts(2a,pl(1b,1c))),pl(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,1c),ts(2a,2c))),pl(pl(ts(1a,2b),ts(2a,1b)),pl(ts(1a,2c),ts(2a,1c))),t1".4d201"(1a,2a,1b,2b,1c,2c),t1".4d201"(1a,2a,2b,1b,2c,1c)),pdeq12b(pl(ts(1a,1b),ts(2a,2b)),pl(ts(1a,2b),ts(2a,1b)),pl(ts(1a,1c),ts(2a,2c)),pl(ts(1a,2c),ts(2a,1c)))):eq(td(a,pd(b,c)),pd(td(a,b),td(a,c)))
disttpd1:=tr3eq(td(pd(a,b),c),td(c,pd(a,b)),pd(td(c,a),td(c,b)),pd(td(a,c),td(b,c)),comtd(pd(a,b),c),satzd201(c,a,b),eqpd12(td(c,a),td(a,c),td(c,b),td(b,c),comtd(c,a),comtd(c,b))):eq(td(pd(a,b),c),pd(td(a,c),td(b,c)))
disttpd2:=satzd201:eq(td(a,pd(b,c)),pd(td(a,b),td(a,c)))
distptd1:=symeq(td(pd(a,b),c),pd(td(a,c),td(b,c)),disttpd1):eq(pd(td(a,c),td(b,c)),td(pd(a,b),c))
distptd2:=symeq(td(a,pd(b,c)),pd(td(a,b),td(a,c)),disttpd2):eq(pd(td(a,b),td(a,c)),td(a,pd(b,c)))
satzd202:=treq(td(a,md(b,c)),pd(td(a,b),td(a,m0d(c))),md(td(a,b),td(a,c)),disttpd2(a,b,m0d(c)),eqpd2(td(a,m0d(c)),m0d(td(a,c)),td(a,b),satzd197b(a,c))):eq(td(a,md(b,c)),md(td(a,b),td(a,c)))
disttmd1:=treq(td(md(a,b),c),pd(td(a,c),td(m0d(b),c)),md(td(a,c),td(b,c)),disttpd1(a,m0d(b),c),eqpd2(td(m0d(b),c),m0d(td(b,c)),td(a,c),satzd197a(b,c))):eq(td(md(a,b),c),md(td(a,c),td(b,c)))
disttmd2:=satzd202:eq(td(a,md(b,c)),md(td(a,b),td(a,c)))
distmtd1:=symeq(td(md(a,b),c),md(td(a,c),td(b,c)),disttmd1):eq(md(td(a,c),td(b,c)),td(md(a,b),c))
distmtd2:=symeq(td(a,md(b,c)),md(td(a,b),td(a,c)),disttmd2):eq(md(td(a,b),td(a,c)),td(a,md(b,c)))
satzd200:=satzd202:eq(td(a,md(b,c)),md(td(a,b),td(a,c)))
[m:mored(a,b)]
+4d203
t1:=satzd182d(a,b,m):posd(md(a,b))
-4d203
[p:posd(c)]
+*4d203
p@t2:=eqposd(td(md(a,b),c),md(td(a,c),td(b,c)),disttmd1(a,b,c),ptdpp(md(a,b),c,t1,p)):posd(md(td(a,c),td(b,c)))
-4d203
p@satzd203a:=satzd182a(td(a,c),td(b,c),t2".4d203"):mored(td(a,c),td(b,c))
m@[z:zero(c)]
satzd203b:=zeroeq(td(a,c),td(b,c),td02(a,c,z),td02(b,c,z)):eq(td(a,c),td(b,c))
m@[n:negd(c)]
+*4d203
n@t3:=eqnegd(td(md(a,b),c),md(td(a,c),td(b,c)),disttmd1(a,b,c),ntdpn(md(a,b),c,t1,n)):negd(md(td(a,c),td(b,c)))
-4d203
n@satzd203c:=satzd182c(td(a,c),td(b,c),t3".4d203"):lessd(td(a,c),td(b,c))
p@satzd203d:=eqmored12(td(a,c),td(c,a),td(b,c),td(c,b),comtd(a,c),comtd(b,c),satzd203a):mored(td(c,a),td(c,b))
z@satzd203e:=zeroeq(td(c,a),td(c,b),td01(c,a,z),td01(c,b,z)):eq(td(c,a),td(c,b))
n@satzd203f:=eqlessd12(td(a,c),td(c,a),td(b,c),td(c,b),comtd(a,c),comtd(b,c),satzd203c):lessd(td(c,a),td(c,b))
c@[l:lessd(a,b)][p:posd(c)]
satzd203g:=lemmad5(td(b,c),td(a,c),satzd203a(b,a,c,lemmad6(a,b,l),p)):lessd(td(a,c),td(b,c))
l@[z:zero(c)]
satzd203h:=zeroeq(td(a,c),td(b,c),td02(a,c,z),td02(b,c,z)):eq(td(a,c),td(b,c))
l@[n:negd(c)]
satzd203j:=lemmad6(td(b,c),td(a,c),satzd203c(b,a,c,lemmad6(a,b,l),n)):mored(td(a,c),td(b,c))
p@satzd203k:=lemmad5(td(c,b),td(c,a),satzd203d(b,a,c,lemmad6(a,b,l),p)):lessd(td(c,a),td(c,b))
z@satzd203l:=zeroeq(td(c,a),td(c,b),td01(c,a,z),td01(c,b,z)):eq(td(c,a),td(c,b))
n@satzd203m:=lemmad6(td(c,b),td(c,a),satzd203f(b,a,c,lemmad6(a,b,l),n)):mored(td(c,a),td(c,b))
+*iv4d
@[p:cut][q:cut]
t19:=tris(cut,ts(q,pl(p,1rp)),pl(ts(q,p),ts(q,1rp)),pl(ts(q,p),q),disttp2(q,p,1rp),ispl2(ts(q,1rp),q,ts(q,p),satz151(q))):is(ts(q,pl(p,1rp)),pl(ts(q,p),q))
[r:cut]
t20:=tris(cut,pl(ts(q,pl(p,1rp)),ts(r,1rp)),pl(pl(ts(q,p),q),r),pl(ts(q,p),pl(q,r)),ispl12(ts(q,pl(p,1rp)),pl(ts(q,p),q),ts(r,1rp),r,t19,satz151(r)),asspl1(ts(q,p),q,r)):is(pl(ts(q,pl(p,1rp)),ts(r,1rp)),pl(ts(q,p),pl(q,r)))
t21:=tr3is(cut,pl(ts(r,1rp),ts(q,pl(p,1rp))),pl(ts(q,pl(p,1rp)),ts(r,1rp)),pl(ts(q,p),pl(q,r)),pl(ts(q,p),pl(r,q)),compl(ts(r,1rp),ts(q,pl(p,1rp))),t20,ispl2(pl(q,r),pl(r,q),ts(q,p),compl(q,r))):is(pl(ts(r,1rp),ts(q,pl(p,1rp))),pl(ts(q,p),pl(r,q)))
a@[p:posd(a)]
arp:=rpofpd(a,p):cut
arpi:=ov(1rp,arp):cut
ai:=pdofrp(arpi):dif
t22:=tr3eq(td(a,ai),df(pl(ts(1a,pl(arpi,1rp)),ts(2a,1rp)),pl(ts(1a,1rp),ts(2a,pl(arpi,1rp)))),df(pl(ts(1a,arpi),pl(1a,2a)),pl(ts(2a,arpi),pl(1a,2a))),df(ts(1a,arpi),ts(2a,arpi)),tdeq1a(a,pl(arpi,1rp),1rp),eqsmsd(pl(ts(1a,pl(arpi,1rp)),ts(2a,1rp)),pl(ts(1a,1rp),ts(2a,pl(arpi,1rp))),pl(ts(1a,arpi),pl(1a,2a)),pl(ts(2a,arpi),pl(1a,2a)),t20(arpi,1a,2a),t21(arpi,2a,1a)),lemmad2(ts(1a,arpi),ts(2a,arpi),pl(1a,2a))):eq(td(a,ai),df(ts(1a,arpi),ts(2a,arpi)))
t23:=tr3is(cut,ts(1a,arpi),ts(pl(2a,arp),arpi),pl(ts(2a,arpi),ts(arp,arpi)),pl(ts(2a,arpi),1rp),ists1(1a,pl(2a,arp),arpi,satz140d(1a,2a,p)),disttp1(2a,arp,arpi),ispl2(ts(arp,arpi),1rp,ts(2a,arpi),satz153c(1rp,arp))):is(ts(1a,arpi),pl(ts(2a,arpi),1rp))
t24:=tr3is(cut,pl(ts(1a,arpi),1rp),pl(pl(ts(2a,arpi),1rp),1rp),pl(ts(2a,arpi),pl(1rp,1rp)),pl(pl(1rp,1rp),ts(2a,arpi)),ispl1(ts(1a,arpi),pl(ts(2a,arpi),1rp),1rp,t23),asspl1(ts(2a,arpi),1rp,1rp),compl(ts(2a,arpi),pl(1rp,1rp))):is(pl(ts(1a,arpi),1rp),pl(pl(1rp,1rp),ts(2a,arpi)))
t25:=eqi12(ts(1a,arpi),ts(2a,arpi),pl(1rp,1rp),1rp,t24):eq(df(ts(1a,arpi),ts(2a,arpi)),1df)
t26:=treq(td(a,ai),df(ts(1a,arpi),ts(2a,arpi)),1df,t22,t25):eq(td(a,ai),1df)
t27:=somei(dif,[x:dif]eq(td(a,x),1df),ai,t26):some"l"(dif,[x:dif]eq(td(a,x),1df))
a@[n:negd(a)]
t28:=satzd176c(a,n):posd(m0d(a))
[h:dif][e:eq(td(m0d(a),h),1df)]
t29:=treq(td(a,m0d(h)),td(m0d(a),h),1df,satzd197d(a,h),e):eq(td(a,m0d(h)),1df)
t30:=somei(dif,[x:dif]eq(td(a,x),1df),m0d(h),t29):some"l"(dif,[x:dif]eq(td(a,x),1df))
n@t31:=someapp(dif,[x:dif]eq(td(m0d(a),x),1df),t27(m0d(a),t28),some"l"(dif,[x:dif]eq(td(a,x),1df)),[x:dif][t:eq(td(m0d(a),x),1df)]t30(x,t)):some"l"(dif,[x:dif]eq(td(a,x),1df))
-iv4d
a@[n:not(zero(a))]
lemmad7:=rappd(a,some"l"(dif,[x:dif]eq(td(a,x),1df)),[t:posd(a)]t27".iv4d"(t),th2"l.imp"(zero(a),some"l"(dif,[x:dif]eq(td(a,x),1df)),n),[t:negd(a)]t31".iv4d"(t)):some"l"(dif,[x:dif]eq(td(a,x),1df))
b@[n:not(zero(b))][h:dif][k:dif][e:eq(td(b,h),a)][f:eq(td(b,k),a)]
+4d204
t1:=treq2(td(b,h),td(b,k),a,e,f):eq(td(b,h),td(b,k))
t2:=eqzero(md(td(b,h),td(b,k)),td(b,md(h,k)),distmtd2(b,h,k),satzd182e(td(b,h),td(b,k),t1)):zero(td(b,md(h,k)))
t3:=ore2(zero(b),zero(md(h,k)),satzd192c(b,md(h,k),t2),n):zero(md(h,k))
-4d204
satzd204b:=satzd182b(h,k,t3".4d204"):eq(h,k)
+*4d204
n@[h:dif][e:eq(td(b,h),1df)]
t4:=tr3eq(td(b,td(h,a)),td(td(b,h),a),td(1df,a),a,asstd2(b,h,a),eqtd1(td(b,h),1df,a,e),satzd195b(a)):eq(td(b,td(h,a)),a)
t5:=somei(dif,[x:dif]eq(td(b,x),a),td(h,a),t4):some"l"(dif,[x:dif]eq(td(b,x),a))
-4d204
n@satzd204a:=someapp(dif,[x:dif]eq(td(b,x),1df),lemmad7(b,n),some"l"(dif,[x:dif]eq(td(b,x),a)),[x:dif][t:eq(td(b,x),1df)]t5".4d204"(x,t)):some"l"(dif,[x:dif]eq(td(b,x),a))
@[r:cut][s:cut][m:more(r,s)]
+iv5d
t1:=ismore12(pl(r,pl(1rp,1rp)),pl(pl(r,1rp),1rp),pl(s,pl(1rp,1rp)),pl(pl(s,1rp),1rp),asspl2(r,1rp,1rp),asspl2(s,1rp,1rp),satz134(r,s,pl(1rp,1rp),m)):more(pl(pl(r,1rp),1rp),pl(pl(s,1rp),1rp))
-iv5d
morerpepd:=moredi12(pl(r,1rp),1rp,pl(s,1rp),1rp,t1".iv5d"):mored(pdofrp(r),pdofrp(s))
s@[m:mored(pdofrp(r),pdofrp(s))]
+*iv5d
m@t2:=morede12(pl(r,1rp),1rp,pl(s,1rp),1rp,m):more(pl(pl(r,1rp),1rp),pl(pl(s,1rp),1rp))
-iv5d
m@morerpipd:=satz136a(r,s,pl(1rp,1rp),ismore12(pl(pl(r,1rp),1rp),pl(r,pl(1rp,1rp)),pl(pl(s,1rp),1rp),pl(s,pl(1rp,1rp)),asspl1(r,1rp,1rp),asspl1(s,1rp,1rp),t2".iv5d")):more(r,s)
s@[l:less(r,s)]
lessrpepd:=lemmad5(pdofrp(s),pdofrp(r),morerpepd(s,r,satz122(r,s,l))):lessd(pdofrp(r),pdofrp(s))
s@[l:lessd(pdofrp(r),pdofrp(s))]
lessrpipd:=satz121(s,r,morerpipd(s,r,lemmad6(pdofrp(r),pdofrp(s),l))):less(r,s)
+*iv5d
@i:=1rp:cut
2:=pl(i,i):cut
r@rp1:=pl(r,i):cut
s@sp1:=pl(s,i):cut
rps:=pl(r,s):cut
rs:=ts(r,s):cut
t3:=tris(cut,pl(pl(rp1,sp1),i),pl(pl(rps,2),i),pl(pl(rps,i),2),ispl1(pl(rp1,sp1),pl(rps,2),i,4pl23(r,i,s,i)),3pl23(rps,2,i)):is(pl(pl(rp1,sp1),i),pl(pl(rps,i),2))
t4:=treq(pd(pdofrp(r),pdofrp(s)),df(pl(rp1,sp1),2),pdofrp(rps),pdeq12a(rp1,i,sp1,i),eqi12(pl(rp1,sp1),2,pl(rps,i),i,t3)):eq(pd(pdofrp(r),pdofrp(s)),pdofrp(rps))
-iv5d
s@lemmad8:=t4".iv5d":eq(pd(pdofrp(r),pdofrp(s)),pdofrp(pl(r,s)))
+*iv5d
s@t5:=tris(cut,ts(r,sp1),pl(rs,ts(r,i)),pl(rs,r),disttp2(r,s,i),ispl2(ts(r,i),r,rs,satz151(r))):is(ts(r,sp1),pl(rs,r))
t6:=tr4is(cut,ts(rp1,sp1),pl(ts(r,sp1),ts(i,sp1)),pl(pl(rs,r),sp1),pl(pl(pl(rs,r),s),i),pl(pl(rs,rps),i),disttp1(r,i,sp1),ispl12(ts(r,sp1),pl(rs,r),ts(i,sp1),sp1,t5,satz151b(sp1)),asspl2(pl(rs,r),s,i),ispl1(pl(pl(rs,r),s),pl(rs,rps),i,asspl1(rs,r,s))):is(ts(rp1,sp1),pl(pl(rs,rps),i))
t7:=tr3is(cut,pl(ts(rp1,sp1),ts(i,i)),pl(pl(pl(rs,rps),i),i),pl(pl(rs,rps),2),pl(rs,pl(rps,2)),ispl12(ts(rp1,sp1),pl(pl(rs,rps),i),ts(i,i),i,t6,satz151(i)),asspl1(pl(rs,rps),i,i),asspl1(rs,rps,2)):is(pl(ts(rp1,sp1),ts(i,i)),pl(rs,pl(rps,2)))
t8:=tris(cut,pl(ts(rp1,i),ts(i,sp1)),pl(rp1,sp1),pl(rps,2),ispl12(ts(rp1,i),rp1,ts(i,sp1),sp1,satz151(rp1),satz151b(sp1)),4pl23(r,i,s,i)):is(pl(ts(rp1,i),ts(i,sp1)),pl(rps,2))
t9:=tris(cut,pl(pl(rs,i),pl(ts(rp1,i),ts(i,sp1))),pl(pl(rs,i),pl(rps,2)),pl(pl(rs,pl(rps,2)),i),ispl2(pl(ts(rp1,i),ts(i,sp1)),pl(rps,2),pl(rs,i),t8),3pl23(rs,i,pl(rps,2))):is(pl(pl(rs,i),pl(ts(rp1,i),ts(i,sp1))),pl(pl(rs,pl(rps,2)),i))
t10:=tris2(cut,pl(pl(ts(rp1,sp1),ts(i,i)),i),pl(pl(rs,i),pl(ts(rp1,i),ts(i,sp1))),pl(pl(rs,pl(rps,2)),i),ispl1(pl(ts(rp1,sp1),ts(i,i)),pl(rs,pl(rps,2)),i,t7),t9):is(pl(pl(ts(rp1,sp1),ts(i,i)),i),pl(pl(rs,i),pl(ts(rp1,i),ts(i,sp1))))
t11:=treq(td(pdofrp(r),pdofrp(s)),df(pl(ts(rp1,sp1),ts(i,i)),pl(ts(rp1,i),ts(i,sp1))),pdofrp(rs),tdeq12a(rp1,i,sp1,i),eqi12(pl(ts(rp1,sp1),ts(i,i)),pl(ts(rp1,i),ts(i,sp1)),pl(rs,i),i,t10)):eq(td(pdofrp(r),pdofrp(s)),pdofrp(rs))
-iv5d
s@lemmad9:=t11".iv5d":eq(td(pdofrp(r),pdofrp(s)),pdofrp(ts(r,s)))
@[r:cut][s0:set(cut)]
in:=esti(cut,r,s0):'prop'
@[s0:set(cut)][t0:set(cut)][p0:all([x:cut]or(in(x,s0),in(x,t0)))][p1a:nonempty(cut,s0)][p1b:nonempty(cut,t0)][p2:all([x:cut][t:in(x,s0)]all([y:cut][u:in(y,t0)]less(x,y)))]
+5p205
t0@[r:cut]
prop1:=all([x:cut][t:less(x,r)]in(x,s0)):'prop'
prop2:=all([x:cut][t:more(x,r)]in(x,t0)):'prop'
prop3:=and(prop1,prop2):'prop'
p2@[r1:cut][r2:cut][pr1:prop3(r1)][pr2:prop3(r2)]
t1:=ande2(prop1(r1),prop2(r1),pr1):prop2(r1)
t2:=ande1(prop1(r2),prop2(r2),pr2):prop1(r2)
[l:less(r1,r2)][x0:rat]
rx:=rpofrt(x0):cut
[l1:less(r1,rx)][l2:less(rx,r2)]
t3:=<l2><rx>t2:in(rx,s0)
t4:=<satz122(r1,rx,l1)><rx>t1:in(rx,t0)
t5:=<refis(cut,rx)>ec3e31(is(rx,rx),more(rx,rx),less(rx,rx),satz123b(rx,rx),<t4><rx><t3><rx>p2):con
l@t6:=satz159app(r1,r2,l,con,[x:rat][t:less(r1,rpofrt(x))][u:less(rpofrt(x),r2)]t5(x,t,u)):con
pr2@t7:=[t:less(r1,r2)]t6(t):not(less(r1,r2))
t8:=[t:more(r1,r2)]t6(r2,r1,pr2,pr1,satz121(r1,r2,t)):not(more(r1,r2))
t9:=or3e1(is(r1,r2),more(r1,r2),less(r1,r2),satz123a(r1,r2),t8,t7):is(r1,r2)
p2@t10:=[x:cut][y:cut][t:prop3(x)][u:prop3(y)]t9(x,y,t,u):amone(cut,[x:cut]prop3(x))
[x0:rat]
schnittprop:=some([y:cut]and(in(y,s0),lrt(y,x0))):'prop'
p2@schnittset:=setof(rat,[x:rat]schnittprop(x)):set(rat)
[r:cut][i:in(r,s0)][x0:rat][lx:lrt(r,x0)]
t11:=andi(in(r,s0),lrt(r,x0),i,lx):and(in(r,s0),lrt(r,x0))
t12:=somei(cut,[y:cut]and(in(y,s0),lrt(y,x0)),r,t11):schnittprop(x0)
t13:=estii(rat,[x:rat]schnittprop(x),x0,t12):in"rt"(x0,schnittset)
r@[i:in(r,t0)][x0:rat][ux:urt(r,x0)][s:cut][j:in(s,s0)]
t14:=satz122(s,r,<i><r><j><s>p2):more(r,s)
t15:=satz158b(r,x0,ux):moreis(rpofrt(x0),r)
t16:=moreisi1(rpofrt(x0),s,satz127c(rpofrt(x0),r,s,t15,t14)):moreis(rpofrt(x0),s)
t17:=satz158d(s,x0,t16):urt(s,x0)
s@t18:=weli(ec(in(s,s0),lrt(s,x0)),[t:in(s,s0)]t17(t)):not(and(in(s,s0),lrt(s,x0)))
ux@t19:=th5"l.some"(cut,[y:cut]and(in(y,s0),lrt(y,x0)),[y:cut]t18(y)):not(schnittprop(x0))
t20:=th3"l.imp"(in"rt"(x0,schnittset),schnittprop(x0),t19,[t:in"rt"(x0,schnittset)]estie(rat,[x:rat]schnittprop(x),x0,t)):not(in"rt"(x0,schnittset))
p2@[x0:rat][i:in"rt"(x0,schnittset)][y0:rat][l:less"rt"(y0,x0)]
i@t21:=estie(rat,[x:rat]schnittprop(x),x0,i):schnittprop(x0)
l@[r:cut][a:and(in(r,s0),lrt(r,x0))]
t22:=ande1(in(r,s0),lrt(r,x0),a):in(r,s0)
t23:=ande2(in(r,s0),lrt(r,x0),a):lrt(r,x0)
t24:=satz120(r,x0,t23,y0,l):lrt(r,y0)
t25:=andi(in(r,s0),lrt(r,y0),t22,t24):and(in(r,s0),lrt(r,y0))
t26:=somei(cut,[y:cut]and(in(y,s0),lrt(y,y0)),r,t25):schnittprop(y0)
l@t27:=someapp(cut,[y:cut]and(in(y,s0),lrt(y,x0)),t21,schnittprop(y0),[y:cut][r:and(in(y,s0),lrt(y,x0))]t26(y,r)):schnittprop(y0)
t28:=estii(rat,[x:rat]schnittprop(x),y0,t27):in"rt"(y0,schnittset)
i@[r:cut][a:and(in(r,s0),lrt(r,x0))]
t29:=ande1(in(r,s0),lrt(r,x0),a):in(r,s0)
t30:=ande2(in(r,s0),lrt(r,x0),a):lrt(r,x0)
[y0:rat][ly:lrt(r,y0)][l:less"rt"(x0,y0)]
t31:=andi(in(r,s0),lrt(r,y0),t29,ly):and(in(r,s0),lrt(r,y0))
t32:=somei(cut,[y:cut]and(in(y,s0),lrt(y,y0)),r,t31):schnittprop(y0)
t33:=estii(rat,[x:rat]schnittprop(x),y0,t32):in"rt"(y0,schnittset)
t34:=satz83(x0,y0,l):more"rt"(y0,x0)
t35:=andi(in"rt"(y0,schnittset),more"rt"(y0,x0),t33,t34):and(in"rt"(y0,schnittset),more"rt"(y0,x0))
t36:=somei(rat,[y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0)),y0,t35):some"rt"([y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0)))
a@t37:=cutapp3(r,x0,t30,some"rt"([y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0))),[y:rat][t:lrt(r,y)][u:less"rt"(x0,y)]t36(y,t,u)):some"rt"([y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0)))
i@t38:=someapp(cut,[y:cut]and(in(y,s0),lrt(y,x0)),t21,some"rt"([y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0))),[y:cut][t:and(in(y,s0),lrt(y,x0))]t37(y,t)):some"rt"([y:rat]and(in"rt"(y,schnittset),more"rt"(y,x0)))
p2@[r:cut][i:in(r,s0)][x0:rat][lx:lrt(r,x0)][s:cut][j:in(s,t0)][y0:rat][uy:urt(s,y0)]
t39:=cut2(schnittset,x0,t13(r,i,x0,lx),y0,t20(s,j,y0,uy),[x:rat][t:in"rt"(x,schnittset)][y:rat][u:less"rt"(y,x)]t28(x,t,y,u),[x:rat][t:in"rt"(x,schnittset)]t38(x,t)):cutprop(schnittset)
j@t40:=cutapp1b(s,cutprop(schnittset),[x:rat][t:urt(s,x)]t39(x,t)):cutprop(schnittset)
lx@t41:=nonemptyapp(cut,t0,p1b,cutprop(schnittset),[y:cut][t:in(y,t0)]t40(y,t)):cutprop(schnittset)
i@t42:=cutapp1a(r,cutprop(schnittset),[x:rat][t:lrt(r,x)]t41(x,t)):cutprop(schnittset)
p2@t43:=nonemptyapp(cut,s0,p1a,cutprop(schnittset),[y:cut][t:in(y,s0)]t42(y,t)):cutprop(schnittset)
snt:=cutof(schnittset,t43):cut
[r:cut][l:less(r,snt)][x0:rat][ux:urt(r,x0)][lx:lrt(snt,x0)]
t44:=ini(schnittset,t43,x0,lx):in"rt"(x0,schnittset)
t45:=estie(rat,[x:rat]schnittprop(x),x0,t44):schnittprop(x0)
[s:cut][a:and(in(s,s0),lrt(s,x0))]
t46:=ande1(in(s,s0),lrt(s,x0),a):in(s,s0)
t47:=ande2(in(s,s0),lrt(s,x0),a):lrt(s,x0)
t48:=andi(urt(r,x0),lrt(s,x0),ux,t47):and(urt(r,x0),lrt(s,x0))
t49:=somei(rat,[x:rat]and(urt(r,x),lrt(s,x)),x0,t48):less(r,s)
t50:=ec3e23(is(s,r),more(s,r),less(s,r),satz123b(s,r),satz122(r,s,t49)):not(less(s,r))
t51:=th3"l.imp"(in(r,t0),less(s,r),t50,<r><t46><s>p2):not(in(r,t0))
t52:=ore1(in(r,s0),in(r,t0),<r>p0,t51):in(r,s0)
lx@t53:=someapp(cut,[y:cut]and(in(y,s0),lrt(y,x0)),t45,in(r,s0),[y:cut][t:and(in(y,s0),lrt(y,x0))]t52(y,t)):in(r,s0)
l@t54:=lessapp(r,snt,l,in(r,s0),[x:rat][t:urt(r,x)][u:lrt(snt,x)]t53(x,t,u)):in(r,s0)
r@[m:more(r,snt)][x0:rat][lx:lrt(r,x0)][ux:urt(snt,x0)][i:in(r,s0)]
t55:=andi(in(r,s0),lrt(r,x0),i,lx):and(in(r,s0),lrt(r,x0))
t56:=somei(cut,[y:cut]and(in(y,s0),lrt(y,x0)),r,t55):schnittprop(x0)
t57:=estii(rat,[x:rat]schnittprop(x),x0,t56):in"rt"(x0,schnittset)
t58:=ine(schnittset,t43,x0,t57):lrt(snt,x0)
ux@t59:=th3"l.imp"(in(r,s0),lrt(snt,x0),ux,[t:in(r,s0)]t58(t)):not(in(r,s0))
t60:=ore2(in(r,s0),in(r,t0),<r>p0,t59):in(r,t0)
m@t61:=moreapp(r,snt,m,in(r,t0),[x:rat][t:lrt(r,x)][u:urt(snt,x)]t60(x,t,u)):in(r,t0)
p2@t62:=andi(prop1(snt),prop2(snt),[x:cut][t:less(x,snt)]t54(x,t),[x:cut][t:more(x,snt)]t61(x,t)):prop3(snt)
t63:=somei(cut,[x:cut]prop3(x),snt,t62):some([x:cut]prop3(x))
-5p205
satzp205:=onei(cut,[x:cut]prop3".5p205"(x),t10".5p205",t63".5p205"):one([x:cut]and(all([y:cut][t:less(y,x)]in(y,s0)),all([y:cut][t:more(y,x)]in(y,t0))))
schnitt:=ind(cut,[x:cut]prop3".5p205"(x),satzp205):cut
satzp205a:=ande1(prop1".5p205"(schnitt),prop2".5p205"(schnitt),oneax(cut,[x:cut]prop3".5p205"(x),satzp205)):all([x:cut][t:less(x,schnitt)]in(x,s0))
satzp205b:=ande2(prop1".5p205"(schnitt),prop2".5p205"(schnitt),oneax(cut,[x:cut]prop3".5p205"(x),satzp205)):all([x:cut][t:more(x,schnitt)]in(x,t0))
@[r:cut][s:cut]
+ivad
@i:=1rp:cut
r@r1:=pl(r,i):cut
s@s1:=pl(s,i):cut
rps:=pl(r,s):cut
@2:=pl(i,i):cut
s@t1:=pdeq12a(r1,i,s1,i):eq(pd(pdofrp(r),pdofrp(s)),df(pl(r1,s1),2))
t2:=tris(cut,pl(r1,s1),pl(rps,2),pl(pl(rps,i),i),4pl23(r,i,s,i),asspl2(rps,i,i)):is(pl(r1,s1),pl(pl(rps,i),i))
t3:=ispl1(pl(r1,s1),pl(pl(rps,i),i),i,t2):is(pl(pl(r1,s1),i),pl(pl(pl(rps,i),i),i))
t4:=tris(cut,pl(pl(r1,s1),i),pl(pl(pl(rps,i),i),i),pl(pl(rps,i),2),t3,asspl1(pl(rps,i),i,i)):is(pl(pl(r1,s1),i),pl(pl(rps,i),2))
t5:=eqi12(pl(r1,s1),2,pl(rps,i),i,t4):eq(df(pl(r1,s1),2),pdofrp(rps))
-ivad
lemmaivad1:=treq(pd(pdofrp(r),pdofrp(s)),df(pl(pl(r,1rp),pl(s,1rp)),pl(1rp,1rp)),pdofrp(pl(r,s)),t1".ivad",t5".ivad"):eq(pd(pdofrp(r),pdofrp(s)),pdofrp(pl(r,s)))
+*ivad
s@rs:=ts(r,s):cut
t6:=tdeq12a(r1,i,s1,i):eq(td(pdofrp(r),pdofrp(s)),df(pl(ts(r1,s1),ts(i,i)),pl(ts(r1,i),ts(i,s1))))
t7:=tris(cut,ts(r1,s),pl(rs,ts(i,s)),pl(rs,s),disttp1(r,i,s),ispl2(ts(i,s),s,rs,satz151b(s))):is(ts(r1,s),pl(rs,s))
t8:=tr3is(cut,ts(r1,s1),pl(ts(r1,s),ts(r1,i)),pl(pl(rs,s),r1),pl(pl(rs,i),rps),disttp2(r1,s,i),ispl12(ts(r1,s),pl(rs,s),ts(r1,i),r1,t7,satz151(r1)),4pl24(rs,s,r,i)):is(ts(r1,s1),pl(pl(rs,i),rps))
t9:=tris(cut,pl(ts(r1,s1),ts(i,i)),pl(pl(pl(rs,i),rps),i),pl(pl(rs,i),pl(rps,i)),ispl12(ts(r1,s1),pl(pl(rs,i),rps),ts(i,i),i,t8,satz151(i)),asspl1(pl(rs,i),rps,i)):is(pl(ts(r1,s1),ts(i,i)),pl(pl(rs,i),pl(rps,i)))
t10:=tris(cut,pl(pl(ts(r1,s1),ts(i,i)),i),pl(pl(pl(rs,i),pl(rps,i)),i),pl(pl(rs,i),pl(pl(rps,i),i)),ispl1(pl(ts(r1,s1),ts(i,i)),pl(pl(rs,i),pl(rps,i)),i,t9),asspl1(pl(rs,i),pl(rps,i),i)):is(pl(pl(ts(r1,s1),ts(i,i)),i),pl(pl(rs,i),pl(pl(rps,i),i)))
t11:=tr3is(cut,pl(pl(rps,i),i),pl(rps,2),pl(r1,s1),pl(ts(r1,i),ts(i,s1)),asspl1(rps,i,i),4pl23(r,s,i,i),ispl12(r1,ts(r1,i),s1,ts(i,s1),satz151a(r1),satz151c(s1))):is(pl(pl(rps,i),i),pl(ts(r1,i),ts(i,s1)))
t12:=tris(cut,pl(pl(ts(r1,s1),ts(i,i)),i),pl(pl(rs,i),pl(pl(rps,i),i)),pl(pl(rs,i),pl(ts(r1,i),ts(i,s1))),t10,ispl2(pl(pl(rps,i),i),pl(ts(r1,i),ts(i,s1)),pl(rs,i),t11)):is(pl(pl(ts(r1,s1),ts(i,i)),i),pl(pl(rs,i),pl(ts(r1,i),ts(i,s1))))
t13:=eqi12(pl(ts(r1,s1),ts(i,i)),pl(ts(r1,i),ts(i,s1)),pl(rs,i),i,t12):eq(df(pl(ts(r1,s1),ts(i,i)),pl(ts(r1,i),ts(i,s1))),pdofrp(rs))
-ivad
s@lemmaivad2:=treq(td(pdofrp(r),pdofrp(s)),df(pl(ts(pl(r,1rp),pl(s,1rp)),ts(1rp,1rp)),pl(ts(pl(r,1rp),1rp),ts(1rp,pl(s,1rp)))),pdofrp(ts(r,s)),t6".ivad",t13".ivad"):eq(td(pdofrp(r),pdofrp(s)),pdofrp(ts(r,s)))
[m:mored(pdofrp(r),pdofrp(s))]
+*ivad
m@t14:=morede12(r1,i,s1,i,m):more(pl(r1,i),pl(s1,i))
t15:=satz136a(r1,s1,i,t14):more(r1,s1)
-ivad
m@lemmaivad3:=satz136a(r,s,1rp,t15".ivad"):more(r,s)
@[c:dif][a:dif][b:dif][n:not(negd(a))][o:not(negd(b))][e:eq(td(a,a),c)][f:eq(td(b,b),c)]
+d161
t1:=treq2(td(a,a),td(b,b),c,e,f):eq(td(a,a),td(b,b))
t2:=treq(pd(md(td(a,a),td(a,b)),td(b,a)),pd(md(td(a,a),td(a,b)),td(a,b)),td(a,a),eqpd2(td(b,a),td(a,b),md(td(a,a),td(a,b)),comtd(b,a)),pdmd(td(a,a),td(a,b))):eq(pd(md(td(a,a),td(a,b)),td(b,a)),td(a,a))
t3:=tr4eq(td(pd(a,b),md(a,b)),pd(td(a,md(a,b)),td(b,md(a,b))),pd(md(td(a,a),td(a,b)),md(td(b,a),td(b,b))),md(pd(md(td(a,a),td(a,b)),td(b,a)),td(b,b)),md(td(a,a),td(b,b)),disttpd1(a,b,md(a,b)),eqpd12(td(a,md(a,b)),md(td(a,a),td(a,b)),td(b,md(a,b)),md(td(b,a),td(b,b)),disttmd2(a,a,b),disttmd2(b,a,b)),asspd2(md(td(a,a),td(a,b)),td(b,a),m0d(td(b,b))),eqmd1(pd(md(td(a,a),td(a,b)),td(b,a)),td(a,a),td(b,b),t2)):eq(td(pd(a,b),md(a,b)),md(td(a,a),td(b,b)))
t4:=eqzero(md(td(a,a),td(b,b)),td(pd(a,b),md(a,b)),symeq(td(pd(a,b),md(a,b)),md(td(a,a),td(b,b)),t3),satzd182e(td(a,a),td(b,b),t1)):zero(td(pd(a,b),md(a,b)))
t5:=satzd192c(pd(a,b),md(a,b),t4):or(zero(pd(a,b)),zero(md(a,b)))
[z:zero(a)]
t6:=eqzero(td(a,a),td(b,b),t1,td01(a,a,z)):zero(td(b,b))
t7:=th1"l.imp"(zero(b),zero(b),refimp(zero(b)),satzd192c(b,b,t6)):zero(b)
t8:=zeroeq(a,b,z,t7):eq(a,b)
f@[p:not(zero(a))]
t9:=or3e2(zero(a),posd(a),negd(a),axrdo(a),n,p):posd(a)
t10:=th3"l.imp"(zero(b),zero(a),p,[t:zero(b)]t7(b,a,o,n,f,e,t)):not(zero(b))
t11:=t9(b,a,o,n,f,e,t10):posd(b)
t12:=pnot0d(pd(a,b),ppd(a,b,t9,t11)):not(zero(pd(a,b)))
t13:=ore2(zero(pd(a,b)),zero(md(a,b)),t5,t12):zero(md(a,b))
t14:=satzd182b(a,b,t13):eq(a,b)
-d161
satzd161b:=th1"l.imp"(zero(a),eq(a,b),[t:zero(a)]t8".d161"(t),[t:not(zero(a))]t14".d161"(t)):eq(a,b)
c@[n:not(negd(c))]
+*d161
n@[z:zero(c)]
t15:=zeroeq(td(c,c),c,td01(c,c,z),z):eq(td(c,c),c)
t16:=andi(not(negd(c)),eq(td(c,c),c),n,t15):and(not(negd(c)),eq(td(c,c),c))
t17:=somei(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c)),c,t16):some"l"(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c)))
n@[o:not(zero(c))]
t18:=or3e2(zero(c),posd(c),negd(c),axrdo(c),n,o):posd(c)
crp:=rpofpd(c,t18):cut
srp:=sqrt(crp):cut
s:=pdofrp(srp):dif
t19:=tr3eq(td(s,s),pdofrp(ts(srp,srp)),pdofrp(crp),c,lemmaivad2(srp,srp),isrpepd(ts(srp,srp),crp,thsqrt1(crp)),eqpdrp2(c,t18)):eq(td(s,s),c)
t20:=andi(not(negd(s)),eq(td(s,s),c),pnotnd(s,posdirp(srp)),t19):and(not(negd(s)),eq(td(s,s),c))
t21:=somei(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c)),s,t20):some"l"(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c)))
-d161
n@satzd161a:=th1"l.imp"(zero(c),some"l"(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c))),[t:zero(c)]t17".d161"(t),[t:not(zero(c))]t21".d161"(t)):some"l"(dif,[x:dif]and(not(negd(x)),eq(td(x,x),c)))
@[a:dif][i:intd(a)]
+intd
[z:zero(a)]
t1:=ori1(zero(absd(a)),natd(absd(absd(a))),satzd166f(a,z)):intd(absd(a))
i@[n:natd(absd(a))]
t2:=natintd(absd(a),n):intd(absd(a))
-intd
intabsd:=orapp(zero(a),natd(absd(a)),intd(absd(a)),i,[t:zero(a)]t1".intd"(t),[t:natd(absd(a))]t2".intd"(t)):intd(absd(a))
+*intd
n@t4:=eqnatd(absd(a),absd(m0d(a)),satzd178a(a),n):natd(absd(m0d(a)))
-intd
i@intm0d:=th9"l.or"(zero(a),natd(absd(a)),zero(m0d(a)),natd(absd(m0d(a))),i,[t:zero(a)]satzd176b(a,t),[t:natd(absd(a))]t4".intd"(t)):intd(m0d(a))
a@[b:dif][i:intd(a)][j:intd(b)]
+*intd
j@[z:zero(a)]
t5:=symeq(pd(a,b),b,pd01(a,b,z)):eq(b,pd(a,b))
t6:=eqintd(b,pd(a,b),t5,j):intd(pd(a,b))
j@[z:zero(b)]
t7:=symeq(pd(a,b),a,pd02(a,b,z)):eq(a,pd(a,b))
t8:=eqintd(a,pd(a,b),t7,i):intd(pd(a,b))
a@[i:intd(a)][p:posd(a)]
t9:=<p>ande2(posd(a),[t:posd(a)]natrp(rpofpd(a,t)),posintnatd(a,p,i)):natrp(rpofpd(a,p))
j@[pp:posd(pd(a,b))]
apb1:=rpofpd(pd(a,b),pp):cut
[p:posd(a)]
a1:=rpofpd(a,p):cut
[q:posd(b)]
b1:=rpofpd(b,q):cut
t10:=natpl(a1,t9(a,i,p),b1,t9(b,j,q)):natrp(pl(a1,b1))
t11:=eqpd12(a,pdofrp(a1),b,pdofrp(b1),eqpdrp1(a,p),eqpdrp1(b,q)):eq(pd(a,b),pd(pdofrp(a1),pdofrp(b1)))
t12:=treq(pd(a,b),pd(pdofrp(a1),pdofrp(b1)),pdofrp(pl(a1,b1)),t11,lemmaivad1(a1,b1)):eq(pd(a,b),pdofrp(pl(a1,b1)))
t13:=tris(cut,apb1,rpofpd(pdofrp(pl(a1,b1)),posdirp(pl(a1,b1))),pl(a1,b1),eqpderp(pd(a,b),pp,pdofrp(pl(a1,b1)),posdirp(pl(a1,b1)),t12),isrppd2(pl(a1,b1))):is(apb1,pl(a1,b1))
t14:=isp1(cut,[t:cut]natrp(t),pl(a1,b1),apb1,t10,t13):natrp(apb1)
t15:=andi(posd(pd(a,b)),[t:posd(pd(a,b))]natrp(apb1(t)),pp,[t:posd(pd(a,b))]t14(t,p,q)):natd(pd(a,b))
t16:=natintd(pd(a,b),t15):intd(pd(a,b))
p@[n:negd(b)]
t17:=satzd176c(b,n):posd(m0d(b))
b2:=rpofpd(m0d(b),t17):cut
t18:=eqpd2(b,m0d(m0d(b)),a,satzd177a(b)):eq(pd(a,b),md(a,m0d(b)))
t19:=eqposd(pd(a,b),md(a,m0d(b)),t18,pp):posd(md(a,m0d(b)))
t20:=satzd182a(a,m0d(b),t19):mored(a,m0d(b))
t21:=eqmored12(a,pdofrp(a1),m0d(b),pdofrp(b2),eqpdrp1(a,p),eqpdrp1(m0d(b),t17),t20):mored(pdofrp(a1),pdofrp(b2))
t22:=lemmaivad3(a1,b2,t21):more(a1,b2)
t23:=natmn(a1,t9(a,i,p),b2,t9(m0d(b),intm0d(b,j),t17),t22):natrp(mn(a1,b2,t22))
t24:=eqpd12(m0d(b),pdofrp(b2),pd(a,b),pdofrp(apb1),eqpdrp1(m0d(b),t17),eqpdrp1(pd(a,b),pp)):eq(pd(m0d(b),pd(a,b)),pd(pdofrp(b2),pdofrp(apb1)))
t25:=tr4eq(a,md(pd(a,b),b),pd(m0d(b),pd(a,b)),pd(pdofrp(b2),pdofrp(apb1)),pdofrp(pl(b2,apb1)),symeq(md(pd(a,b),b),a,mdpd(a,b)),compd(pd(a,b),m0d(b)),t24,lemmaivad1(b2,apb1)):eq(a,pdofrp(pl(b2,apb1)))
t26:=tris2(cut,pl(b2,apb1),a1,rpofpd(pdofrp(pl(b2,apb1)),posdirp(pl(b2,apb1))),isrppd1(pl(b2,apb1)),eqpderp(a,p,pdofrp(pl(b2,apb1)),posdirp(pl(b2,apb1)),t25)):is(pl(b2,apb1),a1)
t27:=satz140g(a1,b2,apb1,t22,t26):is(apb1,mn(a1,b2,t22))
t28:=isp1(cut,[t:cut]natrp(t),mn(a1,b2,t22),apb1,t23,t27):natrp(apb1)
t29:=andi(posd(pd(a,b)),[t:posd(pd(a,b))]natrp(apb1(t)),pp,[t:posd(pd(a,b))]t28(t,p,n)):natd(pd(a,b))
t30:=natintd(pd(a,b),t29):intd(pd(a,b))
p@t31:=rappd(b,intd(pd(a,b)),[t:posd(b)]t16(t),[t:zero(b)]t8(t),[t:negd(b)]t30(t)):intd(pd(a,b))
pp@[n:negd(a)]
t31a:=th3"l.imp"(negd(b),negd(pd(a,b)),pnotnd(pd(a,b),pp),[t:negd(b)]npd(a,b,n,t)):not(negd(b))
t32:=th3"l.imp"(zero(b),negd(pd(a,b)),pnotnd(pd(a,b),pp),[t:zero(b)]eqnegd(a,pd(a,b),symeq(pd(a,b),a,pd02(a,b,t)),n)):not(zero(b))
t33:=or3e2(zero(b),posd(b),negd(b),axrdo(b),t31a,t32):posd(b)
t34:=eqposd(pd(a,b),pd(b,a),compd(a,b),pp):posd(pd(b,a))
t35:=t30(b,a,j,i,t34,t33,n):intd(pd(b,a))
t36:=eqintd(pd(b,a),pd(a,b),compd(b,a),t35):intd(pd(a,b))
pp@t37:=rappd(a,intd(pd(a,b)),[t:posd(a)]t31(t),[t:zero(a)]t6(t),[t:negd(a)]t36(t)):intd(pd(a,b))
j@[0p:zero(pd(a,b))]
t38:=intdi0(pd(a,b),0p):intd(pd(a,b))
j@[np:negd(pd(a,b))]
t39:=satzd176c(pd(a,b),np):posd(m0d(pd(a,b)))
t40:=eqposd(m0d(pd(a,b)),pd(m0d(a),m0d(b)),satzd180(a,b),t39):posd(pd(m0d(a),m0d(b)))
t41:=t37(m0d(a),m0d(b),intm0d(a,i),intm0d(b,j),t40):intd(pd(m0d(a),m0d(b)))
t42:=eqintd(pd(m0d(a),m0d(b)),m0d(pd(a,b)),satzd180a(a,b),t41):intd(m0d(pd(a,b)))
t43:=intm0d(m0d(pd(a,b)),t42):intd(m0d(m0d(pd(a,b))))
t44:=eqintd(m0d(m0d(pd(a,b))),pd(a,b),satzd177(pd(a,b)),t43):intd(pd(a,b))
-intd
j@intpd:=rappd(pd(a,b),intd(pd(a,b)),[t:posd(pd(a,b))]t37".intd"(t),[t:zero(pd(a,b))]t38".intd"(t),[t:negd(pd(a,b))]t44".intd"(t)):intd(pd(a,b))
intmd:=intpd(a,m0d(b),i,intm0d(b,j)):intd(md(a,b))
+*intd
j@[n:not(zero(td(a,b)))]
t45:=th3"l.imp"(zero(a),zero(td(a,b)),n,[t:zero(a)]td01(a,b,t)):not(zero(a))
t46:=th3"l.imp"(zero(b),zero(td(a,b)),n,[t:zero(b)]td02(a,b,t)):not(zero(b))
t47:=satzd166e(a,t45):posd(absd(a))
a3:=rpofpd(absd(a),t47):cut
t48:=satzd166e(b,t46):posd(absd(b))
b3:=rpofpd(absd(b),t48):cut
t49:=natts(a3,t9(absd(a),intabsd(a,i),t47),b3,t9(absd(b),intabsd(b,j),t48)):natrp(ts(a3,b3))
t50:=satzd166e(td(a,b),n):posd(absd(td(a,b)))
[p:posd(absd(td(a,b)))]
atb3:=rpofpd(absd(td(a,b)),p):cut
t51:=eqtd12(absd(a),pdofrp(a3),absd(b),pdofrp(b3),eqpdrp1(absd(a),t47),eqpdrp1(absd(b),t48)):eq(td(absd(a),absd(b)),td(pdofrp(a3),pdofrp(b3)))
t52:=tr3eq(absd(td(a,b)),td(absd(a),absd(b)),td(pdofrp(a3),pdofrp(b3)),pdofrp(ts(a3,b3)),satzd193(a,b),t51,lemmaivad2(a3,b3)):eq(absd(td(a,b)),pdofrp(ts(a3,b3)))
t53:=tris2(cut,atb3,ts(a3,b3),rpofpd(pdofrp(ts(a3,b3)),posdirp(ts(a3,b3))),eqpderp(absd(td(a,b)),p,pdofrp(ts(a3,b3)),posdirp(ts(a3,b3)),t52),isrppd1(ts(a3,b3))):is(atb3,ts(a3,b3))
t54:=isp1(cut,[t:cut]natrp(t),ts(a3,b3),atb3,t49,t53):natrp(atb3)
n@t55:=andi(posd(absd(td(a,b))),[t:posd(absd(td(a,b)))]natrp(atb3(t)),t50,[t:posd(absd(td(a,b)))]t54(t)):natd(absd(td(a,b)))
-intd
j@inttd:=[t:not(zero(td(a,b)))]t55".intd"(t):intd(td(a,b))
+r
@eq:=[x:dif][y:dif]eq"rp"(x,y):[x:dif][y:dif]'prop'
refeq:=[x:dif]refeq"rp"(x):[x:dif]<x><x>eq
symeq:=[x:dif][y:dif][t:<y><x>eq]symeq"rp"(x,y,t):[x:dif][y:dif][t:<y><x>eq]<x><y>eq
treq:=[x:dif][y:dif][z:dif][t:<y><x>eq][u:<z><y>eq]treq"rp"(x,y,z,t,u):[x:dif][y:dif][z:dif][t:<y><x>eq][u:<z><y>eq]<z><x>eq
[a:dif][s:set(dif)]
inn:=esti(dif,a,s):'prop'
@real:=ect"eq"(dif,eq,refeq,symeq,treq):'type'
[r:real][s:real]
is:=is"e"(real,r,s):'prop'
nis:=not(is(r,s)):'prop'
@[p:[x:real]'prop']
some:=some"l"(real,p):'prop'
all:=all"l"(real,p):'prop'
one:=one"e"(real,p):'prop'
r@[s0:set(real)]
in:=esti(real,r,s0):'prop'
a@realof:=ectelt"eq"(dif,eq,refeq,symeq,treq,a):real
r@class:=ecect"eq"(dif,eq,refeq,symeq,treq,r):set(dif)
a@innclass:=th5"eq.4"(dif,eq,refeq,symeq,treq,a):inn(a,class(realof(a)))
r@[a:dif][b:dif][e:eq"rp"(a,b)][air:inn(a,class(r))]
eqinn:=th8"eq.4"(dif,eq,refeq,symeq,treq,r,a,air,b,e):inn(b,class(r))
r@[p:'prop'][p1:[x:dif][xi:inn(x,class(r))]p]
realapp1:=th3"eq.4"(dif,eq,refeq,symeq,treq,r,p,p1):p
r@[s:real][p:'prop'][p1:[x:dif][y:dif][xi:inn(x,class(r))][yi:inn(y,class(s))]p]
+ivr1
[a:dif][air:inn(a,class(r))]
t1:=realapp1(s,p,[y:dif]<air><y><a>p1):p
-ivr1
realapp2:=realapp1(r,p,[x:dif][xi:inn(x,class(r))]t1".ivr1"(x,xi)):p
s@[t:real][p:'prop'][p1:[x:dif][y:dif][z:dif][xi:inn(x,class(r))][yi:inn(y,class(s))][zi:inn(z,class(t))]p]
+*ivr1
p1@[a:dif][air:inn(a,class(r))]
t2:=realapp2(s,t,p,[y:dif][z:dif]<air><z><y><a>p1):p
-ivr1
p1@realapp3:=realapp1(r,p,[x:dif][xi:inn(x,class(r))]t2".ivr1"(x,xi)):p
t@[u:real][p:'prop'][p1:[x:dif][y:dif][z:dif][v:dif][xi:inn(x,class(r))][yi:inn(y,class(s))][zi:inn(z,class(t))][vi:inn(v,class(u))]p]
+*ivr1
p1@[a:dif][air:inn(a,class(r))]
t3:=realapp3(s,t,u,p,[y:dif][z:dif][v:dif]<air><v><z><y><a>p1):p
-ivr1
p1@realapp4:=realapp1(r,p,[x:dif][xi:inn(x,class(r))]t3".ivr1"(x,xi)):p
s@[a1:dif][b1:dif][a1ir:inn(a1,class(r))][b1is:inn(b1,class(s))][e:eq"rp"(a1,b1)]
isin:=th3"eq.5"(dif,eq,refeq,symeq,treq,r,s,a1,a1ir,b1,b1is,e):is(r,s)
b1is@[i:is(r,s)]
isex:=th5"eq.5"(dif,eq,refeq,symeq,treq,r,s,a1,a1ir,b1,b1is,i):eq"rp"(a1,b1)
b1is@[n:not(eq"rp"(a1,b1))]
nisin:=th3"l.imp"(is(r,s),eq"rp"(a1,b1),n,[t:is(r,s)]isex(t)):nis(r,s)
b1is@[n:nis(r,s)]
nisex:=th3"l.imp"(eq"rp"(a1,b1),is(r,s),n,[t:eq"rp"(a1,b1)]isin(t)):not(eq"rp"(a1,b1))
@[alpha:'type'][f:[x:dif]alpha]
fixf:=fixfu"eq"(dif,eq,refeq,symeq,treq,alpha,f):'prop'
[ff:fixf(alpha,f)][r0:real]
indreal:=indeq"eq"(dif,eq,refeq,symeq,treq,alpha,f,ff,r0):alpha
[a:dif][air:inn(a,class(r0))]
isindreal:=th2"eq.10"(dif,eq,refeq,symeq,treq,alpha,f,ff,r0,a,air):is"e"(alpha,<a>f,indreal)
alpha@[g:[x:dif][y:dif]alpha]
fixf2:=fixfu2"eq"(dif,eq,refeq,symeq,treq,alpha,g):'prop'
[ff2:fixf2(alpha,g)][r0:real][s0:real]
indreal2:=indeq2"eq"(dif,eq,refeq,symeq,treq,alpha,g,ff2,r0,s0):alpha
[a:dif][b:dif][air:inn(a,class(r0))][bis:inn(b,class(s0))]
isindreal2:=th1"eq.11"(dif,eq,refeq,symeq,treq,alpha,g,ff2,r0,s0,a,air,b,bis):is"e"(alpha,<b><a>g,indreal2)
@0:=realof(df(1rp,1rp)):real
r@[a0:dif][a0ir:inn(a0,class(r))][z:zero(a0)]
0in:=isin(r,0,a0,df(1rp,1rp),a0ir,innclass(df(1rp,1rp)),zeroeq(a0,df(1rp,1rp),z,zeroi(1rp,1rp,refis(cut,1rp)))):is(r,0)
a0ir@[i:is(r,0)]
0ex:=eqzero(df(1rp,1rp),a0,isex(0,r,df(1rp,1rp),a0,innclass(df(1rp,1rp)),a0ir,symis(real,r,0,i)),tris(cut,stm(df(1rp,1rp)),1rp,std(df(1rp,1rp)),stmis(1rp,1rp),isstd(1rp,1rp))):zero(a0)
+*ivr1
a0@propp:=and(inn(a0,class(r)),posd(a0)):'prop'
-ivr1
r@pos:=some"l"(dif,[x:dif]propp".ivr1"(x)):'prop'
a0ir@[p:posd(a0)]
+*ivr1
p@t4:=andi(inn(a0,class(r)),posd(a0),a0ir,p):propp(a0)
-ivr1
p@posin:=somei(dif,[x:dif]propp".ivr1"(x),a0,t4".ivr1"):pos(r)
a0ir@[p:pos(r)]
+*ivr1
p@[a:dif][q1:propp(a)]
t5:=ande1(inn(a,class(r)),posd(a),q1):inn(a,class(r))
t6:=ande2(inn(a,class(r)),posd(a),q1):posd(a)
t7:=eqposd(a,a0,isex(r,r,a,a0,t5,a0ir,refis(real,r)),t6):posd(a0)
-ivr1
p@posex:=someapp(dif,[x:dif]propp".ivr1"(x),p,posd(a0),[x:dif][t:propp".ivr1"(x)]t7".ivr1"(x,t)):posd(a0)
+*ivr1
a0@propn:=and(inn(a0,class(r)),negd(a0)):'prop'
-ivr1
r@neg:=some"l"(dif,[x:dif]propn".ivr1"(x)):'prop'
a0ir@[n:negd(a0)]
+*ivr1
n@t8:=andi(inn(a0,class(r)),negd(a0),a0ir,n):propn(a0)
-ivr1
n@negin:=somei(dif,[x:dif]propn".ivr1"(x),a0,t8".ivr1"):neg(r)
a0ir@[n:neg(r)]
+*ivr1
n@[a:dif][pl:propn(a)]
t9:=ande1(inn(a,class(r)),negd(a),pl):inn(a,class(r))
t10:=ande2(inn(a,class(r)),negd(a),pl):negd(a)
t11:=eqnegd(a,a0,isex(r,r,a,a0,t9,a0ir,refis(real,r)),t10):negd(a0)
-ivr1
n@negex:=someapp(dif,[x:dif]propn".ivr1"(x),n,negd(a0),[x:dif][t:propn".ivr1"(x)]t11".ivr1"(x,t)):negd(a0)
+*ivr1
a0ir@[p:posd(a0)]
t12:=or3i2(is(r,0),pos(r),neg(r),posin(p)):or3(is(r,0),pos(r),neg(r))
a0ir@[z:zero(a0)]
t13:=or3i1(is(r,0),pos(r),neg(r),0in(z)):or3(is(r,0),pos(r),neg(r))
a0ir@[n:negd(a0)]
t14:=or3i3(is(r,0),pos(r),neg(r),negin(n)):or3(is(r,0),pos(r),neg(r))
a0ir@t15:=rappd(a0,or3(is(r,0),pos(r),neg(r)),[t:posd(a0)]t12(t),[t:zero(a0)]t13(t),[t:negd(a0)]t14(t)):or3(is(r,0),pos(r),neg(r))
-ivr1
r@axrlo:=realapp1(r,or3(is(r,0),pos(r),neg(r)),[x:dif][xi:inn(x,class(r))]t15".ivr1"(x,xi)):or3(is(r,0),pos(r),neg(r))
+*ivr1
a0ir@[i:is(r,0)]
t16:=th3"l.imp"(pos(r),posd(a0),0notpd(a0,0ex(i)),[t:pos(r)]posex(t)):not(pos(r))
a0ir@[p:pos(r)]
t17:=th3"l.imp"(neg(r),negd(a0),pnotnd(a0,posex(p)),[t:neg(r)]negex(t)):not(neg(r))
a0ir@[n:neg(r)]
t18:=th3"l.imp"(is(r,0),zero(a0),nnot0d(a0,negex(n)),[t:is(r,0)]0ex(t)):not(is(r,0))
a0ir@t19:=th6"l.ec3"(is(r,0),pos(r),neg(r),[t:is(r,0)]t16(t),[t:pos(r)]t17(t),[t:neg(r)]t18(t)):ec3(is(r,0),pos(r),neg(r))
-ivr1
r@axrle:=realapp1(r,ec3(is(r,0),pos(r),neg(r)),[x:dif][xi:inn(x,class(r))]t19".ivr1"(x,xi)):ec3(is(r,0),pos(r),neg(r))
axrl:=orec3i(is(r,0),pos(r),neg(r),axrlo,axrle):orec3(is(r,0),pos(r),neg(r))
[p:'prop'][p1:[t:pos(r)]p][p2:[t:is(r,0)]p][p3:[t:neg(r)]p]
rapp:=or3app(is(r,0),pos(r),neg(r),p,axrlo,p2,p1,p3):p
r@[p:pos(r)]
pnotn:=ec3e23(is(r,0),pos(r),neg(r),axrle,p):not(neg(r))
pnot0:=ec3e21(is(r,0),pos(r),neg(r),axrle,p):nis(r,0)
r@[i:is(r,0)]
0notp:=ec3e12(is(r,0),pos(r),neg(r),axrle,i):not(pos(r))
0notn:=ec3e13(is(r,0),pos(r),neg(r),axrle,i):not(neg(r))
r@[n:neg(r)]
nnotp:=ec3e32(is(r,0),pos(r),neg(r),axrle,n):not(pos(r))
nnot0:=ec3e31(is(r,0),pos(r),neg(r),axrle,n):nis(r,0)
s@[i:is(r,s)][p:pos(r)]
ispos:=isp(real,[x:real]pos(x),r,s,p,i):pos(s)
i@[n:neg(r)]
isneg:=isp(real,[x:real]neg(x),r,s,n,i):neg(s)
@[r0:cut]
pofrp:=realof(pdofrp(r0)):real
nofrp:=realof(ndofrp(r0)):real
[s0:cut][i:is"rp"(r0,s0)]
isrpep:=isf(cut,real,[x:cut]pofrp(x),r0,s0,i):is(pofrp(r0),pofrp(s0))
isrpen:=isf(cut,real,[x:cut]nofrp(x),r0,s0,i):is(nofrp(r0),nofrp(s0))
s0@[i:is(pofrp(r0),pofrp(s0))]
+*ivr1
i"r"@t20:=isex(pofrp(r0),pofrp(s0),pdofrp(r0),pdofrp(s0),innclass(pdofrp(r0)),innclass(pdofrp(s0)),i):eq"rp"(pdofrp(r0),pdofrp(s0))
-ivr1
i@isrpip:=isrpipd(r0,s0,t20".ivr1"):is"rp"(r0,s0)
s0@[i:is(nofrp(r0),nofrp(s0))]
+*ivr1
i"r"@t21:=isex(nofrp(r0),nofrp(s0),ndofrp(r0),ndofrp(s0),innclass(ndofrp(r0)),innclass(ndofrp(s0)),i):eq"rp"(ndofrp(r0),ndofrp(s0))
-ivr1
i@isrpin:=isrpind(r0,s0,t21".ivr1"):is"rp"(r0,s0)
r0@posi:=posin(pofrp(r0),pdofrp(r0),innclass(pdofrp(r0)),posdirp(r0)):pos(pofrp(r0))
negi:=negin(nofrp(r0),ndofrp(r0),innclass(ndofrp(r0)),negdirp(r0)):neg(nofrp(r0))
s@[r0:cut][s0:cut][i:is(r,pofrp(r0))][j:is(s,pofrp(s0))][k:is(r,s)]
+*ivr1
k@t22:=isrpip(r0,s0,tr3is(real,pofrp(r0),r,s,pofrp(s0),symis(real,r,pofrp(r0),i),k,j)):is"rp"(r0,s0)
r@t23:=[x:cut][y:cut][t:is(r,pofrp(x))][u:is(r,pofrp(y))]t22(r,r,x,y,t,u,refis(real,r)):amone(cut,[x:cut]is(r,pofrp(x)))
a0ir@[p1:pos(r)]
t24:=posex(p1):posd(a0)
pr:=rpofpd(a0,t24):cut
t25:=isin(r,pofrp(pr),a0,pdofrp(pr),a0ir,innclass(pdofrp(pr)),eqpdrp1(a0,t24)):is(r,pofrp(pr))
t26:=somei(cut,[x:cut]is(r,pofrp(x)),pr,t25):some"rp"([x:cut]is(r,pofrp(x)))
-ivr1
r@[p:pos(r)]
+*ivr1
p"r"@t27:=realapp1(some"rp"([x:cut]is(r,pofrp(x))),[x:dif][t:inn(x,class(r))]t26(x,t,p)):some"rp"([x:cut]is(r,pofrp(x)))
t28:=onei(cut,[x:cut]is(r,pofrp(x)),t23,t27):one"rp"([x:cut]is(r,pofrp(x)))
-ivr1
p@rpofp:=ind(cut,[x:cut]is(r,pofrp(x)),t28".ivr1"):cut
isprp1:=oneax(cut,[x:cut]is(r,pofrp(x)),t28".ivr1"):is(r,pofrp(rpofp(r,p)))
isprp2:=symis(real,r,pofrp(rpofp(r,p)),isprp1):is(pofrp(rpofp(r,p)),r)
@[r1:real][p:pos(r1)][s1:real][q:pos(s1)][i:is(r1,s1)]
isperp:=t22".ivr1"(r1,s1,rpofp(r1,p),rpofp(s1,q),isprp1(r1,p),isprp1(s1,q),i):is"rp"(rpofp(r1,p),rpofp(s1,q))
q@[i:is"rp"(rpofp(r1,p),rpofp(s1,q))]
ispirp:=tr3is(real,r1,pofrp(rpofp(r1,p)),pofrp(rpofp(s1,q)),s1,isprp1(r1,p),isrpep(rpofp(r1,p),rpofp(s1,q),i),isprp2(s1,q)):is(r1,s1)
@[r0:cut]
isrpp1:=t22".ivr1"(pofrp(r0),pofrp(r0),r0,rpofp(pofrp(r0),posi(r0)),refis(real,pofrp(r0)),isprp1(pofrp(r0),posi(r0)),refis(real,pofrp(r0))):is"rp"(r0,rpofp(pofrp(r0),posi(r0)))
isrpp2:=symis(cut,r0,rpofp(pofrp(r0),posi(r0)),isrpp1):is"rp"(rpofp(pofrp(r0),posi(r0)),r0)
s@[r0:cut][s0:cut][i:is(r,nofrp(r0))][j:is(s,nofrp(s0))][k:is(r,s)]
+*ivr1
k@t29:=isrpin(r0,s0,tr3is(real,nofrp(r0),r,s,nofrp(s0),symis(real,r,nofrp(r0),i),k,j)):is"rp"(r0,s0)
r@t30:=[x:cut][y:cut][t:is(r,nofrp(x))][u:is(r,nofrp(y))]t29(r,r,x,y,t,u,refis(real,r)):amone(cut,[x:cut]is(r,nofrp(x)))
a0ir@[n1:neg(r)]
t31:=negex(n1):negd(a0)
nr:=rpofnd(a0,t31):cut
t32:=isin(r,nofrp(nr),a0,ndofrp(nr),a0ir,innclass(ndofrp(nr)),eqndrp1(a0,t31)):is(r,nofrp(nr))
t33:=somei(cut,[x:cut]is(r,nofrp(x)),nr,t32):some"rp"([x:cut]is(r,nofrp(x)))
-ivr1
r@[n:neg(r)]
+*ivr1
n"r"@t34:=realapp1(some"rp"([x:cut]is(r,nofrp(x))),[x:dif][t:inn(x,class(r))]t33(x,t,n)):some"rp"([x:cut]is(r,nofrp(x)))
t35:=onei(cut,[x:cut]is(r,nofrp(x)),t30,t34):one"rp"([x:cut]is(r,nofrp(x)))
-ivr1
n@rpofn:=ind(cut,[x:cut]is(r,nofrp(x)),t35".ivr1"):cut
isnrp1:=oneax(cut,[x:cut]is(r,nofrp(x)),t35".ivr1"):is(r,nofrp(rpofn(r,n)))
isnrp2:=symis(real,r,nofrp(rpofn(r,n)),isnrp1):is(nofrp(rpofn(r,n)),r)
@[r1:real][n:neg(r1)][s1:real][m:neg(s1)][i:is(r1,s1)]
isnerp:=t29".ivr1"(r1,s1,rpofn(r1,n),rpofn(s1,m),isnrp1(r1,n),isnrp1(s1,m),i):is"rp"(rpofn(r1,n),rpofn(s1,m))
m@[i:is"rp"(rpofn(r1,n),rpofn(s1,m))]
isnirp:=tr3is(real,r1,nofrp(rpofn(r1,n)),nofrp(rpofn(s1,m)),s1,isnrp1(r1,n),isrpen(rpofn(r1,n),rpofn(s1,m),i),isnrp2(s1,m)):is(r1,s1)
@[r0:cut]
isrpn1:=t29".ivr1"(nofrp(r0),nofrp(r0),r0,rpofn(nofrp(r0),negi(r0)),refis(real,nofrp(r0)),isnrp1(nofrp(r0),negi(r0)),refis(real,nofrp(r0))):is"rp"(r0,rpofn(nofrp(r0),negi(r0)))
isrpn2:=symis(cut,r0,rpofn(nofrp(r0),negi(r0)),isrpn1):is"rp"(rpofn(nofrp(r0),negi(r0)),r0)
r@satz163:=refis(real,r):is(r,r)
s@[i:is(r,s)]
satz164:=symis(real,r,s,i):is(s,r)
t@[i:is(r,s)][j:is(s,t)]
satz165:=tris(real,r,s,t,i,j):is(r,t)
@absdr:=[x:dif]realof(absd(x)):[x:dif]real
+ivr2
[a:dif][b:dif][e:eq"rp"(a,b)]
t1:=isin(realof(absd(a)),realof(absd(b)),absd(a),absd(b),innclass(absd(a)),innclass(absd(b)),eqabsd(a,b,e)):is(<a>absdr,<b>absdr)
-ivr2
fabsdr:=[x:dif][y:dif][t:<y><x>eq]t1".ivr2"(x,y,t):fixf(real,absdr)
r@abs:=indreal(real,absdr,fabsdr,r):real
+*ivr2
a0ir@t2:=isindreal(real,absdr,fabsdr,r,a0,a0ir):is(realof(absd(a0)),abs(r))
-ivr2
a0ir@aica:=isp(real,[x:real]inn(absd(a0),class(x)),realof(absd(a0)),abs(r),innclass(absd(a0)),t2".ivr2"):inn(absd(a0),class(abs(r)))
s@[i:is(r,s)]
isabs:=isf(real,real,[x:real]abs(x),r,s,i):is(abs(r),abs(s))
+2r166
a0ir@[p:pos(r)]
t1:=satzd166a(a0,posex(p)):posd(absd(a0))
t2:=posin(abs(r),absd(a0),aica,t1):pos(abs(r))
-2r166
r@[p:pos(r)]
satz166a:=realapp1(pos(abs(r)),[x:dif][t:inn(x,class(r))]t2".2r166"(x,t,p)):pos(abs(r))
+*2r166
a0ir@[n:neg(r)]
t3:=satzd166b(a0,negex(n)):posd(absd(a0))
t4:=posin(abs(r),absd(a0),aica,t3):pos(abs(r))
-2r166
r@[n:neg(r)]
satz166b:=realapp1(pos(abs(r)),[x:dif][t:inn(x,class(r))]t4".2r166"(x,t,n)):pos(abs(r))
+*2r166
b1is@[p:pos(r)][q:pos(s)][i:is(abs(r),abs(s))]
t5:=satzd166c(a1,b1,posex(r,a1,a1ir,p),posex(s,b1,b1is,q),isex(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is),i)):eq"rp"(a1,b1)
t6:=isin(t5):is(r,s)
-2r166
s@[p:pos(r)][q:pos(s)][i:is(abs(r),abs(s))]
satz166c:=realapp2(is(r,s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".2r166"(x,y,t,u,p,q,i)):is(r,s)
+*2r166
b1is@[n:neg(r)][o:neg(s)][i:is(abs(r),abs(s))]
t7:=satzd166d(a1,b1,negex(r,a1,a1ir,n),negex(s,b1,b1is,o),isex(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is),i)):eq"rp"(a1,b1)
t8:=isin(t7):is(r,s)
-2r166
s@[n:neg(r)][o:neg(s)][i:is(abs(r),abs(s))]
satz166d:=realapp2(is(r,s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t8".2r166"(x,y,t,u,n,o,i)):is(r,s)
r@[n:nis(r,0)]
satz166e:=rapp(r,pos(abs(r)),[t:pos(r)]satz166a(t),th2"l.imp"(is(r,0),pos(abs(r)),n),[t:neg(r)]satz166b(t)):pos(abs(r))
+*2r166
a0ir@[i:is(r,0)]
t9:=satzd166f(a0,0ex(i)):zero(absd(a0))
t10:=0in(abs(r),absd(a0),aica,t9):is(abs(r),0)
-2r166
r@[i:is(r,0)]
satz166f:=realapp1(is(abs(r),0),[x:dif][t:inn(x,class(r))]t10".2r166"(x,t,i)):is(abs(r),0)
s@more:=some"l"(dif,[x:dif]some"l"(dif,[y:dif]and3(inn(x,class(r)),inn(y,class(s)),mored(x,y)))):'prop'
+*ivr2
b1@propm:=and3(inn(a1,class(r)),inn(b1,class(s)),mored(a1,b1)):'prop'
-ivr2
b1is@[m:mored(a1,b1)]
+*ivr2
m@t3:=and3i(inn(a1,class(r)),inn(b1,class(s)),mored(a1,b1),a1ir,b1is,m):propm(a1,b1)
t4:=somei(dif,[x:dif]propm(a1,x),b1,t3):some"l"(dif,[x:dif]propm(a1,x))
-ivr2
m@morein:=somei(dif,[x:dif]some"l"(dif,[y:dif]propm".ivr2"(x,y)),a1,t4".ivr2"):more(r,s)
b1is@[m:more(r,s)]
+*ivr2
m@[a:dif][sa:some"l"(dif,[x:dif]propm(a,x))][b:dif][p2:propm(a,b)]
t5:=and3e1(inn(a,class(r)),inn(b,class(s)),mored(a,b),p2):inn(a,class(r))
t6:=and3e2(inn(a,class(r)),inn(b,class(s)),mored(a,b),p2):inn(b,class(s))
t7:=and3e3(inn(a,class(r)),inn(b,class(s)),mored(a,b),p2):mored(a,b)
t8:=eqmored12(a,a1,b,b1,isex(r,r,a,a1,t5,a1ir,refis(real,r)),isex(s,s,b,b1,t6,b1is,refis(real,s)),t7):mored(a1,b1)
sa@t9:=someapp(dif,[x:dif]propm(a,x),sa,mored(a1,b1),[x:dif][t:propm(a,x)]t8(x,t)):mored(a1,b1)
-ivr2
m@moreex:=someapp(dif,[x:dif]some"l"(dif,[y:dif]propm".ivr2"(x,y)),m,mored(a1,b1),[x:dif][t:some"l"(dif,[y:dif]propm".ivr2"(x,y))]t9".ivr2"(x,t)):mored(a1,b1)
s@less:=some"l"(dif,[x:dif]some"l"(dif,[y:dif]and3(inn(x,class(r)),inn(y,class(s)),lessd(x,y)))):'prop'
+*ivr2
b1@propl:=and3(inn(a1,class(r)),inn(b1,class(s)),lessd(a1,b1)):'prop'
-ivr2
b1is@[l:lessd(a1,b1)]
+*ivr2
l@t10:=and3i(inn(a1,class(r)),inn(b1,class(s)),lessd(a1,b1),a1ir,b1is,l):propl(a1,b1)
t11:=somei(dif,[x:dif]propl(a1,x),b1,t10):some"l"(dif,[x:dif]propl(a1,x))
-ivr2
l@lessin:=somei(dif,[x:dif]some"l"(dif,[y:dif]propl".ivr2"(x,y)),a1,t11".ivr2"):less(r,s)
b1is@[l:less(r,s)]
+*ivr2
l@[a:dif][sa:some"l"(dif,[x:dif]propl(a,x))][b:dif][p2:propl(a,b)]
t12:=and3e1(inn(a,class(r)),inn(b,class(s)),lessd(a,b),p2):inn(a,class(r))
t13:=and3e2(inn(a,class(r)),inn(b,class(s)),lessd(a,b),p2):inn(b,class(s))
t14:=and3e3(inn(a,class(r)),inn(b,class(s)),lessd(a,b),p2):lessd(a,b)
t15:=eqlessd12(a,a1,b,b1,isex(r,r,a,a1,t12,a1ir,refis(real,r)),isex(s,s,b,b1,t13,b1is,refis(real,s)),t14):lessd(a1,b1)
sa@t16:=someapp(dif,[x:dif]propl(a,x),sa,lessd(a1,b1),[x:dif][t:propl(a,x)]t15(x,t)):lessd(a1,b1)
-ivr2
l@lessex:=someapp(dif,[x:dif]some"l"(dif,[y:dif]propl".ivr2"(x,y)),l,lessd(a1,b1),[x:dif][t:some"l"(dif,[y:dif]propl".ivr2"(x,y))]t16".ivr2"(x,t)):lessd(a1,b1)
t@[i:is(r,s)][m:more(r,t)]
ismore1:=isp(real,[x:real]more(x,t),r,s,m,i):more(s,t)
i@[m:more(t,r)]
ismore2:=isp(real,[x:real]more(t,x),r,s,m,i):more(t,s)
i@[l:less(r,t)]
isless1:=isp(real,[x:real]less(x,t),r,s,l,i):less(s,t)
i@[l:less(t,r)]
isless2:=isp(real,[x:real]less(t,x),r,s,l,i):less(t,s)
u@[i:is(r,s)][j:is(t,u)][m:more(r,t)]
ismore12:=ismore2(t,u,s,j,ismore1(r,s,t,i,m)):more(s,u)
j@[l:less(r,t)]
isless12:=isless2(t,u,s,j,isless1(r,s,t,i,l)):less(s,u)
+*ivr2
b1is@[m:more(r,s)]
t17:=lemmad5(a1,b1,moreex(m)):lessd(b1,a1)
t18:=lessin(s,r,b1,a1,b1is,a1ir,t17):less(s,r)
-ivr2
s@[m:more(r,s)]
lemma1:=realapp2(less(s,r),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t18".ivr2"(x,y,t,u,m)):less(s,r)
+*ivr2
b1is@[l:less(r,s)]
t19:=lemmad6(a1,b1,lessex(l)):mored(b1,a1)
t20:=morein(s,r,b1,a1,b1is,a1ir,t19):more(s,r)
-ivr2
s@[l:less(r,s)]
lemma2:=realapp2(more(s,r),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t20".ivr2"(x,y,t,u,l)):more(s,r)
+2r167
b1is@t1:=satzd167a(a1,b1):or3(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1))
[e:eq"rp"(a1,b1)]
t2:=or3i1(is(r,s),more(r,s),less(r,s),isin(e)):or3(is(r,s),more(r,s),less(r,s))
b1is@[m:mored(a1,b1)]
t3:=or3i2(is(r,s),more(r,s),less(r,s),morein(m)):or3(is(r,s),more(r,s),less(r,s))
b1is@[l:lessd(a1,b1)]
t4:=or3i3(is(r,s),more(r,s),less(r,s),lessin(l)):or3(is(r,s),more(r,s),less(r,s))
b1is@t5:=or3app(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1),or3(is(r,s),more(r,s),less(r,s)),t1,[t:eq"rp"(a1,b1)]t2(t),[t:mored(a1,b1)]t3(t),[t:lessd(a1,b1)]t4(t)):or3(is(r,s),more(r,s),less(r,s))
t6:=satzd167b(a1,b1):ec3(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1))
[i:is(r,s)]
t7:=th3"l.imp"(more(r,s),mored(a1,b1),ec3e12(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1),t6,isex(i)),[t:more(r,s)]moreex(t)):not(more(r,s))
b1is@[m:more(r,s)]
t8:=th3"l.imp"(less(r,s),lessd(a1,b1),ec3e23(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1),t6,moreex(m)),[t:less(r,s)]lessex(t)):not(less(r,s))
b1is@[l:less(r,s)]
t9:=th3"l.imp"(is(r,s),eq"rp"(a1,b1),ec3e31(eq"rp"(a1,b1),mored(a1,b1),lessd(a1,b1),t6,lessex(l)),[t:is(r,s)]isex(t)):not(is(r,s))
b1is@t10:=th6"l.ec3"(is(r,s),more(r,s),less(r,s),th1"l.ec"(is(r,s),more(r,s),[t:is(r,s)]t7(t)),th1"l.ec"(more(r,s),less(r,s),[t:more(r,s)]t8(t)),th1"l.ec"(less(r,s),is(r,s),[t:less(r,s)]t9(t))):ec3(is(r,s),more(r,s),less(r,s))
t11:=orec3i(is(r,s),more(r,s),less(r,s),t5,t10):orec3(is(r,s),more(r,s),less(r,s))
-2r167
s@satz167:=realapp2(orec3(is(r,s),more(r,s),less(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t11".2r167"(x,y,t,u)):orec3(is(r,s),more(r,s),less(r,s))
satz167a:=orec3e1(is(r,s),more(r,s),less(r,s),satz167):or3(is(r,s),more(r,s),less(r,s))
satz167b:=orec3e2(is(r,s),more(r,s),less(r,s),satz167):ec3(is(r,s),more(r,s),less(r,s))
moreis:=or(more(r,s),is(r,s)):'prop'
lessis:=or(less(r,s),is(r,s)):'prop'
[m:moreis(r,s)]
satz168a:=th9"l.or"(more(r,s),is(r,s),less(s,r),is(s,r),m,[t:more(r,s)]lemma1(t),[t:is(r,s)]symis(real,r,s,t)):lessis(s,r)
s@[l:lessis(r,s)]
satz168b:=th9"l.or"(less(r,s),is(r,s),more(s,r),is(s,r),l,[t:less(r,s)]lemma2(t),[t:is(r,s)]symis(real,r,s,t)):moreis(s,r)
t@[i:is(r,s)][m:moreis(r,t)]
ismoreis1:=isp(real,[x:real]moreis(x,t),r,s,m,i):moreis(s,t)
i@[m:moreis(t,r)]
ismoreis2:=isp(real,[x:real]moreis(t,x),r,s,m,i):moreis(t,s)
i@[l:lessis(r,t)]
islessis1:=isp(real,[x:real]lessis(x,t),r,s,l,i):lessis(s,t)
i@[l:lessis(t,r)]
islessis2:=isp(real,[x:real]lessis(t,x),r,s,l,i):lessis(t,s)
u@[i:is(r,s)][j:is(t,u)][m:moreis(r,t)]
ismoreis12:=ismoreis2(t,u,s,j,ismoreis1(r,s,t,i,m)):moreis(s,u)
j@[l:lessis(r,t)]
islessis12:=islessis2(t,u,s,j,islessis1(r,s,t,i,l)):lessis(s,u)
s@[m:more(r,s)]
moreisi1:=ori1(more(r,s),is(r,s),m):moreis(r,s)
s@[l:less(r,s)]
lessisi1:=ori1(less(r,s),is(r,s),l):lessis(r,s)
s@[i:is(r,s)]
moreisi2:=ori2(more(r,s),is(r,s),i):moreis(r,s)
lessisi2:=ori2(less(r,s),is(r,s),i):lessis(r,s)
b1is@[m:moreq(a1,b1)]
moreisin:=orapp(mored(a1,b1),eq"rp"(a1,b1),moreis(r,s),m,[t:mored(a1,b1)]moreisi1(morein(t)),[t:eq"rp"(a1,b1)]moreisi2(isin(t))):moreis(r,s)
b1is@[m:moreis(r,s)]
moreisex:=orapp(more(r,s),is(r,s),moreq(a1,b1),m,[t:more(r,s)]moreqi1(a1,b1,moreex(t)),[t:is(r,s)]moreqi2(a1,b1,isex(t))):moreq(a1,b1)
b1is@[l:lesseq(a1,b1)]
lessisin:=orapp(lessd(a1,b1),eq"rp"(a1,b1),lessis(r,s),l,[t:lessd(a1,b1)]lessisi1(lessin(t)),[t:eq"rp"(a1,b1)]lessisi2(isin(t))):lessis(r,s)
b1is@[l:lessis(r,s)]
lessisex:=orapp(less(r,s),is(r,s),lesseq(a1,b1),l,[t:less(r,s)]lesseqi1(a1,b1,lessex(t)),[t:is(r,s)]lesseqi2(a1,b1,isex(t))):lesseq(a1,b1)
s@[m:moreis(r,s)]
satz167c:=th7"l.ec3"(is(r,s),more(r,s),less(r,s),satz167b,comor(more(r,s),is(r,s),m)):not(less(r,s))
s@[l:lessis(r,s)]
satz167d:=th9"l.ec3"(is(r,s),more(r,s),less(r,s),satz167b,l):not(more(r,s))
s@[n:not(more(r,s))]
satz167e:=th2"l.or3"(is(r,s),more(r,s),less(r,s),satz167a,n):lessis(r,s)
s@[n:not(less(r,s))]
s@[n:not(less(r,s))]
satz167f:=comor(is(r,s),more(r,s),th3"l.or3"(is(r,s),more(r,s),less(r,s),satz167a,n)):moreis(r,s)
s@[m:more(r,s)]
satz167g:=th3"l.imp"(lessis(r,s),not(more(r,s)),weli(more(r,s),m),[t:lessis(r,s)]satz167d(t)):not(lessis(r,s))
s@[l:less(r,s)]
satz167h:=th3"l.imp"(moreis(r,s),not(less(r,s)),weli(less(r,s),l),[t:moreis(r,s)]satz167c(t)):not(moreis(r,s))
s@[n:not(moreis(r,s))]
satz167j:=or3e3(is(r,s),more(r,s),less(r,s),satz167a,th5"l.or"(more(r,s),is(r,s),n),th4"l.or"(more(r,s),is(r,s),n)):less(r,s)
s@[n:not(lessis(r,s))]
satz167k:=or3e2(is(r,s),more(r,s),less(r,s),satz167a,th4"l.or"(less(r,s),is(r,s),n),th5"l.or"(less(r,s),is(r,s),n)):more(r,s)
r@[p:pos(r)]
+2r169
[a:dif][b:dif][air:inn(a,class(r))][bi0:inn(b,class(0))]
t1:=satzd169a(a,b,0ex(0,b,bi0,refis(real,0)),posex(a,air,p)):mored(a,b)
t2:=morein(r,0,a,b,air,bi0,t1):more(r,0)
-2r169
satz169a:=realapp2(r,0,more(r,0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(0))]t2".2r169"(x,y,t,u)):more(r,0)
r@[m:more(r,0)]
+*2r169
m@[a:dif][b:dif][air:inn(a,class(r))][bi0:inn(b,class(0))]
t3:=satzd169b(a,b,0ex(0,b,bi0,refis(real,0)),moreex(r,0,a,b,air,bi0,m)):posd(a)
t4:=posin(r,a,air,t3):pos(r)
-2r169
m@satz169b:=realapp2(r,0,pos(r),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(0))]t4".2r169"(x,y,t,u)):pos(r)
r@[n:neg(r)]
+*2r169
n@[a:dif][b:dif][air:inn(a,class(r))][bi0:inn(b,class(0))]
t5:=satzd169c(a,b,0ex(0,b,bi0,refis(real,0)),negex(a,air,n)):lessd(a,b)
t6:=lessin(r,0,a,b,air,bi0,t5):less(r,0)
-2r169
n@satz169c:=realapp2(r,0,less(r,0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(0))]t6".2r169"(x,y,t,u)):less(r,0)
r@[l:less(r,0)]
+*2r169
l@[a:dif][b:dif][air:inn(a,class(r))][bi0:inn(b,class(0))]
t7:=satzd169d(a,b,0ex(0,b,bi0,refis(real,0)),lessex(r,0,a,b,air,bi0,l)):negd(a)
t8:=negin(r,a,air,t7):neg(r)
-2r169
l@satz169d:=realapp2(r,0,neg(r),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(0))]t8".2r169"(x,y,t,u)):neg(r)
+2r170
r@[a:dif][b:dif][air:inn(a,class(r))][bi0:inn(b,class(0))]
t1:=satzd170(a,b,0ex(0,b,bi0,refis(real,0))):moreq(absd(a),b)
t2:=moreisin(abs(r),0,absd(a),b,aica(r,a,air),bi0,t1):moreis(abs(r),0)
-2r170
r@satz170:=realapp2(r,0,moreis(abs(r),0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(0))]t2".2r170"(x,y,t,u)):moreis(abs(r),0)
satz170a:=th3"l.imp"(neg(abs(r)),less(abs(r),0),satz167c(abs(r),0,satz170),[t:neg(abs(r))]satz169c(abs(r),t)):not(neg(abs(r)))
t@[l:less(r,s)][k:less(s,t)]
+2r171
[a:dif][b:dif][c:dif][air:inn(a,class(r))][bis:inn(b,class(s))][cit:inn(c,class(t))]
t1:=satzd171(a,b,c,lessex(r,s,a,b,air,bis,l),lessex(s,t,b,c,bis,cit,k)):lessd(a,c)
t2:=lessin(r,t,a,c,air,cit,t1):less(r,t)
-2r171
satz171:=realapp3(r,s,t,less(r,t),[x:dif][y:dif][z:dif][w:inn(x,class(r))][u:inn(y,class(s))][v:inn(z,class(t))]t2".2r171"(x,y,z,w,u,v)):less(r,t)
trless:=satz171:less(r,t)
t@[m:more(r,s)][n:more(s,t)]
trmore:=lemma2(t,r,trless(t,s,r,lemma1(s,t,n),lemma1(r,s,m))):more(r,t)
t@[a2:dif][b2:dif][c2:dif][a2ir:inn(a2,class(r))][b2is:inn(b2,class(s))][c2it:inn(c2,class(t))]
+2r172
[l:lessis(r,s)][k:less(s,t)]
t1:=satzd172a(a2,b2,c2,lessisex(a2,b2,a2ir,b2is,l),lessex(s,t,b2,c2,b2is,c2it,k)):lessd(a2,c2)
t2:=lessin(r,t,a2,c2,a2ir,c2it,t1):less(r,t)
-2r172
t@[l:lessis(r,s)][k:less(s,t)]
satz172a:=realapp3(less(r,t),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".2r172"(x,y,z,u,v,w,l,k)):less(r,t)
+*2r172
c2it@[l:less(r,s)][k:lessis(s,t)]
t3:=satzd172b(a2,b2,c2,lessex(a2,b2,a2ir,b2is,l),lessisex(s,t,b2,c2,b2is,c2it,k)):lessd(a2,c2)
t4:=lessin(r,t,a2,c2,a2ir,c2it,t3):less(r,t)
-2r172
t@[l:less(r,s)][k:lessis(s,t)]
satz172b:=realapp3(less(r,t),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t4".2r172"(x,y,z,u,v,w,l,k)):less(r,t)
t@[m:moreis(r,s)][n:more(s,t)]
satz172c:=lemma2(t,r,satz172b(t,s,r,lemma1(s,t,n),satz168a(m))):more(r,t)
t@[m:more(r,s)][n:moreis(s,t)]
satz172d:=lemma2(t,r,satz172a(t,s,r,satz168a(s,t,n),lemma1(m))):more(r,t)
+2r173
c2it@[l:lessis(r,s)][k:lessis(s,t)]
t1:=satzd173(a2,b2,c2,lessisex(a2,b2,a2ir,b2is,l),lessisex(s,t,b2,c2,b2is,c2it,k)):lesseq(a2,c2)
t2:=lessisin(r,t,a2,c2,a2ir,c2it,t1):lessis(r,t)
-2r173
t@[l:lessis(r,s)][k:lessis(s,t)]
satz173:=realapp3(lessis(r,t),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".2r173"(x,y,z,u,v,w,l,k)):lessis(r,t)
trlessis:=satz173:lessis(r,t)
t@[m:moreis(r,s)][n:moreis(s,t)]
trmoreis:=satz168b(t,r,trlessis(t,s,r,satz168a(s,t,n),satz168a(m))):moreis(r,t)
r@ratrl:=some"l"(dif,[x:dif]and(inn(x,class(r)),ratd(x))):'prop'
a0ir@[r1:ratd(a0)]
+*ivr2
r1@t21:=andi(inn(a0,class(r)),ratd(a0),a0ir,r1):and(inn(a0,class(r)),ratd(a0))
-ivr2
r1@ratrlin:=somei(dif,[x:dif]and(inn(x,class(r)),ratd(x)),a0,t21".ivr2"):ratrl(r)
a0ir@[rr:ratrl(r)]
+*ivr2
rr@[a:dif][b:and(inn(a,class(r)),ratd(a))]
t22:=ande1(inn(a,class(r)),ratd(a),b):inn(a,class(r))
t23:=ande2(inn(a,class(r)),ratd(a),b):ratd(a)
t24:=eqratd(a,a0,isex(r,r,a,a0,t22,a0ir,refis(real,r)),t23):ratd(a0)
-ivr2
rr@ratrlex:=someapp(dif,[x:dif]and(inn(x,class(r)),ratd(x)),rr,ratd(a0),[x:dif][t:and(inn(x,class(r)),ratd(x))]t24".ivr2"(x,t)):ratd(a0)
r@irratrl:=not(ratrl(r)):'prop'
@[r0:cut][rr:ratrp(r0)]
remark2:=ratrlin(pofrp(r0),pdofrp(r0),innclass(pdofrp(r0)),remark2a(r0,rr)):ratrl(pofrp(r0))
remark3:=ratrlin(nofrp(r0),ndofrp(r0),innclass(ndofrp(r0)),remark3a(r0,rr)):ratrl(nofrp(r0))
r0@[ir:irratrp(r0)]
remark4:=th3"l.imp"(ratrl(pofrp(r0)),ratd(pdofrp(r0)),remark4a(r0,ir),[t:ratrl(pofrp(r0))]ratrlex(pofrp(r0),pdofrp(r0),innclass(pdofrp(r0)),t)):irratrl(pofrp(r0))
remark5:=th3"l.imp"(ratrl(nofrp(r0)),ratd(ndofrp(r0)),remark5a(r0,ir),[t:ratrl(nofrp(r0))]ratrlex(nofrp(r0),ndofrp(r0),innclass(ndofrp(r0)),t)):irratrl(nofrp(r0))
r@natrl:=some"l"(dif,[x:dif]and(inn(x,class(r)),natd(x))):'prop'
a0ir@[n:natd(a0)]
+*ivr2
n@t25:=andi(inn(a0,class(r)),natd(a0),a0ir,n):and(inn(a0,class(r)),natd(a0))
-ivr2
n@natrlin:=somei(dif,[x:dif]and(inn(x,class(r)),natd(x)),a0,t25".ivr2"):natrl(r)
a0ir@[n:natrl(r)]
+*ivr2
n@[a:dif][b:and(inn(a,class(r)),natd(a))]
t26:=ande1(inn(a,class(r)),natd(a),b):inn(a,class(r))
t27:=ande2(inn(a,class(r)),natd(a),b):natd(a)
t28:=eqnatd(a,a0,isex(r,r,a,a0,t26,a0ir,refis(real,r)),t27):natd(a0)
-ivr2
n@natrlex:=someapp(dif,[x:dif]and(inn(x,class(r)),natd(x)),n,natd(a0),[x:dif][t:and(inn(x,class(r)),natd(x))]t28".ivr2"(x,t)):natd(a0)
+*ivr2
a0ir@[n:natrl(r)]
t29:=natposd(a0,natrlex(n)):posd(a0)
t30:=posin(t29):pos(r)
-ivr2
r@[n:natrl(r)]
natpos:=realapp1(pos(r),[x:dif][t:inn(x,class(r))]t30".ivr2"(x,t,n)):pos(r)
@[x:nat]
rlofnt:=realof(pdofnt(x)):real
natrli:=natrlin(rlofnt(x),pdofnt(x),innclass(pdofnt(x)),natdi(x)):natrl(rlofnt(x))
[y:nat][i:is"n"(x,y)]
isnterl:=isf(nat,real,[u:nat]rlofnt(u),x,y,i):is(rlofnt(x),rlofnt(y))
y@[i:is(rlofnt(x),rlofnt(y))]
isntirl:=isntirp(x,y,isrpip(rpofnt(x),rpofnt(y),i)):is"n"(x,y)
+*ivr2
@t31:=[x:nat][y:nat][t:is(rlofnt(x),rlofnt(y))]isntirl(x,y,t):injective(nat,real,[x:nat]rlofnt(x))
a0ir@[n:natrl(r)]
t32:=natposd(a0,natrlex(n)):posd(a0)
ap:=rpofpd(a0,t32):cut
t33:=natderp(a0,natrlex(n)):natrp(ap)
x0:=ntofrp(ap,t33):nat
t34:=isrpepd(ap,rpofnt(x0),isrpnt1(ap,t33)):eq"rp"(pdofrp(ap),pdofnt(x0))
t35:=treq"rp"(a0,pdofrp(ap),pdofnt(x0),eqpdrp1(a0,t32),t34):eq"rp"(a0,pdofnt(x0))
t36:=isin(r,rlofnt(x0),a0,pdofnt(x0),a0ir,innclass(pdofnt(x0)),t35):is(r,rlofnt(x0))
t37:=somei(nat,[x:nat]is(r,rlofnt(x)),x0,t36):image(nat,real,[x:nat]rlofnt(x),r)
-ivr2
r@[n:natrl(r)]
natimage:=realapp1(image(nat,real,[x:nat]rlofnt(x),r),[x:dif][t:inn(x,class(r))]t37".ivr2"(x,t,n)):image(nat,real,[x:nat]rlofnt(x),r)
r@[i:image(nat,real,[x:nat]rlofnt(x),r)]
+*ivr2
i"r"@[x:nat][j:is(r,rlofnt(x))]
t38:=isp1(real,[u:real]natrl(u),rlofnt(x),r,natrli(x),j):natrl(r)
-ivr2
i@imagenat:=someapp(nat,[u:nat]is(r,rlofnt(u)),i,natrl(r),[u:nat][v:is(r,rlofnt(u))]t38".ivr2"(u,v)):natrl(r)
r@[n:natrl(r)]
ntofrl:=soft(nat,real,[x:nat]rlofnt(x),t31".ivr2",r,natimage(r,n)):nat
@[r1:real][n:natrl(r1)][s1:real][m:natrl(s1)][i:is(r1,s1)]
isrlent:=isinv(nat,real,[x:nat]rlofnt(x),t31".ivr2",r1,natimage(r1,n),s1,natimage(s1,m),i):is"n"(ntofrl(r1,n),ntofrl(s1,m))
m@[i:is"n"(ntofrl(r1,n),ntofrl(s1,m))]
isrlint:=isinve(nat,real,[x:nat]rlofnt(x),t31".ivr2",r1,natimage(r1,n),s1,natimage(s1,m),i):is(r1,s1)
r@[n:natrl(r)]
isrlnt1:=ists1"e"(nat,real,[x:nat]rlofnt(x),t31".ivr2",r,natimage(r,n)):is(r,rlofnt(ntofrl(r,n)))
isrlnt2:=symis(real,r,rlofnt(ntofrl(r,n)),isrlnt1):is(rlofnt(ntofrl(r,n)),r)
@[x:nat]
+*ivr2
x"r"@xn:=soft(nat,real,[u:nat]rlofnt(u),t31,rlofnt(x),imagei(nat,real,[u:nat]rlofnt(u),x)):nat
t39:=isinv(nat,real,[u:nat]rlofnt(u),t31,rlofnt(x),imagei(nat,real,[u:nat]rlofnt(u),x),rlofnt(x),natimage(rlofnt(x),natrli(x)),refis(real,rlofnt(x))):is"n"(xn,ntofrl(rlofnt(x),natrli(x)))
-ivr2
x@isntrl1:=tris(nat,x,xn".ivr2",ntofrl(rlofnt(x),natrli(x)),isst1(nat,real,[u:nat]rlofnt(u),t31".ivr2",x),t39".ivr2"):is"n"(x,ntofrl(rlofnt(x),natrli(x)))
isntrl2:=symis(nat,x,ntofrl(rlofnt(x),natrli(x)),isntrl1):is"n"(ntofrl(rlofnt(x),natrli(x)),x)
r@intrl:=some"l"(dif,[x:dif]and(inn(x,class(r)),intd(x))):'prop'
a0ir@[i:intd(a0)]
+*ivr2
i@t40:=andi(inn(a0,class(r)),intd(a0),a0ir,i):and(inn(a0,class(r)),intd(a0))
-ivr2
i@intrlin:=somei(dif,[x:dif]and(inn(x,class(r)),intd(x)),a0,t40".ivr2"):intrl(r)
a0ir@[i:intrl(r)]
+*ivr2
i@[a:dif][b:and(inn(a,class(r)),intd(a))]
t41:=ande1(inn(a,class(r)),intd(a),b):inn(a,class(r))
t42:=ande2(inn(a,class(r)),intd(a),b):intd(a)
t43:=eqintd(a,a0,isex(r,r,a,a0,t41,a0ir,refis(real,r)),t42):intd(a0)
-ivr2
i@intrlex:=someapp(dif,[x:dif]and(inn(x,class(r)),intd(x)),i,intd(a0),[x:dif][t:and(inn(x,class(r)),intd(x))]t43".ivr2"(x,t)):intd(a0)
+*ivr2
a0ir@[n:natrl(r)]
t44:=natintd(a0,natrlex(n)):intd(a0)
t45:=intrlin(t44):intrl(r)
-ivr2
r@[n:natrl(r)]
natintrl:=realapp1(intrl(r),[x:dif][t:inn(x,class(r))]t45".ivr2"(x,t,n)):intrl(r)
+*ivr2
a0ir@[p:pos(r)][i:intrl(r)]
t46:=posintnatd(a0,posex(p),intrlex(i)):natd(a0)
t47:=natrlin(t46):natrl(r)
-ivr2
r@[p:pos(r)][i:intrl(r)]
posintnatrl:=realapp1(natrl(r),[x:dif][t:inn(x,class(r))]t47".ivr2"(x,t,p,i)):natrl(r)
+*ivr2
a0ir@[i2:is(r,0)]
t48:=intdi0(a0,0ex(i2)):intd(a0)
t49:=intrlin(t48):intrl(r)
-ivr2
r@[i:is(r,0)]
intrli0:=realapp1(intrl(r),[x:dif][t:inn(x,class(r))]t49".ivr2"(x,t,i)):intrl(r)
r0@[n:natrp(r0)]
remark6:=intrlin(pofrp(r0),pdofrp(r0),innclass(pdofrp(r0)),remark6"rp"(r0,n)):intrl(pofrp(r0))
remark7:=intrlin(nofrp(r0),ndofrp(r0),innclass(ndofrp(r0)),remark7"rp"(r0,n)):intrl(nofrp(r0))
+2r174
a0ir@[i:intrl(r)]
t1:=satzd174(a0,intrlex(i)):ratd(a0)
t2:=ratrlin(t1):ratrl(r)
-2r174
r@[i:intrl(r)]
satz174:=realapp1(ratrl(r),[x:dif][t:inn(x,class(r))]t2".2r174"(x,t,i)):ratrl(r)
@plusdr:=[x:dif][y:dif]realof(pd(x,y)):[x:dif][y:dif]real
+ivr3
[a:dif][b:dif][c:dif][d:dif][e:eq"rp"(a,b)][f:eq"rp"(c,d)]
t1:=isin(realof(pd(a,c)),realof(pd(b,d)),pd(a,c),pd(b,d),innclass(pd(a,c)),innclass(pd(b,d)),eqpd12(a,b,c,d,e,f)):is(<c><a>plusdr,<d><b>plusdr)
-ivr3
fplusdr:=[x:dif][y:dif][z:dif][v:dif][t:<y><x>eq][u:<v><z>eq]t1".ivr3"(x,y,z,v,t,u):fixf2(real,plusdr)
s@pl:=indreal2(real,plusdr,fplusdr,r,s):real
+*ivr3
b1is@t2:=isindreal2(real,plusdr,fplusdr,r,s,a1,b1,a1ir,b1is):is(realof(pd(a1,b1)),pl(r,s))
-ivr3
b1is@picp:=isp(real,[x:real]inn(pd(a1,b1),class(x)),realof(pd(a1,b1)),pl(r,s),innclass(pd(a1,b1)),t2".ivr3"):inn(pd(a1,b1),class(pl(r,s)))
t@[i:is(r,s)]
ispl1:=isf(real,real,[x:real]pl(x,t),r,s,i):is(pl(r,t),pl(s,t))
ispl2:=isf(real,real,[x:real]pl(t,x),r,s,i):is(pl(t,r),pl(t,s))
u@[i:is(r,s)][j:is(t,u)]
ispl12:=tris(real,pl(r,t),pl(s,t),pl(s,u),ispl1(i),ispl2(t,u,s,j)):is(pl(r,t),pl(s,u))
+3r175
b1is@t1:=satzd175(a1,b1):eq"rp"(pd(a1,b1),pd(b1,a1))
t2:=isin(pl(r,s),pl(s,r),pd(a1,b1),pd(b1,a1),picp,picp(s,r,b1,a1,b1is,a1ir),t1):is(pl(r,s),pl(s,r))
-3r175
s@satz175:=realapp2(is(pl(r,s),pl(s,r)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".3r175"(x,y,t,u)):is(pl(r,s),pl(s,r))
compl:=satz175:is(pl(r,s),pl(s,r))
+*ivr3
b1is@[i:is(r,0)]
t3:=pd01(a1,b1,0ex(r,a1,a1ir,i)):eq"rp"(pd(a1,b1),b1)
t4:=isin(pl(r,s),s,pd(a1,b1),b1,picp,b1is,t3):is(pl(r,s),s)
-ivr3
s@[i:is(r,0)]
pl01:=realapp2(is(pl(r,s),s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t4".ivr3"(x,y,t,u,i)):is(pl(r,s),s)
s@[i:is(s,0)]
pl02:=tris(real,pl(r,s),pl(s,r),r,compl,pl01(s,r,i)):is(pl(r,s),r)
+*ivr3
b1is@[p:pos(r)][q:pos(s)]
t5:=ppd(a1,b1,posex(r,a1,a1ir,p),posex(s,b1,b1is,q)):posd(pd(a1,b1))
t6:=posin(pl(r,s),pd(a1,b1),picp,t5):pos(pl(r,s))
-ivr3
s@[p:pos(r)][q:pos(s)]
pospl:=realapp2(pos(pl(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".ivr3"(x,y,t,u,p,q)):pos(pl(r,s))
+*ivr3
b1is@[n:neg(r)][o:neg(s)]
t7:=npd(a1,b1,negex(r,a1,a1ir,n),negex(s,b1,b1is,o)):negd(pd(a1,b1))
t8:=negin(pl(r,s),pd(a1,b1),picp,t7):neg(pl(r,s))
-ivr3
s@[n:neg(r)][o:neg(s)]
negpl:=realapp2(neg(pl(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t8".ivr3"(x,y,t,u,n,o)):neg(pl(r,s))
@m0dr:=[x:dif]realof(m0d(x)):[x:dif]real
+*ivr3
@[a:dif][b:dif][e:eq"rp"(a,b)]
t5a:=isin(realof(m0d(a)),realof(m0d(b)),m0d(a),m0d(b),innclass(m0d(a)),innclass(m0d(b)),eqm0d(a,b,e)):is(<a>m0dr,<b>m0dr)
-ivr3
@fm0dr:=[x:dif][y:dif][t:<y><x>eq]t5a".ivr3"(x,y,t):fixf(real,m0dr)
r@m0:=indreal(real,m0dr,fm0dr,r):real
+*ivr3
a0ir@t6a:=isindreal(real,m0dr,fm0dr,r,a0,a0ir):is(realof(m0d(a0)),m0(r))
-ivr3
a0ir@micm0:=isp(real,[x:real]inn(m0d(a0),class(x)),realof(m0d(a0)),m0(r),innclass(m0d(a0)),t6a".ivr3"):inn(m0d(a0),class(m0(r)))
s@[i:is(r,s)]
ism0:=isf(real,real,[x:real]m0(x),r,s,i):is(m0(r),m0(s))
+*ivr3
a0ir@[n:neg(r)]
t7a:=absnd(a0,negex(n)):eq"rp"(absd(a0),m0d(a0))
t8a:=isin(abs(r),m0(r),absd(a0),m0d(a0),aica,micm0,t7a):is(abs(r),m0(r))
-ivr3
r@[n:neg(r)]
absn:=realapp1(is(abs(r),m0(r)),[x:dif][t:inn(x,class(r))]t8a".ivr3"(x,t,n)):is(abs(r),m0(r))
+*ivr3
a0ir@[nn:not(neg(r))]
t9:=absnnd(a0,th3"l.imp"(negd(a0),neg(r),nn,[t:negd(a0)]negin(t))):eq"rp"(absd(a0),a0)
t10:=isin(abs(r),r,absd(a0),a0,aica,a0ir,t9):is(abs(r),r)
-ivr3
r@[nn:not(neg(r))]
absnn:=realapp1(is(abs(r),r),[x:dif][t:inn(x,class(r))]t10".ivr3"(x,t,nn)):is(abs(r),r)
r@[p:pos(r)]
absp:=absnn(r,pnotn(r,p)):is(abs(r),r)
r@[i:is(r,0)]
abs0:=tris(real,abs(r),r,0,absnn(r,0notn(r,i)),i):is(abs(r),0)
+3r176
a0ir@[p:pos(r)]
t1:=satzd176a(a0,posex(p)):negd(m0d(a0))
t2:=negin(m0(r),m0d(a0),micm0,t1):neg(m0(r))
-3r176
r@[p:pos(r)]
satz176a:=realapp1(neg(m0(r)),[x:dif][t:inn(x,class(r))]t2".3r176"(x,t,p)):neg(m0(r))
+*3r176
a0ir@[i:is(r,0)]
t3:=satzd176b(a0,0ex(i)):zero(m0d(a0))
t4:=0in(m0(r),m0d(a0),micm0,t3):is(m0(r),0)
-3r176
r@[i:is(r,0)]
satz176b:=realapp1(is(m0(r),0),[x:dif][t:inn(x,class(r))]t4".3r176"(x,t,i)):is(m0(r),0)
+*3r176
a0ir@[n:neg(r)]
t5:=satzd176c(a0,negex(n)):posd(m0d(a0))
t6:=posin(m0(r),m0d(a0),micm0,t5):pos(m0(r))
-3r176
r@[n:neg(r)]
satz176c:=realapp1(pos(m0(r)),[x:dif][t:inn(x,class(r))]t6".3r176"(x,t,n)):pos(m0(r))
+*3r176
a0ir@[n:neg(m0(r))]
t7:=satzd176d(a0,negex(m0(r),m0d(a0),micm0,n)):posd(a0)
t8:=posin(t7):pos(r)
-3r176
r@[n:neg(m0(r))]
satz176d:=realapp1(pos(r),[x:dif][t:inn(x,class(r))]t8".3r176"(x,t,n)):pos(r)
+*3r176
a0ir@[i:is(m0(r),0)]
t9:=satzd176e(a0,0ex(m0(r),m0d(a0),micm0,i)):zero(a0)
t10:=0in(t9):is(r,0)
-3r176
r@[i:is(m0(r),0)]
satz176e:=realapp1(is(r,0),[x:dif][t:inn(x,class(r))]t10".3r176"(x,t,i)):is(r,0)
+*3r176
a0ir@[p:pos(m0(r))]
t11:=satzd176f(a0,posex(m0(r),m0d(a0),micm0,p)):negd(a0)
t12:=negin(t11):neg(r)
-3r176
r@[p:pos(m0(r))]
satz176f:=realapp1(neg(r),[x:dif][t:inn(x,class(r))]t12".3r176"(x,t,p)):neg(r)
+3r177
a0ir@t1:=isin(m0(m0(r)),r,m0d(m0d(a0)),a0,micm0(m0(r),m0d(a0),micm0),a0ir,satzd177(a0)):is(m0(m0(r)),r)
-3r177
r@satz177:=realapp1(is(m0(m0(r)),r),[x:dif][t:inn(x,class(r))]t1".3r177"(x,t)):is(m0(m0(r)),r)
satz177a:=symis(real,m0(m0(r)),r,satz177):is(r,m0(m0(r)))
s@[i:is(r,m0(s))]
satz177b:=tris(real,m0(r),m0(m0(s)),s,ism0(r,m0(s),i),satz177(s)):is(m0(r),s)
satz177c:=symis(real,m0(r),s,satz177b):is(s,m0(r))
s@[i:is(m0(r),s)]
satz177d:=satz177c(s,r,symis(real,m0(r),s,i)):is(r,m0(s))
satz177e:=symis(real,r,m0(s),satz177d):is(m0(s),r)
+3r178
a0ir@t1:=isin(abs(m0(r)),abs(r),absd(m0d(a0)),absd(a0),aica(m0(r),m0d(a0),micm0),aica,satzd178(a0)):is(abs(m0(r)),abs(r))
-3r178
r@satz178:=realapp1(is(abs(m0(r)),abs(r)),[x:dif][t:inn(x,class(r))]t1".3r178"(x,t)):is(abs(m0(r)),abs(r))
satz178a:=symis(real,abs(m0(r)),abs(r),satz178):is(abs(r),abs(m0(r)))
+3r179
a0ir@t1:=0in(pl(r,m0(r)),pd(a0,m0d(a0)),picp(r,m0(r),a0,m0d(a0),a0ir,micm0),satzd179(a0)):is(pl(r,m0(r)),0)
-3r179
satz179:=realapp1(is(pl(r,m0(r)),0),[x:dif][t:inn(x,class(r))]t1".3r179"(x,t)):is(pl(r,m0(r)),0)
satz179a:=tris(real,pl(m0(r),r),pl(r,m0(r)),0,compl(m0(r),r),satz179):is(pl(m0(r),r),0)
+3r180
b1is@t1:=isin(m0(pl(r,s)),pl(m0(r),m0(s)),m0d(pd(a1,b1)),pd(m0d(a1),m0d(b1)),micm0(pl(r,s),pd(a1,b1),picp),picp(m0(r),m0(s),m0d(a1),m0d(b1),micm0(r,a1,a1ir),micm0(s,b1,b1is)),satzd180(a1,b1)):is(m0(pl(r,s)),pl(m0(r),m0(s)))
-3r180
s@satz180:=realapp2(is(m0(pl(r,s)),pl(m0(r),m0(s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t1".3r180"(x,y,t,u)):is(m0(pl(r,s)),pl(m0(r),m0(s)))
satz180a:=symis(real,m0(pl(r,s)),pl(m0(r),m0(s)),satz180):is(pl(m0(r),m0(s)),m0(pl(r,s)))
mn:=pl(r,m0(s)):real
b1is@micmn:=picp(r,m0(s),a1,m0d(b1),a1ir,micm0(s,b1,b1is)):inn(md(a1,b1),class(mn(r,s)))
t@[i:is(r,s)]
ismn1:=ispl1(r,s,m0(t),i):is(mn(r,t),mn(s,t))
ismn2:=ispl2(m0(r),m0(s),t,ism0(r,s,i)):is(mn(t,r),mn(t,s))
u@[i:is(r,s)][j:is(t,u)]
ismn12:=ispl12(r,s,m0(t),m0(u),i,ism0(t,u,j)):is(mn(r,t),mn(s,u))
s@satz181:=tr3is(real,m0(mn(r,s)),pl(m0(r),m0(m0(s))),pl(m0(r),s),mn(s,r),satz180(r,m0(s)),ispl2(m0(m0(s)),s,m0(r),satz177(s)),compl(m0(r),s)):is(m0(mn(r,s)),mn(s,r))
satz181a:=symis(real,m0(mn(s,r)),mn(r,s),satz181(s,r)):is(mn(r,s),m0(mn(s,r)))
+3r182
b1is@[p:pos(mn(r,s))]
t1:=satzd182a(a1,b1,posex(mn(r,s),md(a1,b1),micmn,p)):mored(a1,b1)
t2:=morein(t1):more(r,s)
-3r182
[p:pos(mn(r,s))]
satz182a:=realapp2(more(r,s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".3r182"(x,y,t,u,p)):more(r,s)
+*3r182
b1is@[i:is(mn(r,s),0)]
t3:=satzd182b(a1,b1,0ex(mn(r,s),md(a1,b1),micmn,i)):eq"rp"(a1,b1)
t4:=isin(t3):is(r,s)
-3r182
s@[i:is(mn(r,s),0)]
satz182b:=realapp2(is(r,s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t4".3r182"(x,y,t,u,i)):is(r,s)
+*3r182
b1is@[n:neg(mn(r,s))]
t5:=satzd182c(a1,b1,negex(mn(r,s),md(a1,b1),micmn,n)):lessd(a1,b1)
t6:=lessin(t5):less(r,s)
-3r182
s@[n:neg(mn(r,s))]
satz182c:=realapp2(less(r,s),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".3r182"(x,y,t,u,n)):less(r,s)
+*3r182
b1is@[m:more(r,s)]
t7:=satzd182d(a1,b1,moreex(m)):posd(md(a1,b1))
t8:=posin(mn(r,s),md(a1,b1),micmn,t7):pos(mn(r,s))
-3r182
s@[m:more(r,s)]
satz182d:=realapp2(pos(mn(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t8".3r182"(x,y,t,u,m)):pos(mn(r,s))
+*3r182
b1is@[i:is(r,s)]
t9:=satzd182e(a1,b1,isex(i)):zero(md(a1,b1))
t10:=0in(mn(r,s),md(a1,b1),micmn,t9):is(mn(r,s),0)
-3r182
s@[i:is(r,s)]
satz182e:=realapp2(is(mn(r,s),0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t10".3r182"(x,y,t,u,i)):is(mn(r,s),0)
+*3r182
b1is@[l:less(r,s)]
t11:=satzd182f(a1,b1,lessex(l)):negd(md(a1,b1))
t12:=negin(mn(r,s),md(a1,b1),micmn,t11):neg(mn(r,s))
-3r182
s@[l:less(r,s)]
satz182f:=realapp2(neg(mn(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t12".3r182"(x,y,t,u,l)):neg(mn(r,s))
+3r183
b1is@[m:more(r,s)]
t1:=satzd183a(a1,b1,moreex(m)):lessd(m0d(a1),m0d(b1))
t2:=lessin(m0(r),m0(s),m0d(a1),m0d(b1),micm0(r,a1,a1ir),micm0(s,b1,b1is),t1):less(m0(r),m0(s))
-3r183
s@[m:more(r,s)]
satz183a:=realapp2(less(m0(r),m0(s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".3r183"(x,y,t,u,m)):less(m0(r),m0(s))
s@[i:is(r,s)]
satz183b:=ism0(r,s,i):is(m0(r),m0(s))
+*3r183
b1is@[l:less(r,s)]
t3:=satzd183c(a1,b1,lessex(l)):mored(m0d(a1),m0d(b1))
t4:=morein(m0(r),m0(s),m0d(a1),m0d(b1),micm0(r,a1,a1ir),micm0(s,b1,b1is),t3):more(m0(r),m0(s))
-3r183
s@[l:less(r,s)]
satz183c:=realapp2(more(m0(r),m0(s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t4".3r183"(x,y,t,u,l)):more(m0(r),m0(s))
s@[l:less(m0(r),m0(s))]
satz183d:=ismore12(m0(m0(r)),r,m0(m0(s)),s,satz177(r),satz177(s),satz183c(m0(r),m0(s),l)):more(r,s)
s@[i:is(m0(r),m0(s))]
satz183e:=tr3is(real,r,m0(m0(r)),m0(m0(s)),s,satz177a(r),ism0(m0(r),m0(s),i),satz177(s)):is(r,s)
s@[m:more(m0(r),m0(s))]
satz183f:=isless12(m0(m0(r)),r,m0(m0(s)),s,satz177(r),satz177(s),satz183a(m0(r),m0(s),m)):less(r,s)
+3r184
t@prop1:=and3(pos(s),pos(t),is(r,mn(s,t))):'prop'
s@prop2:=some([x:real]prop1(x)):'prop'
r@prop3:=some([x:real]prop2(x)):'prop'
a0ir@[a:dif][b:dif]
prop1d:=and3(posd(a),posd(b),eq"rp"(a0,md(a,b))):'prop'
a@prop2d:=some"l"(dif,[x:dif]prop1d(x)):'prop'
[p2:prop2d(a)][b:dif][p1:prop1d(a,b)]
t1:=and3e1(posd(a),posd(b),eq"rp"(a0,md(a,b)),p1):posd(a)
t2:=and3e2(posd(a),posd(b),eq"rp"(a0,md(a,b)),p1):posd(b)
t3:=and3e3(posd(a),posd(b),eq"rp"(a0,md(a,b)),p1):eq"rp"(a0,md(a,b))
p2@ra:=realof(a):real
p1@rb:=realof(b):real
t4:=innclass(a):inn(a,class(ra))
t5:=innclass(b):inn(b,class(rb))
t6:=isin(r,mn(ra,rb),a0,md(a,b),a0ir,micmn(ra,rb,a,b,t4,t5),t3):is(r,mn(ra,rb))
t7:=and3i(pos(ra),pos(rb),is(r,mn(ra,rb)),posin(ra,a,t4,t1),posin(rb,b,t5,t2),t6):prop1(ra,rb)
t8:=somei(real,[x:real]prop1(ra,x),rb,t7):prop2(ra)
p2@t9:=someapp(dif,[x:dif]prop1d(a,x),p2,prop2(ra),[x:dif][t:prop1d(a,x)]t8(x,t)):prop2(ra)
t10:=somei(real,[x:real]prop2(x),ra,t9):prop3
a0ir@t11:=someapp(dif,[x:dif]prop2d(x),satzd184(a0),prop3,[x:dif][t:prop2d(x)]t10(x,t)):prop3
-3r184
r@satz184:=realapp1(prop3".3r184",[x:dif][t:inn(x,class(r))]t11".3r184"(x,t)):some([x:real]some([y:real]and3(pos(x),pos(y),is(r,mn(x,y)))))
u@[a3:dif][b3:dif][c3:dif][d3:dif][a3ir:inn(a3,class(r))][b3is:inn(b3,class(s))][c3it:inn(c3,class(t))][d3iu:inn(d3,class(u))]
+3r185
t1:=satzd185(a3,b3,c3,d3):eq"rp"(pd(md(a3,b3),md(c3,d3)),md(pd(a3,c3),pd(b3,d3)))
t2:=isin(pl(mn(r,s),mn(t,u)),mn(pl(r,t),pl(s,u)),pd(md(a3,b3),md(c3,d3)),md(pd(a3,c3),pd(b3,d3)),picp(mn(r,s),mn(t,u),md(a3,b3),md(c3,d3),micmn(r,s,a3,b3,a3ir,b3is),micmn(t,u,c3,d3,c3it,d3iu)),micmn(pl(r,t),pl(s,u),pd(a3,c3),pd(b3,d3),picp(r,t,a3,c3,a3ir,c3it),picp(s,u,b3,d3,b3is,d3iu)),t1):is(pl(mn(r,s),mn(t,u)),mn(pl(r,t),pl(s,u)))
-3r185
u@satz185:=realapp4(is(pl(mn(r,s),mn(t,u)),mn(pl(r,t),pl(s,u))),[x:dif][y:dif][z:dif][v:dif][xi:inn(x,class(r))][yi:inn(y,class(s))][zi:inn(z,class(t))][vi:inn(v,class(u))]t2".3r185"(x,y,z,v,xi,yi,zi,vi)):is(pl(mn(r,s),mn(t,u)),mn(pl(r,t),pl(s,u)))
+3r186
c2it@t1:=satzd186(a2,b2,c2):eq"rp"(pd(pd(a2,b2),c2),pd(a2,pd(b2,c2)))
t2:=isin(pl(pl(r,s),t),pl(r,pl(s,t)),pd(pd(a2,b2),c2),pd(a2,pd(b2,c2)),picp(pl(r,s),t,pd(a2,b2),c2,picp(r,s,a2,b2,a2ir,b2is),c2it),picp(r,pl(s,t),a2,pd(b2,c2),a2ir,picp(s,t,b2,c2,b2is,c2it)),t1):is(pl(pl(r,s),t),pl(r,pl(s,t)))
-3r186
t@satz186:=realapp3(is(pl(pl(r,s),t),pl(r,pl(s,t))),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".3r186"(x,y,z,u,v,w)):is(pl(pl(r,s),t),pl(r,pl(s,t)))
asspl1:=satz186:is(pl(pl(r,s),t),pl(r,pl(s,t)))
asspl2:=symis(real,pl(pl(r,s),t),pl(r,pl(s,t)),satz186):is(pl(r,pl(s,t)),pl(pl(r,s),t))
s@plmn:=tris(real,pl(mn(r,s),s),pl(r,pl(m0(s),s)),r,asspl1(r,m0(s),s),pl02(r,pl(m0(s),s),satz179a(s))):is(pl(mn(r,s),s),r)
mnpl:=tris(real,mn(pl(r,s),s),pl(r,pl(s,m0(s))),r,asspl1(r,s,m0(s)),pl02(r,pl(s,m0(s)),satz179(s))):is(mn(pl(r,s),s),r)
satz187a:=tris(real,pl(s,mn(r,s)),pl(mn(r,s),s),r,compl(s,mn(r,s)),plmn):is(pl(s,mn(r,s)),r)
satz187b:=somei(real,[x:real]is(pl(s,x),r),mn(r,s),satz187a):some([x:real]is(pl(s,x),r))
[x:real][i:is(pl(s,x),r)]
satz187c:=tris(real,mn(r,s),mn(pl(x,s),s),x,ismn1(r,pl(x,s),s,tris1(real,r,pl(x,s),pl(s,x),i,compl(s,x))),mnpl(x,s)):is(mn(r,s),x)
satz187d:=symis(real,mn(r,s),x,satz187c):is(x,mn(r,s))
x@[i:is(pl(x,s),r)]
satz187e:=satz187c(tris(real,pl(s,x),pl(x,s),r,compl(s,x),i)):is(mn(r,s),x)
satz187f:=symis(real,mn(r,s),x,satz187e):is(x,mn(r,s))
+3r187
s@[x:real][y:real][i:is(pl(s,x),r)][j:is(pl(s,y),r)]
t1:=tris1(real,x,y,mn(r,s),satz187c(x,i),satz187c(y,j)):is(x,y)
s@t2:=[x:real][y:real][t:is(pl(s,x),r)][u:is(pl(s,y),r)]t1(x,y,t,u):amone(real,[x:real]is(pl(s,x),r))
-3r187
s@satz187:=onei(real,[x:real]is(pl(s,x),r),t2".3r187",satz187b):one([x:real]is(pl(s,x),r))
+3r188
c2it@[m:more(pl(r,t),pl(s,t))]
t1:=satzd188a(a2,b2,c2,moreex(pl(r,t),pl(s,t),pd(a2,c2),pd(b2,c2),picp(r,t,a2,c2,a2ir,c2it),picp(s,t,b2,c2,b2is,c2it),m)):mored(a2,b2)
t2:=morein(r,s,a2,b2,a2ir,b2is,t1):more(r,s)
-3r188
t@[m:more(pl(r,t),pl(s,t))]
satz188a:=realapp3(more(r,s),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".3r188"(x,y,z,u,v,w,m)):more(r,s)
+*3r188
c2it@[i:is(pl(r,t),pl(s,t))]
t3:=satzd188b(a2,b2,c2,isex(pl(r,t),pl(s,t),pd(a2,c2),pd(b2,c2),picp(r,t,a2,c2,a2ir,c2it),picp(s,t,b2,c2,b2is,c2it),i)):eq"rp"(a2,b2)
t4:=isin(r,s,a2,b2,a2ir,b2is,t3):is(r,s)
-3r188
t@[i:is(pl(r,t),pl(s,t))]
satz188b:=realapp3(is(r,s),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t4".3r188"(x,y,z,u,v,w,i)):is(r,s)
+*3r188
c2it@[l:less(pl(r,t),pl(s,t))]
t5:=satzd188c(a2,b2,c2,lessex(pl(r,t),pl(s,t),pd(a2,c2),pd(b2,c2),picp(r,t,a2,c2,a2ir,c2it),picp(s,t,b2,c2,b2is,c2it),l)):lessd(a2,b2)
t6:=lessin(r,s,a2,b2,a2ir,b2is,t5):less(r,s)
-3r188
t@[l:less(pl(r,t),pl(s,t))]
satz188c:=realapp3(less(r,s),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t6".3r188"(x,y,z,u,v,w,l)):less(r,s)
+*3r188
c2it@[m:more(r,s)]
t7:=satzd188d(a2,b2,c2,moreex(r,s,a2,b2,a2ir,b2is,m)):mored(pd(a2,c2),pd(b2,c2))
t8:=morein(pl(r,t),pl(s,t),pd(a2,c2),pd(b2,c2),picp(r,t,a2,c2,a2ir,c2it),picp(s,t,b2,c2,b2is,c2it),t7):more(pl(r,t),pl(s,t))
-3r188
t@[m:more(r,s)]
satz188d:=realapp3(more(pl(r,t),pl(s,t)),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t8".3r188"(x,y,z,u,v,w,m)):more(pl(r,t),pl(s,t))
t@[i:is(r,s)]
satz188e:=ispl1(r,s,t,i):is(pl(r,t),pl(s,t))
+*3r188
c2it@[l:less(r,s)]
t9:=satzd188f(a2,b2,c2,lessex(r,s,a2,b2,a2ir,b2is,l)):lessd(pd(a2,c2),pd(b2,c2))
t10:=lessin(pl(r,t),pl(s,t),pd(a2,c2),pd(b2,c2),picp(r,t,a2,c2,a2ir,c2it),picp(s,t,b2,c2,b2is,c2it),t9):less(pl(r,t),pl(s,t))
-3r188
t@[l:less(r,s)]
satz188f:=realapp3(less(pl(r,t),pl(s,t)),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t10".3r188"(x,y,z,u,v,w,l)):less(pl(r,t),pl(s,t))
t@[m:more(pl(t,r),pl(t,s))]
satz188g:=satz188a(ismore12(pl(t,r),pl(r,t),pl(t,s),pl(s,t),compl(t,r),compl(t,s),m)):more(r,s)
t@[i:is(pl(t,r),pl(t,s))]
satz188h:=satz188b(tr3is(real,pl(r,t),pl(t,r),pl(t,s),pl(s,t),compl(r,t),i,compl(t,s))):is(r,s)
t@[l:less(pl(t,r),pl(t,s))]
satz188j:=satz188c(isless12(pl(t,r),pl(r,t),pl(t,s),pl(s,t),compl(t,r),compl(t,s),l)):less(r,s)
t@[m:more(r,s)]
satz188k:=ismore12(pl(r,t),pl(t,r),pl(s,t),pl(t,s),compl(r,t),compl(s,t),satz188d(m)):more(pl(t,r),pl(t,s))
t@[i:is(r,s)]
satz188l:=ispl2(r,s,t,i):is(pl(t,r),pl(t,s))
t@[l:less(r,s)]
satz188m:=isless12(pl(r,t),pl(t,r),pl(s,t),pl(t,s),compl(r,t),compl(s,t),satz188f(l)):less(pl(t,r),pl(t,s))
u@[i:is(r,s)][m:more(t,u)]
satz188n:=ismore2(pl(r,u),pl(s,u),pl(r,t),ispl1(r,s,u,i),satz188k(t,u,r,m)):more(pl(r,t),pl(s,u))
satz188o:=ismore12(pl(r,t),pl(t,r),pl(s,u),pl(u,s),compl(r,t),compl(s,u),satz188n):more(pl(t,r),pl(u,s))
i@[l:less(t,u)]
satz188p:=isless2(pl(r,u),pl(s,u),pl(r,t),ispl1(r,s,u,i),satz188m(t,u,r,l)):less(pl(r,t),pl(s,u))
satz188q:=isless12(pl(r,t),pl(t,r),pl(s,u),pl(u,s),compl(r,t),compl(s,u),satz188p):less(pl(t,r),pl(u,s))
u@[m:more(r,s)][n:more(t,u)]
satz189:=trmore(pl(r,t),pl(s,t),pl(s,u),satz188d(m),satz188k(t,u,s,n)):more(pl(r,t),pl(s,u))
u@[l:less(r,s)][k:less(t,u)]
satz189a:=lemma1(pl(s,u),pl(r,t),satz189(s,r,u,t,lemma2(r,s,l),lemma2(t,u,k))):less(pl(r,t),pl(s,u))
u@[m:moreis(r,s)][n:more(t,u)]
satz190a:=orapp(more(r,s),is(r,s),more(pl(r,t),pl(s,u)),m,[v:more(r,s)]satz189(v,n),[v:is(r,s)]satz188n(v,n)):more(pl(r,t),pl(s,u))
u@[m:more(r,s)][n:moreis(t,u)]
satz190b:=ismore12(pl(t,r),pl(r,t),pl(u,s),pl(s,u),compl(t,r),compl(u,s),satz190a(t,u,r,s,n,m)):more(pl(r,t),pl(s,u))
u@[l:lessis(r,s)][k:less(t,u)]
satz190c:=lemma1(pl(s,u),pl(r,t),satz190a(s,r,u,t,satz168b(l),lemma2(t,u,k))):less(pl(r,t),pl(s,u))
u@[l:less(r,s)][k:lessis(t,u)]
satz190d:=lemma1(pl(s,u),pl(r,t),satz190b(s,r,u,t,lemma2(l),satz168b(t,u,k))):less(pl(r,t),pl(s,u))
+3r191
d3iu@[m:moreis(r,s)][n:moreis(t,u)]
t1:=satzd191(a3,b3,c3,d3,moreisex(r,s,a3,b3,a3ir,b3is,m),moreisex(t,u,c3,d3,c3it,d3iu,n)):moreq(pd(a3,c3),pd(b3,d3))
t2:=moreisin(pl(r,t),pl(s,u),pd(a3,c3),pd(b3,d3),picp(r,t,a3,c3,a3ir,c3it),picp(s,u,b3,d3,b3is,d3iu),t1):moreis(pl(r,t),pl(s,u))
-3r191
u@[m:moreis(r,s)][n:moreis(t,u)]
satz191:=realapp4(moreis(pl(r,t),pl(s,u)),[x:dif][y:dif][z:dif][v:dif][xi:inn(x,class(r))][yi:inn(y,class(s))][zi:inn(z,class(t))][vi:inn(v,class(u))]t2".3r191"(x,y,z,v,xi,yi,zi,vi,m,n)):moreis(pl(r,t),pl(s,u))
u@[l:lessis(r,s)][k:lessis(t,u)]
satz191a:=satz168a(pl(s,u),pl(r,t),satz191(s,r,u,t,satz168b(l),satz168b(t,u,k))):lessis(pl(r,t),pl(s,u))
@timesdr:=[x:dif][y:dif]realof(td(x,y)):[x:dif][y:dif]real
+ivr4
[a:dif][b:dif][c:dif][d:dif][e:eq"rp"(a,b)][f:eq"rp"(c,d)]
t1:=isin(realof(td(a,c)),realof(td(b,d)),td(a,c),td(b,d),innclass(td(a,c)),innclass(td(b,d)),eqtd12(a,b,c,d,e,f)):is(<c><a>timesdr,<d><b>timesdr)
-ivr4
ftimesdr:=[x:dif][y:dif][z:dif][v:dif][t:<y><x>eq][u:<v><z>eq]t1".ivr4"(x,y,z,v,t,u):fixf2(real,timesdr)
s@ts:=indreal2(real,timesdr,ftimesdr,r,s):real
+*ivr4
b1is@t2:=isindreal2(real,timesdr,ftimesdr,r,s,a1,b1,a1ir,b1is):is(realof(td(a1,b1)),ts(r,s))
-ivr4
b1is@tict:=isp(real,[x:real]inn(td(a1,b1),class(x)),realof(td(a1,b1)),ts(r,s),innclass(td(a1,b1)),t2".ivr4"):inn(td(a1,b1),class(ts(r,s)))
t@[i:is(r,s)]
ists1:=isf(real,real,[x:real]ts(x,t),r,s,i):is(ts(r,t),ts(s,t))
ists2:=isf(real,real,[x:real]ts(t,x),r,s,i):is(ts(t,r),ts(t,s))
u@[i:is(r,s)][j:is(t,u)]
ists12:=tris(real,ts(r,t),ts(s,t),ts(s,u),ists1(i),ists2(t,u,s,j)):is(ts(r,t),ts(s,u))
+4r192
b1is@[i:is(r,0)]
t1:=satzd192a(a1,b1,0ex(r,a1,a1ir,i)):zero(td(a1,b1))
t2:=0in(ts(r,s),td(a1,b1),tict,t1):is(ts(r,s),0)
-4r192
s@[i:is(r,0)]
satz192a:=realapp2(is(ts(r,s),0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".4r192"(x,y,t,u,i)):is(ts(r,s),0)
+*4r192
b1is@[i:is(s,0)]
t3:=satzd192b(a1,b1,0ex(s,b1,b1is,i)):zero(td(a1,b1))
t4:=0in(ts(r,s),td(a1,b1),tict,t3):is(ts(r,s),0)
-4r192
s@[i:is(s,0)]
satz192b:=realapp2(is(ts(r,s),0),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t4".4r192"(x,y,t,u,i)):is(ts(r,s),0)
+*4r192
b1is@[i:is(ts(r,s),0)]
t5:=satzd192c(a1,b1,0ex(ts(r,s),td(a1,b1),tict,i)):or(zero(a1),zero(b1))
t6:=th9"l.or"(zero(a1),zero(b1),is(r,0),is(s,0),t5,[t:zero(a1)]0in(r,a1,a1ir,t),[t:zero(b1)]0in(s,b1,b1is,t)):or(is(r,0),is(s,0))
-4r192
s@[i:is(ts(r,s),0)]
satz192c:=realapp2(or(is(r,0),is(s,0)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".4r192"(x,y,t,u,i)):or(is(r,0),is(s,0))
s@[n:nis(r,0)][o:nis(s,0)]
satz192d:=th3"l.imp"(is(ts(r,s),0),or(is(r,0),is(s,0)),th3"l.or"(is(r,0),is(s,0),n,o),[t:is(ts(r,s),0)]satz192c(t)):nis(ts(r,s),0)
s@[i:is(r,0)]
ts01:=satz192a(i):is(ts(r,s),0)
s@[i:is(s,0)]
ts02:=satz192b(i):is(ts(r,s),0)
+4r193
b1is@t1:=satzd193(a1,b1):eq"rp"(absd(td(a1,b1)),td(absd(a1),absd(b1)))
t2:=isin(abs(ts(r,s)),ts(abs(r),abs(s)),absd(td(a1,b1)),td(absd(a1),absd(b1)),aica(ts(r,s),td(a1,b1),tict),tict(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is)),t1):is(abs(ts(r,s)),ts(abs(r),abs(s)))
-4r193
s@satz193:=realapp2(is(abs(ts(r,s)),ts(abs(r),abs(s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".4r193"(x,y,t,u)):is(abs(ts(r,s)),ts(abs(r),abs(s)))
satz193a:=symis(real,abs(ts(r,s)),ts(abs(r),abs(s)),satz193):is(ts(abs(r),abs(s)),abs(ts(r,s)))
+4r194
b1is@t1:=satzd194(a1,b1):eq"rp"(td(a1,b1),td(b1,a1))
t2:=isin(ts(r,s),ts(s,r),td(a1,b1),td(b1,a1),tict,tict(s,r,b1,a1,b1is,a1ir),t1):is(ts(r,s),ts(s,r))
-4r194
satz194:=realapp2(is(ts(r,s),ts(s,r)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".4r194"(x,y,t,u)):is(ts(r,s),ts(s,r))
comts:=satz194:is(ts(r,s),ts(s,r))
@1rl:=realof(1df):real
pos1:=posin(1rl,1df,innclass(1df),posdirp(1rp)):pos(1rl)
natrl1:=natrli(1):natrl(1rl)
intrl1:=natintrl(1rl,natrl1):intrl(1rl)
+4r195
a0ir@t1:=satzd195(a0):eq"rp"(td(a0,1df),a0)
t2:=isin(ts(r,1rl),r,td(a0,1df),a0,tict(r,1rl,a0,1df,a0ir,innclass(1df)),a0ir,t1):is(ts(r,1rl),r)
-4r195
r@satz195:=realapp1(is(ts(r,1rl),r),[x:dif][t:inn(x,class(r))]t2".4r195"(x,t)):is(ts(r,1rl),r)
satz195a:=symis(real,ts(r,1rl),r,satz195):is(r,ts(r,1rl))
satz195b:=tris(real,ts(1rl,r),ts(r,1rl),r,comts(1rl,r),satz195):is(ts(1rl,r),r)
satz195c:=symis(real,ts(1rl,r),r,satz195b):is(r,ts(1rl,r))
s@[p:pos(r)][q:pos(s)]
satz196a:=symis(real,ts(abs(r),abs(s)),ts(r,s),ists12(abs(r),r,abs(s),s,absp(r,p),absp(s,q))):is(ts(r,s),ts(abs(r),abs(s)))
+4r196
b1is@[n:neg(r)][o:neg(s)]
t1:=satzd196b(a1,b1,negex(r,a1,a1ir,n),negex(s,b1,b1is,o)):eq"rp"(td(a1,b1),td(absd(a1),absd(b1)))
t2:=isin(ts(r,s),ts(abs(r),abs(s)),td(a1,b1),td(absd(a1),absd(b1)),tict,tict(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is)),t1):is(ts(r,s),ts(abs(r),abs(s)))
-4r196
s@[n:neg(r)][o:neg(s)]
satz196b:=realapp2(is(ts(r,s),ts(abs(r),abs(s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".4r196"(x,y,t,u,n,o)):is(ts(r,s),ts(abs(r),abs(s)))
+*4r196
b1is@[p:pos(r)][n:neg(s)]
t1a:=satzd196c(a1,b1,posex(r,a1,a1ir,p),negex(s,b1,b1is,n)):eq"rp"(td(a1,b1),m0d(td(absd(a1),absd(b1))))
t2a:=isin(ts(r,s),m0(ts(abs(r),abs(s))),td(a1,b1),m0d(td(absd(a1),absd(b1))),tict,micm0(ts(abs(r),abs(s)),td(absd(a1),absd(b1)),tict(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is))),t1a):is(ts(r,s),m0(ts(abs(r),abs(s))))
-4r196
s@[p:pos(r)][n:neg(s)]
satz196c:=realapp2(is(ts(r,s),m0(ts(abs(r),abs(s)))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2a".4r196"(x,y,t,u,p,n)):is(ts(r,s),m0(ts(abs(r),abs(s))))
s@[n:neg(r)][p:pos(s)]
satz196d:=tr3is(real,ts(r,s),ts(s,r),m0(ts(abs(s),abs(r))),m0(ts(abs(r),abs(s))),comts(r,s),satz196c(s,r,p,n),ism0(ts(abs(s),abs(r)),ts(abs(r),abs(s)),comts(abs(s),abs(r)))):is(ts(r,s),m0(ts(abs(r),abs(s))))
+*4r196
a0ir@[n:not(is(r,0))]
t3:=th3"l.imp"(zero(a0),is(r,0),n,[t:zero(a0)]0in(t)):not(zero(a0))
b1is@[n:not(is(r,0))][o:not(is(s,0))][i:is(ts(r,s),ts(abs(r),abs(s)))]
t4:=satzd196e(a1,b1,t3(r,a1,a1ir,n),t3(s,b1,b1is,o),isex(ts(r,s),ts(abs(r),abs(s)),td(a1,b1),td(absd(a1),absd(b1)),tict,tict(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is)),i)):or(and(posd(a1),posd(b1)),and(negd(a1),negd(b1)))
[a:and(posd(a1),posd(b1))]
t5:=andi(pos(r),pos(s),posin(r,a1,a1ir,ande1(posd(a1),posd(b1),a)),posin(s,b1,b1is,ande2(posd(a1),posd(b1),a))):and(pos(r),pos(s))
i@[a:and(negd(a1),negd(b1))]
t6:=andi(neg(r),neg(s),negin(r,a1,a1ir,ande1(negd(a1),negd(b1),a)),negin(s,b1,b1is,ande2(negd(a1),negd(b1),a))):and(neg(r),neg(s))
i@t7:=th9"l.or"(and(posd(a1),posd(b1)),and(negd(a1),negd(b1)),and(pos(r),pos(s)),and(neg(r),neg(s)),t4,[t:and(posd(a1),posd(b1))]t5(t),[t:and(negd(a1),negd(b1))]t6(t)):or(and(pos(r),pos(s)),and(neg(r),neg(s)))
-4r196
s@[n:not(is(r,0))][o:not(is(s,0))][i:is(ts(r,s),ts(abs(r),abs(s)))]
satz196e:=realapp2(or(and(pos(r),pos(s)),and(neg(r),neg(s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t7".4r196"(x,y,t,u,n,o,i)):or(and(pos(r),pos(s)),and(neg(r),neg(s)))
+*4r196
o@[i:is(ts(r,s),m0(ts(abs(r),abs(s))))]
t8:=satzd196f(a1,b1,t3(r,a1,a1ir,n),t3(s,b1,b1is,o),isex(ts(r,s),m0(ts(abs(r),abs(s))),td(a1,b1),m0d(td(absd(a1),absd(b1))),tict,micm0(ts(abs(r),abs(s)),td(absd(a1),absd(b1)),tict(abs(r),abs(s),absd(a1),absd(b1),aica(r,a1,a1ir),aica(s,b1,b1is))),i)):or(and(posd(a1),negd(b1)),and(negd(a1),posd(b1)))
[a:and(posd(a1),negd(b1))]
t9:=andi(pos(r),neg(s),posin(r,a1,a1ir,ande1(posd(a1),negd(b1),a)),negin(s,b1,b1is,ande2(posd(a1),negd(b1),a))):and(pos(r),neg(s))
i@[a:and(negd(a1),posd(b1))]
t10:=andi(neg(r),pos(s),negin(r,a1,a1ir,ande1(negd(a1),posd(b1),a)),posin(s,b1,b1is,ande2(negd(a1),posd(b1),a))):and(neg(r),pos(s))
i@t11:=th9"l.or"(and(posd(a1),negd(b1)),and(negd(a1),posd(b1)),and(pos(r),neg(s)),and(neg(r),pos(s)),t8,[t:and(posd(a1),negd(b1))]t9(t),[t:and(negd(a1),posd(b1))]t10(t)):or(and(pos(r),neg(s)),and(neg(r),pos(s)))
-4r196
o@[i:is(ts(r,s),m0(ts(abs(r),abs(s))))]
satz196f:=realapp2(or(and(pos(r),neg(s)),and(neg(r),pos(s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t11".4r196"(x,y,t,u,n,o,i)):or(and(pos(r),neg(s)),and(neg(r),pos(s)))
s@[p:pos(ts(r,s))]
+*4r196
p"r"@t12:=th3"l.imp"(is(r,0),is(ts(r,s),0),pnot0(ts(r,s),p),[t:is(r,0)]ts01(r,s,t)):nis(r,0)
t13:=th3"l.imp"(is(s,0),is(ts(r,s),0),pnot0(ts(r,s),p),[t:is(s,0)]ts02(r,s,t)):nis(s,0)
t14:=tris1(real,ts(r,s),ts(abs(r),abs(s)),abs(ts(r,s)),absp(ts(r,s),p),satz193(r,s)):is(ts(r,s),ts(abs(r),abs(s)))
-4r196
p@satz196g:=satz196e(t12".4r196",t13".4r196",t14".4r196"):or(and(pos(r),pos(s)),and(neg(r),neg(s)))
s@[n:neg(ts(r,s))]
+*4r196
n"r"@t15:=th3"l.imp"(is(r,0),is(ts(r,s),0),nnot0(ts(r,s),n),[t:is(r,0)]ts01(r,s,t)):nis(r,0)
t16:=th3"l.imp"(is(s,0),is(ts(r,s),0),nnot0(ts(r,s),n),[t:is(s,0)]ts02(r,s,t)):nis(s,0)
t17:=satz177c(ts(abs(r),abs(s)),ts(r,s),tris(real,ts(abs(r),abs(s)),abs(ts(r,s)),m0(ts(r,s)),satz193a(r,s),absn(ts(r,s),n))):is(ts(r,s),m0(ts(abs(r),abs(s))))
-4r196
n@satz196h:=satz196f(t15".4r196",t16".4r196",t17".4r196"):or(and(pos(r),neg(s)),and(neg(r),pos(s)))
+4r197
b1is@t1:=satzd197a(a1,b1):eq"rp"(td(m0d(a1),b1),m0d(td(a1,b1)))
t2:=isin(ts(m0(r),s),m0(ts(r,s)),td(m0d(a1),b1),m0d(td(a1,b1)),tict(m0(r),s,m0d(a1),b1,micm0(r,a1,a1ir),b1is),micm0(ts(r,s),td(a1,b1),tict),t1):is(ts(m0(r),s),m0(ts(r,s)))
-4r197
s@satz197a:=realapp2(is(ts(m0(r),s),m0(ts(r,s))),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t2".4r197"(x,y,t,u)):is(ts(m0(r),s),m0(ts(r,s)))
satz197b:=tr3is(real,ts(r,m0(s)),ts(m0(s),r),m0(ts(s,r)),m0(ts(r,s)),comts(r,m0(s)),satz197a(s,r),ism0(ts(s,r),ts(r,s),comts(s,r))):is(ts(r,m0(s)),m0(ts(r,s)))
satz197c:=tris2(real,ts(m0(r),s),ts(r,m0(s)),m0(ts(r,s)),satz197a,satz197b):is(ts(m0(r),s),ts(r,m0(s)))
satz197d:=symis(real,ts(m0(r),s),ts(r,m0(s)),satz197c):is(ts(r,m0(s)),ts(m0(r),s))
satz197e:=symis(real,ts(m0(r),s),m0(ts(r,s)),satz197a):is(m0(ts(r,s)),ts(m0(r),s))
satz197f:=symis(real,ts(r,m0(s)),m0(ts(r,s)),satz197b):is(m0(ts(r,s)),ts(r,m0(s)))
satz198:=tris(real,ts(m0(r),m0(s)),ts(r,m0(m0(s))),ts(r,s),satz197c(r,m0(s)),ists2(m0(m0(s)),s,r,satz177(s))):is(ts(m0(r),m0(s)),ts(r,s))
satz198a:=symis(real,ts(m0(r),m0(s)),ts(r,s),satz198):is(ts(r,s),ts(m0(r),m0(s)))
+*ivr4
b1is@[p:pos(r)][q:pos(s)]
t3:=ptdpp(a1,b1,posex(r,a1,a1ir,p),posex(s,b1,b1is,q)):posd(td(a1,b1))
t4:=posin(ts(r,s),td(a1,b1),tict,t3):pos(ts(r,s))
-ivr4
s@[p:pos(r)][q:pos(s)]
postspp:=realapp2(pos(ts(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t4".ivr4"(x,y,t,u,p,q)):pos(ts(r,s))
+*ivr4
p@[n:neg(s)]
t5:=ntdpn(a1,b1,posex(r,a1,a1ir,p),negex(s,b1,b1is,n)):negd(td(a1,b1))
t6:=negin(ts(r,s),td(a1,b1),tict,t5):neg(ts(r,s))
-ivr4
s@[p:pos(r)][n:neg(s)]
negtspn:=realapp2(neg(ts(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".ivr4"(x,y,t,u,p,n)):neg(ts(r,s))
s@[n:neg(r)][p:pos(s)]
negtsnp:=isneg(ts(s,r),ts(r,s),comts(s,r),negtspn(s,r,p,n)):neg(ts(r,s))
s@[n:neg(r)][o:neg(s)]
postsnn:=ispos(ts(m0(r),m0(s)),ts(r,s),satz198,postspp(m0(r),m0(s),satz176c(r,n),satz176c(s,o))):pos(ts(r,s))
r@[n:nis(r,0)]
possq:=rapp(r,pos(ts(r,r)),[t:pos(r)]postspp(r,r,t,t),th2"l.imp"(is(r,0),pos(ts(r,r)),n),[t:neg(r)]postsnn(r,r,t,t)):pos(ts(r,r))
r@nnegsq:=th1"l.imp"(is(r,0),not(neg(ts(r,r))),[t:is(r,0)]0notn(ts(r,r),satz192a(r,r,t)),[t:nis(r,0)]pnotn(ts(r,r),possq(r,t))):not(neg(ts(r,r)))
+4r199
c2it@t1:=satzd199(a2,b2,c2):eq"rp"(td(td(a2,b2),c2),td(a2,td(b2,c2)))
t2:=isin(ts(ts(r,s),t),ts(r,ts(s,t)),td(td(a2,b2),c2),td(a2,td(b2,c2)),tict(ts(r,s),t,td(a2,b2),c2,tict(r,s,a2,b2,a2ir,b2is),c2it),tict(r,ts(s,t),a2,td(b2,c2),a2ir,tict(s,t,b2,c2,b2is,c2it)),t1):is(ts(ts(r,s),t),ts(r,ts(s,t)))
-4r199
t@satz199:=realapp3(is(ts(ts(r,s),t),ts(r,ts(s,t))),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".4r199"(x,y,z,u,v,w)):is(ts(ts(r,s),t),ts(r,ts(s,t)))
assts1:=satz199:is(ts(ts(r,s),t),ts(r,ts(s,t)))
assts2:=symis(real,ts(ts(r,s),t),ts(r,ts(s,t)),satz199):is(ts(r,ts(s,t)),ts(ts(r,s),t))
+4r201
c2it@t1:=satzd201(a2,b2,c2):eq"rp"(td(a2,pd(b2,c2)),pd(td(a2,b2),td(a2,c2)))
t2:=isin(ts(r,pl(s,t)),pl(ts(r,s),ts(r,t)),td(a2,pd(b2,c2)),pd(td(a2,b2),td(a2,c2)),tict(r,pl(s,t),a2,pd(b2,c2),a2ir,picp(s,t,b2,c2,b2is,c2it)),picp(ts(r,s),ts(r,t),td(a2,b2),td(a2,c2),tict(r,s,a2,b2,a2ir,b2is),tict(r,t,a2,c2,a2ir,c2it)),t1):is(ts(r,pl(s,t)),pl(ts(r,s),ts(r,t)))
-4r201
satz201:=realapp3(is(ts(r,pl(s,t)),pl(ts(r,s),ts(r,t))),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".4r201"(x,y,z,u,v,w)):is(ts(r,pl(s,t)),pl(ts(r,s),ts(r,t)))
disttp1:=tr3is(real,ts(pl(r,s),t),ts(t,pl(r,s)),pl(ts(t,r),ts(t,s)),pl(ts(r,t),ts(s,t)),comts(pl(r,s),t),satz201(t,r,s),ispl12(ts(t,r),ts(r,t),ts(t,s),ts(s,t),comts(t,r),comts(t,s))):is(ts(pl(r,s),t),pl(ts(r,t),ts(s,t)))
disttp2:=satz201:is(ts(r,pl(s,t)),pl(ts(r,s),ts(r,t)))
distpt1:=symis(real,ts(pl(r,s),t),pl(ts(r,t),ts(s,t)),disttp1):is(pl(ts(r,t),ts(s,t)),ts(pl(r,s),t))
distpt2:=symis(real,ts(r,pl(s,t)),pl(ts(r,s),ts(r,t)),disttp2):is(pl(ts(r,s),ts(r,t)),ts(r,pl(s,t)))
satz202:=tris(real,ts(r,mn(s,t)),pl(ts(r,s),ts(r,m0(t))),mn(ts(r,s),ts(r,t)),disttp2(r,s,m0(t)),ispl2(ts(r,m0(t)),m0(ts(r,t)),ts(r,s),satz197b(r,t))):is(ts(r,mn(s,t)),mn(ts(r,s),ts(r,t)))
disttm1:=tris(real,ts(mn(r,s),t),pl(ts(r,t),ts(m0(s),t)),mn(ts(r,t),ts(s,t)),disttp1(r,m0(s),t),ispl2(ts(m0(s),t),m0(ts(s,t)),ts(r,t),satz197a(s,t))):is(ts(mn(r,s),t),mn(ts(r,t),ts(s,t)))
disttm2:=satz202:is(ts(r,mn(s,t)),mn(ts(r,s),ts(r,t)))
distmt1:=symis(real,ts(mn(r,s),t),mn(ts(r,t),ts(s,t)),disttm1):is(mn(ts(r,t),ts(s,t)),ts(mn(r,s),t))
distmt2:=symis(real,ts(r,mn(s,t)),mn(ts(r,s),ts(r,t)),disttm2):is(mn(ts(r,s),ts(r,t)),ts(r,mn(s,t)))
satz200:=satz202:is(ts(r,mn(s,t)),mn(ts(r,s),ts(r,t)))
+4r203
c2it@[m:more(r,s)][p:pos(t)]
t1:=satzd203a(a2,b2,c2,moreex(r,s,a2,b2,a2ir,b2is,m),posex(t,c2,c2it,p)):mored(td(a2,c2),td(b2,c2))
t2:=morein(ts(r,t),ts(s,t),td(a2,c2),td(b2,c2),tict(r,t,a2,c2,a2ir,c2it),tict(s,t,b2,c2,b2is,c2it),t1):more(ts(r,t),ts(s,t))
-4r203
[m:more(r,s)][p:pos(t)]
satz203a:=realapp3(more(ts(r,t),ts(s,t)),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t2".4r203"(x,y,z,u,v,w,m,p)):more(ts(r,t),ts(s,t))
m@[i:is(t,0)]
satz203b:=tris2(real,ts(r,t),ts(s,t),0,ts02(r,t,i),ts02(s,t,i)):is(ts(r,t),ts(s,t))
+*4r203
m@[n:neg(t)]
t3:=satzd203c(a2,b2,c2,moreex(r,s,a2,b2,a2ir,b2is,m),negex(t,c2,c2it,n)):lessd(td(a2,c2),td(b2,c2))
t4:=lessin(ts(r,t),ts(s,t),td(a2,c2),td(b2,c2),tict(r,t,a2,c2,a2ir,c2it),tict(s,t,b2,c2,b2is,c2it),t3):less(ts(r,t),ts(s,t))
-4r203
m@[n:neg(t)]
satz203c:=realapp3(less(ts(r,t),ts(s,t)),[x:dif][y:dif][z:dif][u:inn(x,class(r))][v:inn(y,class(s))][w:inn(z,class(t))]t4".4r203"(x,y,z,u,v,w,m,n)):less(ts(r,t),ts(s,t))
p@satz203d:=ismore12(ts(r,t),ts(t,r),ts(s,t),ts(t,s),comts(r,t),comts(s,t),satz203a):more(ts(t,r),ts(t,s))
i@satz203e:=tris2(real,ts(t,r),ts(t,s),0,ts01(t,r,i),ts01(t,s,i)):is(ts(t,r),ts(t,s))
n@satz203f:=isless12(ts(r,t),ts(t,r),ts(s,t),ts(t,s),comts(r,t),comts(s,t),satz203c):less(ts(t,r),ts(t,s))
t@[l:less(r,s)][p:pos(t)]
satz203g:=lemma1(ts(s,t),ts(r,t),satz203a(s,r,t,lemma2(r,s,l),p)):less(ts(r,t),ts(s,t))
l@[i:is(t,0)]
satz203h:=tris2(real,ts(r,t),ts(s,t),0,ts02(r,t,i),ts02(s,t,i)):is(ts(r,t),ts(s,t))
l@[n:neg(t)]
satz203j:=lemma2(ts(s,t),ts(r,t),satz203c(s,r,t,lemma2(r,s,l),n)):more(ts(r,t),ts(s,t))
p@satz203k:=lemma1(ts(t,s),ts(t,r),satz203d(s,r,t,lemma2(r,s,l),p)):less(ts(t,r),ts(t,s))
i@satz203l:=tris2(real,ts(t,r),ts(t,s),0,ts01(t,r,i),ts01(t,s,i)):is(ts(t,r),ts(t,s))
n@satz203m:=lemma2(ts(t,s),ts(t,r),satz203f(s,r,t,lemma2(r,s,l),n)):more(ts(t,r),ts(t,s))
+4r204
a0ir@[n1:nis(r,0)]
t1:=th3"l.imp"(zero(a0),is(r,0),n1,[t:zero(a0)]0in(t)):not(zero(a0))
d3iu@[n1:nis(s,0)][i:is(ts(s,t),r)][j:is(ts(s,u),r)]
t2:=satzd204b(a3,b3,t1(s,b3,b3is,n1),c3,d3,isex(ts(s,t),r,td(b3,c3),a3,tict(s,t,b3,c3,b3is,c3it),a3ir,i),isex(ts(s,u),r,td(b3,d3),a3,tict(s,u,b3,d3,b3is,d3iu),a3ir,j)):eq"rp"(c3,d3)
t3:=isin(t,u,c3,d3,c3it,d3iu,t2):is(t,u)
-4r204
s@[n:nis(s,0)][x:real][y:real][i:is(ts(s,x),r)][j:is(ts(s,y),r)]
satz204b:=realapp4(x,y,is(x,y),[z:dif][u:dif][v:dif][w:dif][zi:inn(z,class(r))][ui:inn(u,class(s))][vi:inn(v,class(x))][wi:inn(w,class(y))]t3".4r204"(x,y,z,u,v,w,zi,ui,vi,wi,n,i,j)):is(x,y)
+*4r204
b1is@[n1:nis(s,0)]
t4:=satzd204a(a1,b1,t1(s,b1,b1is,n1)):some"l"(dif,[x:dif]eq"rp"(td(b1,x),a1))
[a:dif][e:eq"rp"(td(b1,a),a1)]
ar:=realof(a):real
t5:=isin(ts(s,ar),r,td(b1,a),a1,tict(s,ar,b1,a,b1is,innclass(a)),a1ir,e):is(ts(s,ar),r)
t6:=somei(real,[x:real]is(ts(s,x),r),ar,t5):some([x:real]is(ts(s,x),r))
n1@t7:=someapp(dif,[x:dif]eq"rp"(td(b1,x),a1),t4,some([x:real]is(ts(s,x),r)),[x:dif][t:eq"rp"(td(b1,x),a1)]t6(x,t)):some([x:real]is(ts(s,x),r))
-4r204
n@satz204a:=realapp2(some([x:real]is(ts(s,x),r)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t7".4r204"(x,y,t,u,n)):some([x:real]is(ts(s,x),r))
satz204:=onei(real,[x:real]is(ts(s,x),r),[x:real][y:real][t:is(ts(s,x),r)][u:is(ts(s,y),r)]satz204b(x,y,t,u),satz204a):one([x:real]is(ts(s,x),r))
ov:=ind(real,[x:real]is(ts(s,x),r),satz204):real
satz204c:=oneax(real,[x:real]is(ts(s,x),r),satz204):is(ts(s,ov(r,s,n)),r)
satz204d:=symis(real,ts(s,ov(r,s,n)),r,satz204c):is(r,ts(s,ov(r,s,n)))
satz204e:=tris(real,ts(ov(r,s,n),s),ts(s,ov(r,s,n)),r,comts(ov(r,s,n),s),satz204c):is(ts(ov(r,s,n),s),r)
satz204f:=symis(real,ts(ov(r,s,n),s),r,satz204e):is(r,ts(ov(r,s,n),s))
s@[x:real][n:nis(s,0)][i:is(ts(s,x),r)]
satz204g:=satz204b(n,x,ov(r,s,n),i,satz204c(n)):is(x,ov(r,s,n))
s@[n:nis(s,0)][p:pos(r)][q:pos(s)]
+*4r204
n@ros:=ov(r,s,n):real
p@t8:=ispos(r,ts(s,ros),satz204d(n),p):pos(ts(s,ros))
q@t9:=th1"l.and"(neg(s),neg(ros),pnotn(s,q)):not(and(neg(s),neg(ros)))
t10:=ore1(and(pos(s),pos(ros)),and(neg(s),neg(ros)),satz196g(s,ros,t8),t9):and(pos(s),pos(ros))
-4r204
q@posovpp:=ande2(pos(s),pos(ov(r,s,n)),t10".4r204"):pos(ov(r,s,n))
p@[m:neg(s)]
+*4r204
m@t11:=th1"l.and"(pos(s),pos(ros),nnotp(s,m)):not(and(pos(s),pos(ros)))
t12:=ore2(and(pos(s),pos(ros)),and(neg(s),neg(ros)),satz196g(s,ros,t8),t11):and(neg(s),neg(ros))
-4r204
m@negovpn:=ande2(neg(s),neg(ov(r,s,n)),t12".4r204"):neg(ov(r,s,n))
n@[m:neg(r)][p:pos(s)]
+*4r204
m@t13:=isneg(r,ts(s,ros),satz204d(n),m):neg(ts(s,ros))
p@t14:=th1"l.and"(neg(s),pos(ros),pnotn(s,p)):not(and(neg(s),pos(ros)))
t15:=ore1(and(pos(s),neg(ros)),and(neg(s),pos(ros)),satz196h(s,ros,t13),t14):and(pos(s),neg(ros))
-4r204
p@negovnp:=ande2(pos(s),neg(ov(r,s,n)),t15".4r204"):neg(ov(r,s,n))
m@[l:neg(s)]
+*4r204
l@t16:=th1"l.and"(pos(s),neg(ros),nnotp(s,l)):not(and(pos(s),neg(ros)))
t17:=ore2(and(pos(s),neg(ros)),and(neg(s),pos(ros)),satz196h(s,ros,t13),t16):and(neg(s),pos(ros))
-4r204
l@posovnn:=ande2(neg(s),pos(ov(r,s,n)),t17".4r204"):pos(ov(r,s,n))
@[r0:cut][s0:cut][m:more"rp"(r0,s0)]
morerpep:=morein(pofrp(r0),pofrp(s0),pdofrp(r0),pdofrp(s0),innclass(pdofrp(r0)),innclass(pdofrp(s0)),morerpepd(r0,s0,m)):more(pofrp(r0),pofrp(s0))
s0@[m:more(pofrp(r0),pofrp(s0))]
morerpip:=morerpipd(r0,s0,moreex(pofrp(r0),pofrp(s0),pdofrp(r0),pdofrp(s0),innclass(pdofrp(r0)),innclass(pdofrp(s0)),m)):more"rp"(r0,s0)
s0@[l:less"rp"(r0,s0)]
lessrpep:=lemma1(pofrp(s0),pofrp(r0),morerpep(s0,r0,satz122(r0,s0,l))):less(pofrp(r0),pofrp(s0))
s0@[l:less(pofrp(r0),pofrp(s0))]
lessrpip:=satz121(s0,r0,morerpip(s0,r0,lemma2(pofrp(r0),pofrp(s0),l))):less"rp"(r0,s0)
s@[p:pos(r)][q:pos(s)][m:more(r,s)]
q@[m:more(r,s)]
+ivr5
t1:=ismore12(r,pofrp(rpofp(r,p)),s,pofrp(rpofp(s,q)),isprp1(r,p),isprp1(s,q),m):more(pofrp(rpofp(r,p)),pofrp(rpofp(s,q)))
-ivr5
moreperp:=morerpip(rpofp(r,p),rpofp(s,q),t1".ivr5"):more"rp"(rpofp(r,p),rpofp(s,q))
q@[m:more"rp"(rpofp(r,p),rpofp(s,q))]
+*ivr5
m@t2:=morerpep(rpofp(r,p),rpofp(s,q),m):more(pofrp(rpofp(r,p)),pofrp(rpofp(s,q)))
-ivr5
m@morepirp:=ismore12(pofrp(rpofp(r,p)),r,pofrp(rpofp(s,q)),s,isprp2(r,p),isprp2(s,q),t2".ivr5"):more(r,s)
q@[l:less(r,s)]
lessperp:=satz121(rpofp(s,q),rpofp(r,p),moreperp(s,r,q,p,lemma2(r,s,l))):less"rp"(rpofp(r,p),rpofp(s,q))
q@[l:less"rp"(rpofp(r,p),rpofp(s,q))]
lesspirp:=lemma1(s,r,morepirp(s,r,q,p,satz122(rpofp(r,p),rpofp(s,q),l))):less(r,s)
r@s01:=setof(real,[x:real]lessis(x,r)):set(real)
s02:=setof(real,[x:real]more(x,r)):set(real)
+5r205
s@[n:not(in(s,s01))]
t1:=th3"l.imp"(lessis(s,r),in(s,s01),n,[t:lessis(s,r)]estii(real,[x:real]lessis(x,r),s,t)):not(lessis(s,r))
t2:=estii(real,[x:real]more(x,r),s,satz167k(s,r,t1)):in(s,s02)
-5r205
vb00:=[x:real][t:not(in(x,s01))]t2".5r205"(x,t):all([x:real]or(in(x,s01),in(x,s02)))
+*5r205
r@t3:=estii(real,[x:real]lessis(x,r),r,lessisi2(r,r,refis(real,r))):in(r,s01)
-5r205
r@vb01a:=nonemptyi(real,s01,r,t3".5r205"):nonempty(real,s01)
+*5r205
r@t4:=ismore2(pl(r,0),r,pl(r,1rl),pl02(r,0,refis(real,0)),satz188k(1rl,0,r,satz169a(1rl,pos1))):more(pl(r,1rl),r)
t5:=estii(real,[x:real]more(x,r),pl(r,1rl),t4):in(pl(r,1rl),s02)
-5r205
r@vb01b:=nonemptyi(real,s02,pl(r,1rl),t5".5r205"):nonempty(real,s02)
+*5r205
s@[i:in(s,s01)][t:real][j:in(t,s02)]
t6:=satz172a(s,r,t,estie(real,[x:real]lessis(x,r),s,i),lemma1(t,r,estie(real,[x:real]more(x,r),t,j))):less(s,t)
-5r205
r@vb02:=[x:real][t:in(x,s01)][y:real][u:in(y,s02)]t6".5r205"(x,t,y,u):all([x:real][t:in(x,s01)]all([y:real][u:in(y,s02)]less(x,y)))
s@[l:less(s,r)]
vb03a:=estii(real,[x:real]lessis(x,r),s,lessisi1(s,r,l)):in(s,s01)
s@[m:more(s,r)]
vb03b:=estii(real,[x:real]more(x,r),s,m):in(s,s02)
r@s11:=setof(real,[x:real]less(x,r)):set(real)
s12:=setof(real,[x:real]moreis(x,r)):set(real)
+*5r205
s@[n:not(in(s,s11))]
t7:=th3"l.imp"(less(s,r),in(s,s11),n,[t:less(s,r)]estii(real,[x:real]less(x,r),s,t)):not(less(s,r))
t8:=estii(real,[x:real]moreis(x,r),s,satz167f(s,r,t7)):in(s,s12)
-5r205
r@vb10:=[x:real][t:not(in(x,s11))]t8".5r205"(x,t):all([x:real]or(in(x,s11),in(x,s12)))
+*5r205
r@t9:=isless2(pl(r,0),r,mn(r,1rl),pl02(r,0,refis(real,0)),satz188m(m0(1rl),0,r,satz169c(m0(1rl),satz176a(1rl,pos1)))):less(mn(r,1rl),r)
t10:=estii(real,[x:real]less(x,r),mn(r,1rl),t9):in(mn(r,1rl),s11)
-5r205
r@vb11a:=nonemptyi(real,s11,mn(r,1rl),t10".5r205"):nonempty(real,s11)
+*5r205
r@t11:=estii(real,[x:real]moreis(x,r),r,moreisi2(r,r,refis(real,r))):in(r,s12)
-5r205
r@vb11b:=nonemptyi(real,s12,r,t11".5r205"):nonempty(real,s12)
+*5r205
s@[i:in(s,s11)][t:real][j:in(t,s12)]
t12:=satz172b(s,r,t,estie(real,[x:real]less(x,r),s,i),satz168a(t,r,estie(real,[x:real]moreis(x,r),t,j))):less(s,t)
-5r205
r@vb12:=[x:real][t:in(x,s11)][y:real][u:in(y,s12)]t12".5r205"(x,t,y,u):all([x:real][t:in(x,s11)]all([y:real][u:in(y,s12)]less(x,y)))
s@[l:less(s,r)]
vb13a:=estii(real,[x:real]less(x,r),s,l):in(s,s11)
s@[m:more(s,r)]
vb13b:=estii(real,[x:real]moreis(x,r),s,moreisi1(s,r,m)):in(s,s12)
@2rl:=pl(1rl,1rl):real
pos2:=pospl(1rl,1rl,pos1,pos1):pos(2rl)
half:=ov(1rl,2rl,pnot0(2rl,pos2)):real
poshalf:=posovpp(1rl,2rl,pnot0(2rl,pos2),pos1,pos2):pos(half)
+*ivr5
r@t3:=tris(real,pl(r,r),pl(ts(1rl,r),ts(1rl,r)),ts(2rl,r),ispl12(r,ts(1rl,r),r,ts(1rl,r),satz195c(r),satz195c(r)),distpt1(1rl,1rl,r)):is(pl(r,r),ts(2rl,r))
t4:=tr4is(real,ts(half,pl(r,r)),ts(half,ts(2rl,r)),ts(ts(half,2rl),r),ts(1rl,r),r,ists2(pl(r,r),ts(2rl,r),half,t3),assts2(half,2rl,r),ists1(ts(half,2rl),1rl,r,satz204e(1rl,2rl,pnot0(2rl,pos2))),satz195b(r)):is(ts(half,pl(r,r)),r)
-ivr5
s@[l:less(r,s)]
+*ivr5
l@t5:=satz203k(pl(r,r),pl(r,s),half,satz188m(r,s,r,l),poshalf):less(ts(half,pl(r,r)),ts(half,pl(r,s)))
-ivr5
l@lemma3:=isless1(ts(half,pl(r,r)),r,ts(half,pl(r,s)),t4".ivr5",t5".ivr5"):less(r,ts(half,pl(r,s)))
+*ivr5
l@t6:=satz203k(pl(r,s),pl(s,s),half,satz188f(r,s,s,l),poshalf):less(ts(half,pl(r,s)),ts(half,pl(s,s)))
-ivr5
l@lemma4:=isless2(ts(half,pl(s,s)),s,ts(half,pl(r,s)),t4".ivr5"(s),t6".ivr5"):less(ts(half,pl(r,s)),s)
[p:pos(r)]
lemma5:=satz169b(s,trmore(s,r,0,lemma2(r,s,l),satz169a(r,p))):pos(s)
@[s1:set(real)][s2:set(real)][p0:all([x:real]or(in(x,s1),in(x,s2)))][p1a:nonempty(real,s1)][p1b:nonempty(real,s2)][p2:all([x:real][t:in(x,s1)]all([y:real][u:in(y,s2)]less(x,y)))]
+*5r205
s2@[r:real]
prop1:=all([x:real][t:less(x,r)]in(x,s1)):'prop'
prop2:=all([x:real][t:more(x,r)]in(x,s2)):'prop'
prop3:=and(prop1,prop2):'prop'
p2@[x:real][y:real][px:prop3(x)][py:prop3(y)][l:less(x,y)]
mxy:=ts(half,pl(x,y)):real
t13:=lemma2(x,mxy,lemma3(x,y,l)):more(mxy,x)
t14:=lemma4(x,y,l):less(mxy,y)
t15:=<t14><mxy>ande1(prop1(y),prop2(y),py):in(mxy,s1)
t16:=<t13><mxy>ande2(prop1(x),prop2(x),px):in(mxy,s2)
t17:=<t16><mxy><t15><mxy>p2:less(mxy,mxy)
t18:=<refis(real,mxy)>ec3e31(is(mxy,mxy),more(mxy,mxy),less(mxy,mxy),satz167b(mxy,mxy),t17):con
py@t19:=[t:less(x,y)]t18(t):not(less(x,y))
t20:=[t:more(x,y)]t18(y,x,py,px,lemma1(x,y,t)):not(more(x,y))
t21:=or3e1(is(x,y),more(x,y),less(x,y),satz167a(x,y),t20,t19):is(x,y)
p2@t22:=[x:real][y:real][t:prop3(x)][u:prop3(y)]t21(x,y,t,u):amone(real,[x:real]prop3(x))
[case1:some([x:real]and(pos(x),in(x,s1)))][r:real][a:and(pos(r),in(r,s1))]
t23:=ande1(pos(r),in(r,s1),a):pos(r)
t24:=ande2(pos(r),in(r,s1),a):in(r,s1)
sc1:=setof(cut,[x:cut]in(pofrp(x),s1)):set(cut)
sc2:=setof(cut,[x:cut]in(pofrp(x),s2)):set(cut)
[r0:cut][i:in(pofrp(r0),s1)]
t25:=estii(cut,[x:cut]in(pofrp(x),s1),r0,i):in"rp"(r0,sc1)
r0@[i:in"rp"(r0,sc1)]
t26:=estie(cut,[x:cut]in(pofrp(x),s1),r0,i):in(pofrp(r0),s1)
r0@[i:in(pofrp(r0),s2)]
t27:=estii(cut,[x:cut]in(pofrp(x),s2),r0,i):in"rp"(r0,sc2)
r0@[i:in"rp"(r0,sc2)]
t28:=estie(cut,[x:cut]in(pofrp(x),s2),r0,i):in(pofrp(r0),s2)
r0@t29:=th9"l.or"(in(pofrp(r0),s1),in(pofrp(r0),s2),in"rp"(r0,sc1),in"rp"(r0,sc2),<pofrp(r0)>p0,[t:in(pofrp(r0),s1)]t25(t),[t:in(pofrp(r0),s2)]t27(t)):or(in"rp"(r0,sc1),in"rp"(r0,sc2))
a@pr1:=rpofp(r,t23):cut
t30:=isp(real,[x:real]in(x,s1),r,pofrp(pr1),t24,isprp1(r,t23)):in(pofrp(pr1),s1)
t31:=nonemptyi(cut,sc1,pr1,t25(pr1,t30)):nonempty(cut,sc1)
[s:real][i:in(s,s2)]
t32:=<i><s><t24><r>p2:less(r,s)
t33:=lemma5(r,s,t32,t23):pos(s)
ps1:=rpofp(s,t33):cut
t34:=isp(real,[x:real]in(x,s2),s,pofrp(ps1),i,isprp1(s,t33)):in(pofrp(ps1),s2)
t35:=nonemptyi(cut,sc2,ps1,t27(ps1,t34)):nonempty(cut,sc2)
a@t36:=nonemptyapp(real,s2,p1b,nonempty(cut,sc2),[x:real][t:in(x,s2)]t35(x,t)):nonempty(cut,sc2)
r0@[i:in"rp"(r0,sc1)][s0:cut][j:in"rp"(s0,sc2)]
t37:=<t28(s0,j)><pofrp(s0)><t26(r0,i)><pofrp(r0)>p2:less(pofrp(r0),pofrp(s0))
t38:=lessrpip(r0,s0,t37):less"rp"(r0,s0)
a@stc:=schnitt(sc1,sc2,[x:cut]t29(x),t31,t36,[x:cut][t:in"rp"(x,sc1)][y:cut][u:in"rp"(y,sc2)]t38(x,t,y,u)):cut
t39:=satzp205a(sc1,sc2,[x:cut]t29(x),t31,t36,[x:cut][t:in"rp"(x,sc1)][y:cut][u:in"rp"(y,sc2)]t38(x,t,y,u)):all"rp"([x:cut][t:less"rp"(x,stc)]in"rp"(x,sc1))
t40:=satzp205b(sc1,sc2,[x:cut]t29(x),t31,t36,[x:cut][t:in"rp"(x,sc1)][y:cut][u:in"rp"(y,sc2)]t38(x,t,y,u)):all"rp"([x:cut][t:more"rp"(x,stc)]in"rp"(x,sc2))
stp:=pofrp(stc):real
t41:=posi(stc):pos(stp)
[s:real][l:less(s,stp)][p:pos(s)]
ps2:=rpofp(s,p):cut
t42:=lessrpip(ps2,stc,isless1(s,pofrp(ps2),stp,isprp1(s,p),l)):less"rp"(ps2,stc)
t43:=<t42><ps2>t39:in"rp"(ps2,sc1)
t44:=isp(real,[x:real]in(x,s1),pofrp(ps2),s,t26(ps2,t43),isprp2(s,p)):in(s,s1)
l@[n:not(pos(s))][i:in(s,s2)]
t45:=<i><s><t24><r>p2:less(r,s)
t46:=<lemma5(r,s,t45,t23)>n:con
n@t47:=ore1(in(s,s1),in(s,s2),<s>p0,[t:in(s,s2)]t46(t)):in(s,s1)
l@t48:=th1"l.imp"(pos(s),in(s,s1),[t:pos(s)]t44(t),[t:not(pos(s))]t47(t)):in(s,s1)
s@[m:more(s,stp)]
t49:=lemma5(stp,s,lemma1(s,stp,m),t41):pos(s)
ps3:=rpofp(s,t49):cut
t50:=morerpip(ps3,stc,ismore1(s,pofrp(ps3),stp,isprp1(s,t49),m)):more"rp"(ps3,stc)
t51:=<t50><ps3>t40:in"rp"(ps3,sc2)
t52:=isp(real,[x:real]in(x,s2),pofrp(ps3),s,t28(ps3,t51),isprp2(s,t49)):in(s,s2)
a@t53:=andi(prop1(stp),prop2(stp),[x:real][t:less(x,stp)]t48(x,t),[x:real][t:more(x,stp)]t52(x,t)):prop3(stp)
t54:=somei(real,[x:real]prop3(x),stp,t53):some([x:real]prop3(x))
case1@t55:=someapp(real,[x:real]and(pos(x),in(x,s1)),case1,some([x:real]prop3(x)),[x:real][t:and(pos(x),in(x,s1))]t54(x,t)):some([x:real]prop3(x))
p2@[case2:some([x:real]and(neg(x),in(x,s2)))]
sp1:=setof(real,[x:real]in(m0(x),s1)):set(real)
sp2:=setof(real,[x:real]in(m0(x),s2)):set(real)
[r:real][i:in(m0(r),s1)]
t56:=estii(real,[x:real]in(m0(x),s1),r,i):in(r,sp1)
r@[i:in(r,sp1)]
t57:=estie(real,[x:real]in(m0(x),s1),r,i):in(m0(r),s1)
r@[i:in(m0(r),s2)]
t58:=estii(real,[x:real]in(m0(x),s2),r,i):in(r,sp2)
r@[i:in(r,sp2)]
t59:=estie(real,[x:real]in(m0(x),s2),r,i):in(m0(r),s2)
r@t60:=comor(in(r,sp1),in(r,sp2),th9"l.or"(in(m0(r),s1),in(m0(r),s2),in(r,sp1),in(r,sp2),<m0(r)>p0,[t:in(m0(r),s1)]t56(t),[t:in(m0(r),s2)]t58(t))):or(in(r,sp2),in(r,sp1))
[i:in(r,s2)]
t61:=t58(m0(r),isp(real,[x:real]in(x,s2),r,m0(m0(r)),i,satz177a(r))):in(m0(r),sp2)
t62:=nonemptyi(real,sp2,m0(r),t61):nonempty(real,sp2)
case2@t63:=nonemptyapp(real,s2,p1b,nonempty(real,sp2),[x:real][t:in(x,s2)]t62(x,t)):nonempty(real,sp2)
r@[i:in(r,s1)]
t64:=t56(m0(r),isp(real,[x:real]in(x,s1),r,m0(m0(r)),i,satz177a(r))):in(m0(r),sp1)
t65:=nonemptyi(real,sp1,m0(r),t64):nonempty(real,sp1)
case2@t66:=nonemptyapp(real,s1,p1a,nonempty(real,sp1),[x:real][t:in(x,s1)]t65(x,t)):nonempty(real,sp1)
r@[i:in(r,sp2)][s:real][j:in(s,sp1)]
t67:=<t59(r,i)><m0(r)><t57(s,j)><m0(s)>p2:less(m0(s),m0(r))
t68:=lemma1(s,r,satz183d(s,r,t67)):less(r,s)
r@[a:and(neg(r),in(r,s2))]
t69:=satz176c(r,ande1(neg(r),in(r,s2),a)):pos(m0(r))
t70:=isp(real,[x:real]in(x,s2),r,m0(m0(r)),ande2(neg(r),in(r,s2),a),satz177a(r)):in(m0(m0(r)),s2)
t71:=andi(pos(m0(r)),in(m0(r),sp2),t69,t58(m0(r),t70)):and(pos(m0(r)),in(m0(r),sp2))
t72:=somei(real,[x:real]and(pos(x),in(x,sp2)),m0(r),t71):some([x:real]and(pos(x),in(x,sp2)))
case2@t73:=someapp(real,[x:real]and(neg(x),in(x,s2)),case2,some([x:real]and(pos(x),in(x,sp2))),[x:real][t:and(neg(x),in(x,s2))]t72(x,t)):some([x:real]and(pos(x),in(x,sp2)))
t74:=t55(sp2,sp1,[x:real]t60(x),t63,t66,[x:real][t:in(x,sp2)][y:real][u:in(y,sp1)]t68(x,t,y,u),t73):some([x:real]prop3(sp2,sp1,x))
[r:real][p:prop3(sp2,sp1,r)][s:real][l:less(s,m0(r))]
t75:=ismore2(m0(m0(r)),r,m0(s),satz177(r),satz183c(s,m0(r),l)):more(m0(s),r)
t76:=<t75><m0(s)>ande2(prop1(sp2,sp1,r),prop2(sp2,sp1,r),p):in(m0(s),sp1)
t77:=isp(real,[x:real]in(x,s1),m0(m0(s)),s,t57(m0(s),t76),satz177(s)):in(s,s1)
s@[m:more(s,m0(r))]
t78:=isless2(m0(m0(r)),r,m0(s),satz177(r),satz183a(s,m0(r),m)):less(m0(s),r)
t79:=<t78><m0(s)>ande1(prop1(sp2,sp1,r),prop2(sp2,sp1,r),p):in(m0(s),sp2)
t80:=isp(real,[x:real]in(x,s2),m0(m0(s)),s,t59(m0(s),t79),satz177(s)):in(s,s2)
p@t81:=andi(prop1(m0(r)),prop2(m0(r)),[x:real][t:less(x,m0(r))]t77(x,t),[x:real][t:more(x,m0(r))]t80(x,t)):prop3(m0(r))
t82:=somei(real,[x:real]prop3(x),m0(r),t81):some([x:real]prop3(x))
case2@t83:=someapp(real,[x:real]prop3(sp2,sp1,x),t74,some([x:real]prop3(x)),[x:real][t:prop3(sp2,sp1,x)]t82(x,t)):some([x:real]prop3(x))
p2@[notcase1:not(some([x:real]and(pos(x),in(x,s1))))][notcase2:not(some([x:real]and(neg(x),in(x,s2))))][r:real][l:less(r,0)]
t84:=th4"l.some"(real,[x:real]and(neg(x),in(x,s2)),notcase2,r):not(and(neg(r),in(r,s2)))
t85:=th3"l.and"(neg(r),in(r,s2),t84,satz169d(r,l)):not(in(r,s2))
t86:=ore1(in(r,s1),in(r,s2),<r>p0,t85):in(r,s1)
r@[m:more(r,0)]
t87:=th4"l.some"(real,[x:real]and(pos(x),in(x,s1)),notcase1,r):not(and(pos(r),in(r,s1)))
t88:=th3"l.and"(pos(r),in(r,s1),t87,satz169b(r,m)):not(in(r,s1))
t89:=ore2(in(r,s1),in(r,s2),<r>p0,t88):in(r,s2)
notcase2@t90:=andi(prop1(0),prop2(0),[x:real][t:less(x,0)]t86(x,t),[x:real][t:more(x,0)]t89(x,t)):prop3(0)
t91:=somei(real,[x:real]prop3(x),0,t90):some([x:real]prop3(x))
notcase1@t92:=th1"l.imp"(some([x:real]and(neg(x),in(x,s2))),some([x:real]prop3(x)),[t:some([x:real]and(neg(x),in(x,s2)))]t83(t),[t:not(some([x:real]and(neg(x),in(x,s2))))]t91(t)):some([x:real]prop3(x))
p2@t93:=th1"l.imp"(some([x:real]and(pos(x),in(x,s1))),some([x:real]prop3(x)),[t:some([x:real]and(pos(x),in(x,s1)))]t55(t),[t:not(some([x:real]and(pos(x),in(x,s1))))]t92(t)):some([x:real]prop3(x))
t94:=onei(real,[x:real]prop3(x),t22,t93):one([x:real]prop3(x))
-5r205
p2@satz205:=t94".5r205":one([x:real]and(all([y:real][t:less(y,x)]in(y,s1)),all([y:real][t:more(y,x)]in(y,s2))))
dedekind:=satz205:one([x:real]and(all([y:real][t:less(y,x)]in(y,s1)),all([y:real][t:more(y,x)]in(y,s2))))
schnitt:=ind(real,[x:real]prop3".5r205"(x),satz205):real
[r:real][l:less(r,schnitt)]
satz205a:=<l><r>ande1(prop1".5r205"(schnitt),prop2".5r205"(schnitt),oneax(real,[x:real]prop3".5r205"(x),satz205)):in(r,s1)
r@[m:more(r,schnitt)]
satz205b:=<m><r>ande2(prop1".5r205"(schnitt),prop2".5r205"(schnitt),oneax(real,[x:real]prop3".5r205"(x),satz205)):in(r,s2)
@[r0:cut][s0:cut]
+iva
dr:=pdofrp(r0):dif
ds:=pdofrp(s0):dif
t1:=lemmaivad1(r0,s0):eq"rp"(pd(dr,ds),pdofrp(pl"rp"(r0,s0)))
-iva
lemmaiva1:=isin(pl(pofrp(r0),pofrp(s0)),pofrp(pl"rp"(r0,s0)),pd(pdofrp(r0),pdofrp(s0)),pdofrp(pl"rp"(r0,s0)),picp(pofrp(r0),pofrp(s0),pdofrp(r0),pdofrp(s0),innclass(pdofrp(r0)),innclass(pdofrp(s0))),innclass(pdofrp(pl"rp"(r0,s0))),t1".iva"):is(pl(pofrp(r0),pofrp(s0)),pofrp(pl"rp"(r0,s0)))
+*iva
s0@t2:=lemmaivad2(r0,s0):eq"rp"(td(dr,ds),pdofrp(ts"rp"(r0,s0)))
-iva
s0@lemmaiva2:=isin(ts(pofrp(r0),pofrp(s0)),pofrp(ts"rp"(r0,s0)),td(pdofrp(r0),pdofrp(s0)),pdofrp(ts"rp"(r0,s0)),tict(pofrp(r0),pofrp(s0),pdofrp(r0),pdofrp(s0),innclass(pdofrp(r0)),innclass(pdofrp(s0))),innclass(pdofrp(ts"rp"(r0,s0))),t2".iva"):is(ts(pofrp(r0),pofrp(s0)),pofrp(ts"rp"(r0,s0)))
[m:more(pofrp(r0),pofrp(s0))]
+*iva
m@t3:=moreex(pofrp(r0),pofrp(s0),dr,ds,innclass(dr),innclass(ds),m):mored(dr,ds)
-iva
m@lemmaiva3:=lemmaivad3(r0,s0,t3".iva"):more"rp"(r0,s0)
s0@[m:more"rp"(r0,s0)]
+*iva
m@[l:less(pofrp(r0),pofrp(s0))]
t4:=satz121(s0,r0,lemmaiva3(s0,r0,lemma2(pofrp(r0),pofrp(s0),l))):less"rp"(r0,s0)
m@t5:=ec3e23(is"rp"(r0,s0),more"rp"(r0,s0),less"rp"(r0,s0),satz123b(r0,s0),m):not(less"rp"(r0,s0))
t6:=th3"l.imp"(less(pofrp(r0),pofrp(s0)),less"rp"(r0,s0),t5,[t:less(pofrp(r0),pofrp(s0))]t4(t)):not(less(pofrp(r0),pofrp(s0)))
t7:=ec3e21(is"rp"(r0,s0),more"rp"(r0,s0),less"rp"(r0,s0),satz123b(r0,s0),m):nis"rp"(r0,s0)
t8:=th3"l.imp"(is(pofrp(r0),pofrp(s0)),is"rp"(r0,s0),t7,[t:is(pofrp(r0),pofrp(s0))]isrpip(r0,s0,t)):nis(pofrp(r0),pofrp(s0))
-iva
m@lemmaiva4:=or3e2(is(pofrp(r0),pofrp(s0)),more(pofrp(r0),pofrp(s0)),less(pofrp(r0),pofrp(s0)),satz167a(pofrp(r0),pofrp(s0)),t6".iva",t8".iva"):more(pofrp(r0),pofrp(s0))
@[x:nat][y:nat][m:more(rlofnt(x),rlofnt(y))]
+*iva
m@t9:=lemmaiva3(rpofnt(x),rpofnt(y),m):more"rp"(rpofnt(x),rpofnt(y))
t10:=satz154d(rtofn(x),rtofn(y),t9):more"rt"(rtofn(x),rtofn(y))
t11:=moree"rt"(rtofn(x),rtofn(y),fr(x,1),fr(y,1),inclass"rt"(fr(x,1)),inclass"rt"(fr(y,1)),t10):moref(fr(x,1),fr(y,1))
-iva
m@lemmaiva5:=satz111a(x,y,t11".iva"):more"n"(x,y)
y@[m:more"n"(x,y)]
+*iva
m@t12:=satz111d(x,y,m):moref(fr(x,1),fr(y,1))
t13:=morei"rt"(rtofn(x),rtofn(y),fr(x,1),fr(y,1),inclass"rt"(fr(x,1)),inclass"rt"(fr(y,1)),t12):more"rt"(rtofn(x),rtofn(y))
t14:=satz154a(rtofn(x),rtofn(y),t13):more"rp"(rpofnt(x),rpofnt(y))
-iva
m@lemmaiva6:=lemmaiva4(rpofnt(x),rpofnt(y),t14".iva"):more(rlofnt(x),rlofnt(y))
@[r:real]
+int
a0ir@[i:intrl(r)]
t1:=intabsd(a0,intrlex(r,a0,a0ir,i)):intd(absd(a0))
t2:=intrlin(abs(r),absd(a0),aica,t1):intrl(abs(r))
-int
[i:intrl(r)]
intabs:=realapp1(r,intrl(abs(r)),[x:dif][t:inn(x,class(r))]t2".int"(r,x,t,i)):intrl(abs(r))
+*int
i@t3:=intm0d(a0,intrlex(r,a0,a0ir,i)):intd(m0d(a0))
t4:=intrlin(m0(r),m0d(a0),micm0,t3):intrl(m0(r))
-int
i@intm0:=realapp1(r,intrl(m0(r)),[x:dif][t:inn(x,class(r))]t4".int"(r,x,t,i)):intrl(m0(r))
+*int
b1is@[i:intrl(r)][j:intrl(s)]
t5:=intpd(a1,b1,intrlex(r,a1,a1ir,i),intrlex(s,b1,b1is,j)):intd(pd(a1,b1))
t6:=intrlin(pl(r,s),pd(a1,b1),picp,t5):intrl(pl(r,s))
-int
i@[s:real][j:intrl(s)]
intpl:=realapp2(r,s,intrl(pl(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t6".int"(r,s,x,y,t,u,i,j)):intrl(pl(r,s))
intmn:=intpl(r,i,m0(s),intm0(s,j)):intrl(mn(r,s))
+*int
j@t7:=inttd(a1,b1,intrlex(r,a1,a1ir,i),intrlex(s,b1,b1is,j)):intd(td(a1,b1))
t8:=intrlin(ts(r,s),td(a1,b1),tict,t7):intrl(ts(r,s))
-int
j@intts:=realapp2(r,s,intrl(ts(r,s)),[x:dif][y:dif][t:inn(x,class(r))][u:inn(y,class(s))]t8".int"(r,s,x,y,t,u,i,j)):intrl(ts(r,s))
r@[n:natrl(r)]
+ivr24
t1:=satz24a(ntofrl(r,n)):lessis"n"(1,ntofrl(r,n))
t2:=th3"l.imp"(more(1rl,r),more"n"(1,ntofrl(r,n)),satz10d(1,ntofrl(r,n),t1),[t:more(1rl,r)]lemmaiva5(1,ntofrl(r,n),ismore2(r,rlofnt(ntofrl(r,n)),1rl,isrlnt1(r,n),t))):not(more(1rl,r))
-ivr24
satzr24:=satz167e(1rl,r,t2".ivr24"):lessis(1rl,r)
r@[i:intrl(r)][s:real][j:intrl(s)][l:less(r,s)]
+ivr25
t1:=satz182d(s,r,lemma2(r,s,l)):pos(mn(s,r))
t2:=intmn(s,j,r,i):intrl(mn(s,r))
t3:=posintnatrl(mn(s,r),t1,t2):natrl(mn(s,r))
t4:=satzr24(mn(s,r),t3):lessis(1rl,mn(s,r))
t5:=th9"l.or"(less(1rl,mn(s,r)),is(1rl,mn(s,r)),less(pl(1rl,r),pl(mn(s,r),r)),is(pl(1rl,r),pl(mn(s,r),r)),t4,[t:less(1rl,mn(s,r))]satz188f(1rl,mn(s,r),r,t),[t:is(1rl,mn(s,r))]ispl1(1rl,mn(s,r),r,t)):lessis(pl(1rl,r),pl(mn(s,r),r))
-ivr25
satzr25:=islessis12(pl(1rl,r),pl(r,1rl),pl(mn(s,r),r),s,compl(1rl,r),plmn(s,r),t5".ivr25"):lessis(pl(r,1rl),s)
@[x:nat][y:nat]
+ivr155
t1:=lemmaiva1(rpofnt(x),rpofnt(y)):is(pl(rlofnt(x),rlofnt(y)),pofrp(pl"rp"(rpofnt(x),rpofnt(y))))
t2:=isrpep(rpofnt(pl"n"(x,y)),pl"rp"(rpofnt(x),rpofnt(y)),satz155e(x,y)):is(rlofnt(pl"n"(x,y)),pofrp(pl"rp"(rpofnt(x),rpofnt(y))))
-ivr155
satzr155a:=tris2(real,rlofnt(pl"n"(x,y)),pl(rlofnt(x),rlofnt(y)),pofrp(pl"rp"(rpofnt(x),rpofnt(y))),t2".ivr155",t1".ivr155"):is(rlofnt(pl"n"(x,y)),pl(rlofnt(x),rlofnt(y)))
satzr155b:=symis(real,rlofnt(pl"n"(x,y)),pl(rlofnt(x),rlofnt(y)),satzr155a):is(pl(rlofnt(x),rlofnt(y)),rlofnt(pl"n"(x,y)))
+*ivr155
y@t3:=lemmaiva2(rpofnt(x),rpofnt(y)):is(ts(rlofnt(x),rlofnt(y)),pofrp(ts"rp"(rpofnt(x),rpofnt(y))))
t4:=isrpep(rpofnt(ts"n"(x,y)),ts"rp"(rpofnt(x),rpofnt(y)),satz155f(x,y)):is(rlofnt(ts"n"(x,y)),pofrp(ts"rp"(rpofnt(x),rpofnt(y))))
-ivr155
y@satzr155c:=tris2(real,rlofnt(ts"n"(x,y)),ts(rlofnt(x),rlofnt(y)),pofrp(ts"rp"(rpofnt(x),rpofnt(y))),t4".ivr155",t3".ivr155"):is(rlofnt(ts"n"(x,y)),ts(rlofnt(x),rlofnt(y)))
satzr155d:=symis(real,rlofnt(ts"n"(x,y)),ts(rlofnt(x),rlofnt(y)),satzr155c):is(ts(rlofnt(x),rlofnt(y)),rlofnt(ts"n"(x,y)))
@[t:real][r:real][s:real][a:and(not(neg(r)),is(ts(r,r),t))][b:and(not(neg(s)),is(ts(s,s),t))]
+7r161
[c0:dif][cit:inn(c0,class(t))][a0:dif][air:inn(a0,class(r))][b0:dif][bis:inn(b0,class(s))]
t1:=th3"l.imp"(negd(a0),neg(r),ande1(not(neg(r)),is(ts(r,r),t),a),[u:negd(a0)]negin(r,a0,air,u)):not(negd(a0))
t2:=th3"l.imp"(negd(b0),neg(s),ande1(not(neg(s)),is(ts(s,s),t),b),[u:negd(b0)]negin(s,b0,bis,u)):not(negd(b0))
t3:=isex(ts(r,r),t,td(a0,a0),c0,tict(r,r,a0,a0,air,air),cit,ande2(not(neg(r)),is(ts(r,r),t),a)):eq"rp"(td(a0,a0),c0)
t4:=isex(ts(s,s),t,td(b0,b0),c0,tict(s,s,b0,b0,bis,bis),cit,ande2(not(neg(s)),is(ts(s,s),t),b)):eq"rp"(td(b0,b0),c0)
t5:=isin(r,s,a0,b0,air,bis,satzd161b(c0,a0,b0,t1,t2,t3,t4)):is(r,s)
-7r161
satzr161b:=realapp3(t,r,s,is(r,s),[x:dif][y:dif][z:dif][u:inn(x,class(t))][v:inn(y,class(r))][w:inn(z,class(s))]t5".7r161"(x,u,y,v,z,w)):is(r,s)
t@[n:not(neg(t))]
+*7r161
n@[c0:dif][cit:inn(c0,class(t))]
t6:=th3"l.imp"(negd(c0),neg(t),n,[u:negd(c0)]negin(t,c0,cit,u)):not(negd(c0))
[a0:dif][a:and(not(negd(a0)),eq"rp"(td(a0,a0),c0))]
ar:=realof(a0):real
t7:=th3"l.imp"(neg(ar),negd(a0),ande1(not(negd(a0)),eq"rp"(td(a0,a0),c0),a),[u:neg(ar)]negex(ar,a0,innclass(a0),u)):not(neg(ar))
t8:=isin(ts(ar,ar),t,td(a0,a0),c0,tict(ar,ar,a0,a0,innclass(a0),innclass(a0)),cit,ande2(not(negd(a0)),eq"rp"(td(a0,a0),c0),a)):is(ts(ar,ar),t)
t9:=andi(not(neg(ar)),is(ts(ar,ar),t),t7,t8):and(not(neg(ar)),is(ts(ar,ar),t))
t10:=somei(real,[u:real]and(not(neg(u)),is(ts(u,u),t)),ar,t9):some([u:real]and(not(neg(u)),is(ts(u,u),t)))
cit@t11:=someapp(dif,[x:dif]and(not(negd(x)),eq"rp"(td(x,x),c0)),satzd161a(c0,t6),some([u:real]and(not(neg(u)),is(ts(u,u),t))),[x:dif][v:and(not(negd(x)),eq"rp"(td(x,x),c0))]t10(x,v)):some([u:real]and(not(neg(u)),is(ts(u,u),t)))
-7r161
n@satzr161a:=realapp1(t,some([u:real]and(not(neg(u)),is(ts(u,u),t))),[x:dif][v:inn(x,class(t))]t11".7r161"(x,v)):some([u:real]and(not(neg(u)),is(ts(u,u),t)))
satzr161:=onei(real,[u:real]and(not(neg(u)),is(ts(u,u),t)),[u:real][v:real][a:and(not(neg(u)),is(ts(u,u),t))][b:and(not(neg(v)),is(ts(v,v),t))]satzr161b(u,v,a,b),satzr161a):one([u:real]and(not(neg(u)),is(ts(u,u),t)))
sqrt:=ind(real,[u:real]and(not(neg(u)),is(ts(u,u),t)),satzr161):real
+*7r161
n@t12:=oneax(real,[u:real]and(not(neg(u)),is(ts(u,u),t)),satzr161):and(not(neg(sqrt)),is(ts(sqrt,sqrt),t))
-7r161
n@thsqrt1a:=ande1(not(neg(sqrt)),is(ts(sqrt,sqrt),t),t12".7r161"):not(neg(sqrt))
thsqrt1b:=ande2(not(neg(sqrt)),is(ts(sqrt,sqrt),t),t12".7r161"):is(ts(sqrt,sqrt),t)
[x:real][o:not(neg(x))][i:is(ts(x,x),t)]
thsqrt2:=satzr161b(x,sqrt,andi(not(neg(x)),is(ts(x,x),t),o,i),t12".7r161"):is(x,sqrt(t,n))
o@[i:is(t,ts(x,x))]
thsqrt3:=symis(real,x,sqrt(t,n),thsqrt2(symis(real,t,ts(x,x),i))):is(sqrt(t,n),x)
@[r:real][s:real][n:not(neg(r))][o:not(neg(s))][i:is(r,s)]
issqrt:=thsqrt2(s,o,sqrt(r,n),thsqrt1a(r,n),tris(real,ts(sqrt(r,n),sqrt(r,n)),r,s,thsqrt1b(r,n),i)):is(sqrt(r,n),sqrt(s,o))
r@[n:not(neg(r))][i:is(r,0)]
sqrt0:=thsqrt3(r,n,0,0notn(0,refis(real,0)),tris2(real,r,ts(0,0),0,i,ts01(0,0,refis(real,0)))):is(sqrt(r,n),0)
n@[o:nis(r,0)]
+sqrt
t1:=th3"l.imp"(is(sqrt(r,n),0),is(r,0),o,[t:is(sqrt(r,n),0)]tris1(real,r,0,ts(sqrt(r,n),sqrt(r,n)),thsqrt1b(r,n),ts01(sqrt(r,n),sqrt(r,n),t))):nis(sqrt(r,n),0)
-sqrt
sqrtnot0:=or3e2(is(sqrt(r,n),0),pos(sqrt(r,n)),neg(sqrt(r,n)),axrlo(sqrt(r,n)),thsqrt1a(r,n),t1".sqrt"):pos(sqrt(r,n))
